Active Chocolate Hippo

High

# Invalid token amount calculations leads to DoS in `Launch::updateParticipation(...)`

### Summary

The `Launch.sol` contract allows users to create, update, and cancel participations in different token launching initiatives. Users pay `X amount` of a payment currency to later receive `Y amount` of tokens. Each of the actions users can do with participations are strictly controlled by the ROVA backend, and each user request is signed on the backend. Currently, there seems to be some inconsistency in how token updates are handled in the `updateParticipation(...)` function:

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L351-L377

```solidity
if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
@>      if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) { // @audit - tokenAmoun is compared to currency amount, where both have different decimals
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
@>       userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount); // @audit - `userTokens` is incorrectly set to a mixed value of tokenAmounts and currencyAmounts
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
@>      if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) { // @audit - tokenAmoun is compared to currency amount, where both have different decimals
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
@>      userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);  // @audit - `userTokens` is incorrectly set to a mixed value of tokenAmounts and currencyAmounts
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }
```

From the above, it can be seen that the contract incorrectly adds/subtracts `tokenAmount` and `currencyAmount`, where both will very often have different decimals. These comparisons will most often lead to unwanted reverts causing DoS for users, but in some rare cases, when the comparison checks pass, it could lead to users being set more tokens in the `userTokens` mapping, leading to invalid calculations afterward.

### Root Cause

Arithmetic operations carried out between `tokenAmount` and `currencyAmount` in `Launch::updateParticipation(...)`, which have different decimals, causing reverts (which will DoS the update functionality) and `tokenAmount` miss-calculations for users.

### Internal Pre-conditions

1. A token launch group is created for a token with 6 decimals, where the launch group accepts payments in the ETH Mainnet MOVE token, which has 8 decimals. The launch group allows for updates and cancelations and has a limit of 500 tokens per user.
2. Alice creates a participation in this launch group wanting to buy 100 tokens.
3. Her `currencyAmount` will be: `currencyAmount = (tokenPriceBps * tokenAmount) / 10 ** tokenDecimals = (Xe8 * 100e6) / 1e6 = (X * 100)e8` (In this formula `tokenPriceBps` will have the decimals of the currency token (i.e `e8`), `tokenAmount` will be in token decimal format (i.e. `e6`)

### External Pre-conditions

N/A

### Attack Path

1. Alice wants to update her participation and increase her token amount to 150 tokens (which is in the limit).
2. In `updateParticipation(...)`, the new currency amount will be calculated to be more than in the previous participation - `newCurrencyAmount = (tokenPriceBps * tokenAmount) / 10 ** tokenDecimals = (Xe8 * Ye6) / 1e6 = (X * 150)e8`. In this formula `tokenPriceBps` will have the decimals of the currency token (i.e `e8`), `tokenAmount` will be in token decimal format (i.e. `e6`).
3. This will invoke the `else if` statement in `updateParticipation(...)`, where we will have `if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser)`.
4. The above will try to add `Xe6 + Ye8` and compare it to `settings.maxTokenAmountPerUser`, which will be in token decimals (i.e `e6`).
5. Depending on the launch group settings, this comparison will most often rule out that the updated amount is higher than the allowed maximum causing a revert for the user.
6. In cases where the maximum allowed tokens are set to a high number, and the `if` statements do not revert, then the user will have his/her `userTokens` set to a higher number than intended, which will then mess up calculations in other contract actions.
6. When the token amount is decreased, then the `if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser)` statement will revert with an underflow, again DoS-ing the function.


### Impact

The `Launch::updateParticipation(...)` function will be DoS-ed in most cases, and lead to `userToken` amount miss-calculations in others.

### PoC

N/A

### Mitigation

Apply the following changes:

```diff
@@ -352,26 +352,26 @@ contract Launch is
             // Calculate refund amount
             uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
             // Validate user new requested token amount is greater than min token amount per user
-            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
+            if (userTokenAmount - request.tokenAmount < settings.minTokenAmountPerUser) {
                 revert MinUserTokenAllocationNotReached(
                     request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                 );
             }
             // Update total tokens requested for user for launch group
-            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
+            userTokens.set(request.userId, userTokenAmount - request.tokenAmount);
             // Transfer payment currency from contract to user
             IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
         } else if (newCurrencyAmount > prevInfo.currencyAmount) {
             // Calculate additional payment amount
             uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
             // Validate user new requested token amount is within launch group user allocation limits
-            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
+            if (userTokenAmount + request.tokenAmount > settings.maxTokenAmountPerUser) {
                 revert MaxUserTokenAllocationReached(
                     request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                 );
             }
             // Update total tokens requested for user for launch group
-            userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
+            userTokens.set(request.userId, userTokenAmount + request.tokenAmount);
             // Transfer payment currency from user to contract
             IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
         }
```