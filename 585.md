Shallow Coral Griffin

Medium

# Calculation error for checking min, max in updateParticipation

### Summary

The calculation for checking `minTokenAmountPerUser` and `maxTokenAmountPerUser` in `updateParticipation` is incorrect. As a result, `minTokenAmountPerUser` and `maxTokenAmountPerUser` check can be bypassed in `updateParticipation`.


### Root Cause


[https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L355](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L355)

[https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L368](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L368)

In `updateParticipation`, the function refunds remaining payment tokens or accepts additional payments while updating the launch token amount. However, the calculations for checking `minTokenAmountPerUser` and `maxTokenAmountPerUser` are incorrect. The variable `userTokenAmount` represents the amount of launch tokens, while `refundCurrencyAmount` and `additionalCurrencyAmount` represent payment tokens.

```solidity
function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
    external
    nonReentrant
    whenNotPaused
    onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
{
    ...
    // If new requested token amount is less than old amount, handle refund
    if (prevInfo.currencyAmount > newCurrencyAmount) {
        // Calculate refund amount
@>      uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
        // Validate user new requested token amount is greater than min token amount per user
@>      if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
            revert MinUserTokenAllocationNotReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }
        ...
    } else if (newCurrencyAmount > prevInfo.currencyAmount) {
        // Calculate additional payment amount
@>      uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
        // Validate user new requested token amount is within launch group user allocation limits
@>      if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
            revert MaxUserTokenAllocationReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }
    }
    ...
}
```


### Internal Pre-conditions

1. User calls `participate` to join the launch group
2. The difference becomes more significant when the decimals of launch token and payment token are different or when there's a large price difference, especially when `tokenPriceBps` changes


### External Pre-conditions

None

### Attack Path

1. User calls `updateParticipation` to adjust the requested amount.


### Impact

The checks for `maxTokenAmountPerUser` and `minTokenAmountPerUser` can be bypassed in this `updateParticipation` call.


### PoC

None

### Mitigation

Use `userTokenAmount - prevInfo.tokenAmount + request.tokenAmount` to compare

```diff
function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
    external
    nonReentrant
    whenNotPaused
    onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
{
    ...
    // If new requested token amount is less than old amount, handle refund
    if (prevInfo.currencyAmount > newCurrencyAmount) {
        // Calculate refund amount
        uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
        // Validate user new requested token amount is greater than min token amount per user
-       if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
+       if (userTokenAmount - prevInfo.tokenAmount + request.tokenAmount < settings.minTokenAmountPerUser) {
            revert MinUserTokenAllocationNotReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }
        ...
    } else if (newCurrencyAmount > prevInfo.currencyAmount) {
        // Calculate additional payment amount
        uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
        // Validate user new requested token amount is within launch group user allocation limits
-       if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
+       if (userTokenAmount - prevInfo.tokenAmount + request.tokenAmount > settings.maxTokenAmountPerUser) {
            revert MaxUserTokenAllocationReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }
    }
    ...
}
```