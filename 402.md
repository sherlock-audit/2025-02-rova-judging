Wonderful Watermelon Raven

High

# Incorrect Scaling in `_calculateCurrencyAmount` Leads to Payment Miscalculations

### Summary
The `_calculateCurrencyAmount` function incorrectly considers only the project token's decimals (PTD) but does not account for the payment currency's decimals (CTD). This leads to severe miscalculations in payment amounts, potentially causing users to overpay or underpay significantly.

### Root Cause
The README states that each launch group can accept multiple payment currencies, and users specify the currency they want to use when participating. However, the [_calculateCurrencyAmount](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L596-L598) function mistakenly uses PTD (project token decimals) instead of CTD (currency token decimals) for scaling, leading to incorrect currency amount calculations.
```solidity
  function _calculateCurrencyAmount(uint256 tokenPriceBps, uint256 tokenAmount) internal view returns (uint256) {
      
      return Math.mulDiv(tokenPriceBps, tokenAmount, 10 ** tokenDecimals);
  }
```
The function divides by 10 ** tokenDecimals (PTD) instead of 10 ** currencyDecimals (CTD), causing incorrect scaling when tokens have different decimals (e.g., USDC with 6 decimals vs. MOVE with 8 decimals), which leads to undercharging or overcharging users depending on the decimals mismatch.

### POC
<details> 
<summary>Click to view POC</summary>

```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {UnsafeUpgrades} from "@openzeppelin-foundry-upgrades/Upgrades.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {VmSafe} from "forge-std/Vm.sol";
import {Test} from "forge-std/Test.sol";
import {Launch} from "../src/Launch.sol";
import {
    CurrencyConfig,
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    ParticipationInfo
} from "../src/Types.sol";
import "forge-std/console.sol";

contract Token is ERC20 {
    constructor(uint8 decimals) ERC20("USDC", "USDC") {
        _mint(msg.sender, 1000000 * 10 ** decimals);
    }
}

contract MyTest is Test , Launch{
    Launch public launch;
    Token public usdc;

    VmSafe.Wallet public admin = vm.createWallet("admin");
    address public manager = address(1);
    address public operator = address(2);
    address public signer = address(3);
    address public testWithdrawalAddress = address(4);
    address public user1 = address(5);

    // Dummy cuids for testing
    bytes32 public testLaunchId = "cixf02ym000001b66m45ae4k8";
    bytes32 public testLaunchGroupId = "ch72gsb320000udocl363eofy";
    bytes32 public testLaunchParticipationId = "cm6o2sldi00003b74facm5z9n";
    bytes32 public testUserId = "cm6o2tm1300003b74dsss1s7q";
 function setUp() public {
    vm.startPrank(admin.addr);

    // Deploy contracts
    address proxyAddress = UnsafeUpgrades.deployTransparentProxy(
        address(new Launch()),
        admin.addr,
        abi.encodeWithSelector(Launch.initialize.selector, testWithdrawalAddress, testLaunchId, admin.addr, 8) // Project token with 8 decimals
    );
    launch = Launch(proxyAddress);

    // Deploy USDC token (6 decimals)
    usdc = new Token(6);

    // Setup roles
    launch.grantRole(MANAGER_ROLE, manager);
    launch.grantRole(OPERATOR_ROLE, operator);
    launch.grantRole(SIGNER_ROLE, signer);

    // Fund users with USDC (2000 USDC)
    usdc.transfer(user1, 2000 * 10 ** 6);
    vm.stopPrank();

    LaunchGroupSettings memory settings = _setupLaunchGroupWithStatus(testLaunchGroupId, LaunchGroupStatus.PENDING);
    settings.status = LaunchGroupStatus.ACTIVE;
    settings.finalizesAtParticipation = true;

    vm.startPrank(manager);
    launch.setLaunchGroupSettings(testLaunchGroupId, settings);
    vm.stopPrank();
 }

 function test_CurrencyAmountMiscalculation_DueToIncorrectDecimals() external {
    // Create a participation request with 1000 USDC as the token amount
    ParticipationRequest memory request = ParticipationRequest({
        chainId: block.chainid,
        launchId: testLaunchId,
        launchGroupId: testLaunchGroupId,
        launchParticipationId: testLaunchParticipationId,
        userId: testUserId,
        userAddress: user1,
        tokenAmount: 1000 * 10 ** 6, // 1000 USDC (6 decimals)
        currency: address(usdc),
        requestExpiresAt: block.timestamp + 1 hours
    });

    bytes memory signature = _signRequest(abi.encode(request));

    vm.startPrank(user1);
    usdc.approve(address(launch), type(uint256).max);
    launch.participate(request, signature);
    vm.stopPrank();

    // Mark launch group as completed
    vm.startPrank(manager);
    launch.setLaunchGroupStatus(testLaunchGroupId, LaunchGroupStatus.COMPLETED);
    vm.stopPrank();

    ParticipationInfo memory info = launch.getParticipationInfo(request.launchParticipationId);

    uint256 tokenFunded = info.tokenAmount;
    console.log("Token Amount Funded:", tokenFunded);

    uint256 withdrawableAmount = launch.getWithdrawableAmountByCurrency(address(usdc));
    console.log("Withdrawable Amount in USDC:", withdrawableAmount);

    // Check if the withdrawable amount matches the funded token amount
    assertTrue(tokenFunded != withdrawableAmount, "Mismatch in withdrawable amount due to decimal miscalculation");
 }

    function _signRequest(bytes memory encodedRequest) internal view returns (bytes memory) {
        bytes32 structHash = keccak256(encodedRequest);
        bytes32 messageHash = keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", structHash));
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(admin.privateKey, messageHash);
        return abi.encodePacked(r, s, v);
    }

    function _setupLaunchGroupWithStatus(bytes32 launchGroupId, LaunchGroupStatus status)
        internal
        returns (LaunchGroupSettings memory)
    {
        CurrencyConfig memory currencyConfig =
            CurrencyConfig({tokenPriceBps: 1 * 10 ** 6, isEnabled: true});
        LaunchGroupSettings memory settings = LaunchGroupSettings({
            finalizesAtParticipation: false,
            startsAt: block.timestamp,
            endsAt: block.timestamp + 1 days,
            maxTokenAllocation: 10000 * 10 ** 6,
            minTokenAmountPerUser: 500 * 10 ** 6,
            maxTokenAmountPerUser: 3000 * 10 ** 6,
            status: status
        });
        vm.startPrank(manager);
        launch.createLaunchGroup(launchGroupId, address(usdc), currencyConfig, settings);
        vm.stopPrank();
        return settings;
    }

}
```
- Create a new test file in the [test folder](https://github.com/sherlock-audit/2025-02-rova/tree/main/rova-contracts/test).
- Copy and paste the PoC code.
- Run the following command:
```solidity
forge test --mt test_CurrencyAmountMiscalculation_DueToIncorrectDecimals -vvv
```
</details>


### Impact

The miscalculation in the `_calculateCurrencyAmount` function leads to a violation of the invariant:
> The total amount of payment currency tokens that can be withdrawn should equal the total amount of payment currency tokens funded. 


### Mitigation

The `_calculateCurrencyAmount` function should always use 10^CTD (currency token decimals) for scaling, instead of 10^PTD.