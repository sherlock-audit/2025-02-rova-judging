Mini Brick Mammoth

Medium

# [M-1] Confusion between currency amount and token amount in Launch.sol#updateParticipation() leads to incorrect calculation userTokens

#### Severity: Medium

    Impact: Medium

    Likelihood: High



## Description

A function `updateParticipation()` receives an argument `request` that contains such information as `userId`, `currency`, `tokenAmount` and others. Then it calculates value `newCurrencyAmount` with help `tokenPriceBps`. And then based on whether the obtained value greater or less than the previous one, the function make decision to receive additional currency amount or to refund an excess.

There is also EnumerableMap `userTokens` that contains information about total amount of tokens for `request.userId` for each launch group. This is used to make sure users are within the min/max token per user allocation for each launch group. Calculating the updated value of this parameter is incorrect. Instead of to use the `request.tokenAmount` it uses `refundCurrencyAmount` or `additionalCurrencyAmount`.



https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L351-L377

```solidity
if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) { //<==
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            //@audit token amount and currency amount discrepency
            // Update total tokens requested for user for launch group
            //userTokens.set(request.userId, userTokenAmount - request.tokenAmount);
            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount); //<==
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) { //<==
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount); //<==
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }
```



## Impact

Due to incorrect calculation of the `userTokens` there is a possibility to bypass a test on `minTokenAmountPerUser` and `maxTokenAmountPerUser` and break established rules. Also, incorrect calculation in `Launch.sol#updateParticipation()` can lead to an unexpected revert.



## Proof of Concepts

First, I changed `tokenPriceBps` in `test/LaunchTestBase.t.sol#_setupLaunchGroupWithStatus` and increased initial funds for user1 in `test/LaunchTestBase.t.sol#_setUpLaunch`.

```solidity
        // Fund users
-        currency.transfer(user1, 2000 * 10 ** launch.tokenDecimals());
+        currency.transfer(user1, 4000 * 10 ** launch.tokenDecimals());
        currency.transfer(user2, 1000 * 10 ** launch.tokenDecimals());
        vm.stopPrank();
    }
.....
function _setupLaunchGroupWithStatus(bytes32 launchGroupId, LaunchGroupStatus status)
        internal
        returns (LaunchGroupSettings memory)
    {
-        CurrencyConfig memory currencyConfig =
-            CurrencyConfig({tokenPriceBps: 1 * 10 ** currency.decimals(), isEnabled: true});
+        CurrencyConfig memory currencyConfig =
+            CurrencyConfig({tokenPriceBps: 4 * 10 ** currency.decimals(), isEnabled: true});

```

Then we will try to execute `test/Launch.UpdateParticipation.t.sol#test_UpdateParticipation_IncreaseAmount` test. It will, first, participate with 1000e18 token amount and then try to update with 2000e18 token amount. It does not exceed maxTokenAmountPerUser, but test will fail.

```bash
> forge test --mt test_UpdateParticipation_IncreaseAmount -vvv
[⠆] Compiling...
[⠔] Compiling 1 files with Solc 0.8.22
[⠒] Solc 0.8.22 finished in 37.12s
Compiler run successful!

Ran 1 test for test/Launch.UpdateParticipation.t.sol:LaunchUpdateParticipationTest
[FAIL: log != expected log] test_UpdateParticipation_IncreaseAmount() (gas: 120316)
Traces:
  [120316] LaunchUpdateParticipationTest::test_UpdateParticipation_IncreaseAmount()
    ├─ [7276] TransparentUpgradeableProxy::fallback() [staticcall]
    │   ├─ [2444] Launch::tokenDecimals() [delegatecall]
    │   │   └─ ← [Return] 18
    │   └─ ← [Return] 18
    ├─ [0] VM::sign("<pk>", 0xb443999af8db04216442ff4cdc2b2934eda602365503bc3c4aa89224b62552a6) [staticcall]
    │   └─ ← [Return] 27, 0x57cf582e21dbc23c40394edf3f05b37f7742a92a36ed82c1adc704b62c4ebd9e, 0x79f56aec4ca3e4dc2fc6cf373321937de47a57453f40812e24e78bc7a627b5f3
    ├─ [0] VM::startPrank(ModExp: [0x0000000000000000000000000000000000000005])
    │   └─ ← [Return]
    ├─ [6001] TransparentUpgradeableProxy::fallback(0x6368373267736233323030303075646f636c333633656f667900000000000000, MockERC20: [0xDDd9A038D57372934f1b9c52bd8621F5ED4268DF]) [staticcall]
    │   ├─ [5660] Launch::getLaunchGroupCurrencyConfig(0x6368373267736233323030303075646f636c333633656f667900000000000000, MockERC20: [0xDDd9A038D57372934f1b9c52bd8621F5ED4268DF]) [delegatecall]
    │   │   └─ ← [Return] CurrencyConfig({ tokenPriceBps: 4000000000000000000 [4e18], isEnabled: true })
    │   └─ ← [Return] CurrencyConfig({ tokenPriceBps: 4000000000000000000 [4e18], isEnabled: true })
    ├─ [776] TransparentUpgradeableProxy::fallback() [staticcall]
    │   ├─ [444] Launch::tokenDecimals() [delegatecall]
    │   │   └─ ← [Return] 18
    │   └─ ← [Return] 18
    ├─ [24339] MockERC20::approve(TransparentUpgradeableProxy: [0x6D9da78B6A5BEdcA287AA5d49613bA36b90c15C4], 8000000000000000000000 [8e21])
    │   ├─ emit Approval(owner: ModExp: [0x0000000000000000000000000000000000000005], spender: TransparentUpgradeableProxy: [0x6D9da78B6A5BEdcA287AA5d49613bA36b90c15C4], value: 8000000000000000000000 [8e21])
    │   └─ ← [Return] true
    ├─ [0] VM::expectEmit()
    │   └─ ← [Return]
    ├─ emit ParticipationUpdated(launchGroupId: 0x6368373267736233323030303075646f636c333633656f667900000000000000, launchParticipationId: 0x6e65774c61756e636850617274696369706174696f6e49640000000000000000, userId: 0x636d366f32746d31333030303033623734647373733173377100000000000000, user: ModExp: [0x0000000000000000000000000000000000000005], currencyAmount: 2000000000000000000000 [2e21], currency: MockERC20: [0xDDd9A038D57372934f1b9c52bd8621F5ED4268DF])
    ├─ [45496] TransparentUpgradeableProxy::fallback(UpdateParticipationRequest({ chainId: 31337 [3.133e4], launchId: 0x636978663032796d3030303030316236366d34356165346b3800000000000000, launchGroupId: 0x6368373267736233323030303075646f636c333633656f667900000000000000, prevLaunchParticipationId: 0x636d366f32736c646930303030336237346661636d357a396e00000000000000, newLaunchParticipationId: 0x6e65774c61756e636850617274696369706174696f6e49640000000000000000, userId: 0x636d366f32746d31333030303033623734647373733173377100000000000000, userAddress: 0x0000000000000000000000000000000000000005, tokenAmount: 2000000000000000000000 [2e21], currency: 0xDDd9A038D57372934f1b9c52bd8621F5ED4268DF, requestExpiresAt: 3601 }), 0x57cf582e21dbc23c40394edf3f05b37f7742a92a36ed82c1adc704b62c4ebd9e79f56aec4ca3e4dc2fc6cf373321937de47a57453f40812e24e78bc7a627b5f31b)
    │   ├─ [45067] Launch::updateParticipation(UpdateParticipationRequest({ chainId: 31337 [3.133e4], launchId: 0x636978663032796d3030303030316236366d34356165346b3800000000000000, launchGroupId: 0x6368373267736233323030303075646f636c333633656f667900000000000000, prevLaunchParticipationId: 0x636d366f32736c646930303030336237346661636d357a396e00000000000000, newLaunchParticipationId: 0x6e65774c61756e636850617274696369706174696f6e49640000000000000000, userId: 0x636d366f32746d31333030303033623734647373733173377100000000000000, userAddress: 0x0000000000000000000000000000000000000005, tokenAmount: 2000000000000000000000 [2e21], currency: 0xDDd9A038D57372934f1b9c52bd8621F5ED4268DF, requestExpiresAt: 3601 }), 0x57cf582e21dbc23c40394edf3f05b37f7742a92a36ed82c1adc704b62c4ebd9e79f56aec4ca3e4dc2fc6cf373321937de47a57453f40812e24e78bc7a627b5f31b) [delegatecall]
    │   │   ├─ [3000] PRECOMPILES::ecrecover(0xb443999af8db04216442ff4cdc2b2934eda602365503bc3c4aa89224b62552a6, 27, 39717563766555074727002847350302577972567387586476650957924784077367281630622, 55163470165177266642761727873962424184122923153577014635911366430614371153395) [staticcall]
    │   │   │   └─ ← [Return] 0x000000000000000000000000aa10a84ce7d9ae517a52c6d5ca153b369af99ecf
    │   │   └─ ← [Revert] MaxUserTokenAllocationReached(0x6368373267736233323030303075646f636c333633656f667900000000000000, 0x636d366f32746d31333030303033623734647373733173377100000000000000, 1000000000000000000000 [1e21], 2000000000000000000000 [2e21])
    │   └─ ← [Revert] MaxUserTokenAllocationReached(0x6368373267736233323030303075646f636c333633656f667900000000000000, 0x636d366f32746d31333030303033623734647373733173377100000000000000, 1000000000000000000000 [1e21], 2000000000000000000000 [2e21])
    └─ ← [Revert] log != expected log

Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 4.94ms (973.98µs CPU time)

Ran 1 test suite in 775.29ms (4.94ms CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)

Failing tests:
Encountered 1 failing test in test/Launch.UpdateParticipation.t.sol:LaunchUpdateParticipationTest
[FAIL: log != expected log] test_UpdateParticipation_IncreaseAmount() (gas: 120316)

Encountered a total of 1 failing tests, 0 tests succeeded


```



And now set `tokenAmount` in `_createParticipationRequest#test/LaunchTestBase.t.sol` to 500.

```solidity
    function _createParticipationRequest() internal view returns (ParticipationRequest memory) {
        return ParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: testLaunchParticipationId,
            userId: testUserId,
            userAddress: user1,
+            tokenAmount: 500 * 10 ** launch.tokenDecimals(),
-            tokenAmount: 1000 * 10 ** launch.tokenDecimals(),
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }
```

And make some modifications in `test_UpdateParticipation_IncreaseAmount#test/Launch.UpdateParticipation.t.sol`

```solidity
    function test_UpdateParticipation_IncreaseAmount() public {
        // Prepare update participation request
        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(1000);
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.startPrank(user1);
        uint256 updatedCurrencyAmount =
            _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, updateRequest.tokenAmount);
        currency.approve(address(launch), updatedCurrencyAmount);

        // Expect ParticipationUpdated event
        vm.expectEmit();
        emit ParticipationUpdated(
            updateRequest.launchGroupId,
            updateRequest.newLaunchParticipationId,
            testUserId,
            user1,
            updateRequest.tokenAmount,
            address(currency)
        );

        uint256 beforeTokenAmount = launch.getUserTokensByLaunchGroup(updateRequest.launchGroupId, testUserId);
        console.log("UserTokens before update %e", beforeTokenAmount);

        // Update participation
        launch.updateParticipation(updateRequest, updateSignature);

        console.log(
            "UserTokens after update %e", launch.getUserTokensByLaunchGroup(updateRequest.launchGroupId, testUserId)
        );

        console.log(" but expected %e", (beforeTokenAmount + 1000 * 10 ** launch.tokenDecimals()));

        // Verify update
        ParticipationInfo memory newInfo = launch.getParticipationInfo(updateRequest.newLaunchParticipationId);
        _verifyParticipationInfo(newInfo, updateRequest);
        ParticipationInfo memory oldInfo = launch.getParticipationInfo(updateRequest.prevLaunchParticipationId);
        assertEq(oldInfo.currencyAmount, 0);
        assertEq(oldInfo.tokenAmount, 0);

        // Verify total unique participants by launch group
        assertEq(launch.getNumUniqueParticipantsByLaunchGroup(testLaunchGroupId), 1);

        vm.stopPrank();
    }
```

And we get an output

```bash
> forge test --mt test_UpdateParticipation_IncreaseAmount -vvv
[⠆] Compiling...
No files changed, compilation skipped

Ran 1 test for test/Launch.UpdateParticipation.t.sol:LaunchUpdateParticipationTest
[PASS] test_UpdateParticipation_IncreaseAmount() (gas: 272793)
Logs:
  UserTokens before update 5e20
  UserTokens after update 2.5e21
   but expected 1.5e21

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 5.77ms (1.33ms CPU time)

Ran 1 test suite in 13.08ms (5.77ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)

```



## Recommended mitigation

```solidity
if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
+            if (userTokenAmount - request.tokenAmount < settings.minTokenAmountPerUser) {
-            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
+            userTokens.set(request.userId, userTokenAmount - request.tokenAmount);
-            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
+            if (userTokenAmount + request.tokenAmount > settings.maxTokenAmountPerUser) {
-            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
+            userTokens.set(request.userId, userTokenAmount + request.tokenAmount);
-            userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }
```


