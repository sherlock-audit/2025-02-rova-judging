Soft Basil Alpaca

High

# Incorrect Token-Currency Conversion in updateParticipation Allows Users to Receive Incorrect Token Amounts

### Summary

The `Launch::updateParticipation` function incorrectly updates `userTokenAmount` by adding/subtracting `currenyAmount` without any conversion, which results in users getting a different `tokenAmount` than what they requested.


### Root Cause

The issue arises because the `updateParticipation` ([Launch.sol#L351-L377](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L351-L377)) function directly adds/subtracts `currencyAmount` (in currency units) to/from `userTokenAmount` (in token units) without converting between the two. This is incorrect because `currencyAmount` and `tokenAmount` are not directly comparable.


**For example:**
- When calculating a refund, the contract subtracts `refundCurrencyAmount` (in currency units) from `userTokenAmount` (in token units).
- When calculating an additional payment, the contract adds `additionalCurrencyAmount` (in currency units) to `userTokenAmount` (in token units).

This mixing of units leads to incorrect calculations and inconsistent state.


```solidity

function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
       /// [....]

        // Calculate new payment amount in requested currency based on token price and requested token amount
        uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
        // Get total tokens requested for user for launch group
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        // If new requested token amount is less than old amount, handle refund
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
@>            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
@>            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
@>            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
@>            userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }

        /// [...]
    }
``` 


As a result, users end up having incorrect numbers of tokens than what they paid for. 

### Internal Pre-conditions

User must have participated in the token launch at least once. 

### External Pre-conditions

none

### Attack Path

1. User calls the `participate` function to participate in a token launch
2. User calls the `updateParticipation` to update their token allocation

### Impact

- In the case of additional token purchases from the user, the user will end up with a fraction of the tokens that he has paid for.
- In the case of a refund, the user will receive the full refund but only a fraction of the tokens will be removed from his allocation

### PoC

```solidity



// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {IERC20Errors} from "@openzeppelin/contracts/interfaces/draft-IERC6093.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {Test} from "forge-std/Test.sol";
import {console} from "forge-std/Console.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";

import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    UpdateParticipationRequest,
    ParticipationInfo,
    CurrencyConfig
} from "../src/Types.sol";

contract MockUSDT is ERC20 {
    uint8 constant DECIMALS = 6;

    constructor() ERC20("Mock USDT", "MUSDT") {
        _mint(msg.sender, 1000000 * 10 ** DECIMALS);
    }

    function decimals() public view override returns (uint8) {
        return DECIMALS;
    }
}

contract AuditTests is Test, Launch, LaunchTestBase {
    LaunchGroupSettings public settings;
    ParticipationRequest public originalParticipationRequest;
    MockUSDT mockUsdt;

    function setUp() public {
        _setUpLaunch();

        vm.startPrank(admin.addr);
        mockUsdt = new MockUSDT();
        mockUsdt.transfer(user1, 2000 * 10 ** mockUsdt.decimals());
        vm.stopPrank();

        // Setup initial participation
        settings = _setupLaunchGroupWithMockUSDT(testLaunchGroupId);
        originalParticipationRequest = _createParticipationWithMockUSDT();
        bytes memory signature = _signRequest(abi.encode(originalParticipationRequest));

        vm.startPrank(user1);
        mockUsdt.approve(
            address(launch),
            _getCurrencyAmount(
                originalParticipationRequest.launchGroupId,
                originalParticipationRequest.currency,
                originalParticipationRequest.tokenAmount
            )
        );
        launch.participate(originalParticipationRequest, signature);

        vm.stopPrank();
    }

    function _createParticipationWithMockUSDT() internal view returns (ParticipationRequest memory) {
        return ParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: testLaunchParticipationId,
            userId: testUserId,
            userAddress: user1,
            tokenAmount: 1000 * 10 ** launch.tokenDecimals(),
            currency: address(mockUsdt),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }

    function _setupLaunchGroupWithMockUSDT(bytes32 launchGroupId) internal returns (LaunchGroupSettings memory) {
        CurrencyConfig memory currencyConfig =
            CurrencyConfig({tokenPriceBps: 1 * 10 ** mockUsdt.decimals(), isEnabled: true});

        LaunchGroupSettings memory s = LaunchGroupSettings({
            finalizesAtParticipation: false,
            startsAt: block.timestamp,
            endsAt: block.timestamp + 1 days,
            maxTokenAllocation: 10000 * 10 ** launch.tokenDecimals(),
            minTokenAmountPerUser: 500 * 10 ** launch.tokenDecimals(),
            maxTokenAmountPerUser: 3000 * 10 ** launch.tokenDecimals(),
            status: LaunchGroupStatus.ACTIVE
        });
        vm.startPrank(manager);
        launch.createLaunchGroup(launchGroupId, address(mockUsdt), currencyConfig, s);
        vm.stopPrank();
        return s;
    }

    function _createUpdateParticipationRequest(uint256 newTokenAmount)
        internal
        view
        returns (UpdateParticipationRequest memory)
    {
        uint256 launchTokenDecimals = launch.tokenDecimals();
        return UpdateParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            prevLaunchParticipationId: testLaunchParticipationId,
            newLaunchParticipationId: "newLaunchParticipationId",
            userId: testUserId,
            userAddress: user1,
            tokenAmount: newTokenAmount * 10 ** launchTokenDecimals,
            currency: address(mockUsdt),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }

    function test_Should_Prove_UpdateParticipation_Miscalculation() public {
        uint256 userTokenBalanceBefore = mockUsdt.balanceOf(user1);
        uint256 userTokenAmountBefore = launch.getUserTokensByLaunchGroup(testLaunchGroupId, testUserId);

        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(2000);
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.startPrank(user1);
        uint256 updatedCurrencyAmount =
            _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, updateRequest.tokenAmount);
        mockUsdt.approve(address(launch), updatedCurrencyAmount);

        launch.updateParticipation(updateRequest, updateSignature);
        vm.stopPrank();

        uint256 userTokenBalanceAfter = mockUsdt.balanceOf(user1);

        uint256 userTokenAmountAfter = launch.getUserTokensByLaunchGroup(testLaunchGroupId, testUserId);

        console.log("");
        console.log("userTokenBalanceBefore: ", userTokenBalanceBefore);
        console.log("userTokenBalanceAfter : ", userTokenBalanceAfter);
        console.log("");
        console.log("userTokenAmountBefore : ", userTokenAmountBefore);
        console.log("userTokenAmountAfter  : ", userTokenAmountAfter);
        console.log("expected tokens      : ", updateRequest.tokenAmount);
    }
}

``` 

### Output

```shell

  userTokenBalanceBefore:  1000000000
  userTokenBalanceAfter :  0
  
  userTokenAmountBefore :  1000000000000000000000
  userTokenAmountAfter  :  1000000000001000000000
  expected tokens      :  2000000000000000000000

``` 


### Mitigation

To fix this issue, the contract should:

- Convert `refundCurrencyAmount` or `additionalCurrencyAmount` to `tokenAmount` using the `tokenPriceBps`.
- Update `userTokenAmount` with the converted `tokenAmount`.

Hereâ€™s the corrected logic:

```diff
function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE){
/// [...]

      if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
+           uint256 refundTokenAmount = Math.mulDiv(refundCurrencyAmount, 10 ** tokenDecimals, tokenPriceBps);
            // Validate user new requested token amount is greater than min token amount per user
+            if (userTokenAmount - refundTokenAmount < settings.minTokenAmountPerUser) {
-            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
+            userTokens.set(request.userId, userTokenAmount - refundTokenAmount);
-            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
+            uint256 additionalTokenAmount = Math.mulDiv(additionalCurrencyAmount, 10 ** tokenDecimals, tokenPriceBps);
            // Validate user new requested token amount is within launch group user allocation limits
+            if(userTokenAmount + additionalTokenAmount > settings.maxTokenAmountPerUser) {
-            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
+            userTokens.set(request.userId, userTokenAmount + additionalTokenAmount);
-            userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }
/// [...]
}
``` 