Precise Stone Sparrow

High

# Currency and token amounts are mixed while updating the participation

### Summary

The logic to update the currency and token amounts in a participation is flawed, leading to an incorrect assignment of tokens to users.

### Root Cause

In Launch.sol:312 the implementation of `updateParticipation()` updates the amounts of token and currency, and, depending on the direction, refunds or charges the user with the difference.

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L345-L377

```solidity
// Calculate new payment amount in requested currency based on token price and requested token amount
uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
// Get total tokens requested for user for launch group
EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
(, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
// If new requested token amount is less than old amount, handle refund
if (prevInfo.currencyAmount > newCurrencyAmount) {
    // Calculate refund amount
    uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
    // Validate user new requested token amount is greater than min token amount per user
    if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
        revert MinUserTokenAllocationNotReached(
            request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
        );
    }
    // Update total tokens requested for user for launch group
    userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
    // Transfer payment currency from contract to user
    IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
} else if (newCurrencyAmount > prevInfo.currencyAmount) {
    // Calculate additional payment amount
    uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
    // Validate user new requested token amount is within launch group user allocation limits
    if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
        revert MaxUserTokenAllocationReached(
            request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
        );
    }
    // Update total tokens requested for user for launch group
    userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
    // Transfer payment currency from user to contract
    IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
}
```

This implementation is incorrect as it mixes the token amount with the currency amount. When updating the amount of tokens the user has in the `userTokens` map (lines 361 and 374), the function uses `userTokenAmount` (nominated in the token) and `refundCurrencyAmount` or `additionalCurrencyAmount` (nominated in the currency).

Additionally, the implementation incorrectly assumes that a positive difference in currency will lead to an increment in tokens, and that a negative difference in currency will lead to a decrement in tokens, something that might not hold if the currency pricing changes.

### Internal Pre-conditions

1. User has an existing participation.
2. User creates a request to update participation.
3. User submits the request to update participation by calling `updateParticipation()`.

### External Pre-conditions

1. Signer role signs a request to update the participation.

### Attack Path

The issue can be triggered accidentally or intentionally by an attacker. The user just needs to submit the request to update the participation to execute the flawed logic.

### Impact

This is a critical issue as the implementation will mix amounts from potentially different magnitudes. If the relative value or scale of the currency is higher than the token, the user will get less tokens than expected. If the relative value or scale of the currency is lower than the token, the user will get more tokens than expected.

### PoC

The following test should be placed in Launch.UpdateParticipation.t.sol

```solidity
function test_UpdateParticipation_MixedAmounts() public {
    vm.startPrank(admin.addr);

    // fund user
    currency.transfer(user1, 2000 * 10 ** launch.tokenDecimals());

    // simulate a different relation between currency and token as the original configuration in the test is 1:1
    CurrencyConfig memory currencyConfig =
        CurrencyConfig({tokenPriceBps: 2 * 10 ** currency.decimals(), isEnabled: true});

    launch.setLaunchGroupCurrency(testLaunchGroupId, address(currency), currencyConfig);

    vm.stopPrank();

    // Prepare update participation request
    UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(1500);
    bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

    vm.startPrank(user1);

    currency.approve(address(launch), type(uint256).max);

    // Update participation
    launch.updateParticipation(updateRequest, updateSignature);

    vm.stopPrank();

    // CRITICAL: THIS SHOULD BE 1500 BUT IT IS 3000 !!!
    uint256 userTokens = launch.getUserTokensByLaunchGroup(testLaunchGroupId, testUserId);
    assertEq(userTokens, 3000 * 10 ** launch.tokenDecimals());
}
```

### Mitigation

The implementation should perform these two steps independently:

1. Calculate the new currency amount and refund or charge the user with the difference.
2. To calculate the new token amount, first subtract the previous token amount (`prevInfo.tokenAmount`) and then add the new token amount:

```solidity
(, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
userTokens.set(request.userId, userTokenAmount - prevInfo.tokenAmount + request.tokenAmount);
```
