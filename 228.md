Loud Graphite Mallard

High

# Incorrect Unit Conversion in Participation Update Can Lead to Incorrect Allocation and Fund Loss

### Summary

The `updateParticipation` function in the `Launch.sol` contract incorrectly compares token amounts and currency amounts (e.g., USDC) when validating user participation updates. This leads to incorrect validation checks and user allocation updates, potentially preventing valid updates or allowing improper refunds and overcharges.



### Root Cause

This is the `updateParticipation` function where users can update their `participationInfo`: 

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L312-L397


```Solidity   
 function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        // Validate request is intended for this launch and unexpired
        _validateRequest(
            request.launchId, request.launchGroupId, request.chainId, request.requestExpiresAt, request.userAddress
        );
        // Validate launch group is open for participation
        LaunchGroupSettings memory settings = launchGroupSettings[request.launchGroupId];
        _validateTimestamp(settings);
        // Validate request signature is from signer role
        _validateRequestSignature(keccak256(abi.encode(request)), signature);
        // Validate payment currency is enabled for launch group
        uint256 tokenPriceBps = _validateCurrency(request.launchGroupId, request.currency);

        ParticipationInfo storage prevInfo = launchGroupParticipations[request.prevLaunchParticipationId];
        // If launch group finalizes at participation, the participation is considered complete and not updatable
        if (settings.finalizesAtParticipation || prevInfo.isFinalized) {
            revert ParticipationUpdatesNotAllowed(request.launchGroupId, request.prevLaunchParticipationId);
        }

        // Validate participation exists and user, requested currency match
        ParticipationInfo storage newInfo = launchGroupParticipations[request.newLaunchParticipationId];
        if (request.currency != prevInfo.currency) {
            revert CurrencyMismatch(prevInfo.currency, request.currency);
        }
        if (request.userId != prevInfo.userId) {
            revert UserIdMismatch(prevInfo.userId, request.userId);
        }

        // Calculate new payment amount in requested currency based on token price and requested token amount
        uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
        // Get total tokens requested for user for launch group
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        // If new requested token amount is less than old amount, handle refund
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) { //@audit 
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }

        // Set participation details for user
        newInfo.currencyAmount = newCurrencyAmount;
        newInfo.currency = request.currency;
        newInfo.userAddress = msg.sender;
        newInfo.userId = request.userId;
        newInfo.tokenAmount = request.tokenAmount;
        // Reset previous participation info
        prevInfo.currencyAmount = 0;
        prevInfo.tokenAmount = 0;

        emit ParticipationUpdated(
            request.launchGroupId,
            request.newLaunchParticipationId,
            request.userId,
            msg.sender,
            request.tokenAmount,
            request.currency
        );
    }
```
* The function incorrectly subtracts `refundCurrencyAmount` (USDC) from `userTokenAmount` (tokens).
* Similarly, it adds `additionalCurrencyAmount` (USDC) to `userTokenAmount` (tokens), leading to incorrect validation.
* The function also incorrectly updates `userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);`, reducing tokens based on USDC value.


This results in these issues/problems:

1, Unit Mismatch in Min Allocation Check

```Solidity
if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
```
* `refundCurrencyAmount` is in USDC (or another currency).
* `userTokenAmount` is in tokens.
* Subtracting these directly makes no sense.


2, Unit Mismatch in Max Allocation Check

```Solidity
if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
```
* `additionalCurrencyAmount` is in currency (e.g., USDC).
* `userTokenAmount` is in tokens.
* Again, adding them directly is incorrect.


3, Incorrect Update of userTokens


```Solidity
userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
```
* This reduces `userTokenAmount` by `refundCurrencyAmount` (which is in currency units, not tokens).


### Internal Pre-conditions

1, The attacker must have an active participation in a launch group.
2, The launch group must allow participation updates (i.e., `finalizesAtParticipation` is false).


### External Pre-conditions

Nil

### Attack Path

Step 1: Participate in a Launch.
Step 2: Exploit Refund Logic for Additional ERC20/USDC withdrawal.
Step 3: Exploit Additional Payment Validation to Get Extra Tokens Without Paying.
Step 4: Withdraw Illegitimate Funds & Repeat.


### Impact

* Valid participation updates may get rejected, preventing users from adjusting their token allocation.
* Incorrect refunds or overcharges may occur, leading to financial loss for users.
* Attacker can extract more refunds than they should, draining funds from the launch.

### PoC

Scenario 1: `Incorrect Refund Validation`
Steps to Reproduce:

1, A user participates in a launch, purchasing 10 tokens at a price of 1 token = 2 USDC, spending 20 USDC.

2, The user later requests to reduce their token amount to 5 tokens, expecting a 10 USDC refund.

3, The function incorrectly checks:

```Solidity
if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser)
```
* refundCurrencyAmount = 10 USDC
* userTokenAmount = 10 tokens
* Since USDC and tokens are not the same unit, this check is invalid.

4, If 10 tokens - 10 USDC happens to be below `minTokenAmountPerUser`, the contract wrongly reverts, preventing a valid refund.


Scenario 2: `Incorrect Additional Payment Validation`
Steps to Reproduce:

1, A user initially purchases 5 tokens at 1 token = 2 USDC (cost = 10 USDC).

2, The user later wants to increase their participation to 10 tokens (requiring 10 USDC more).

3, The function incorrectly checks:

```Solidity
if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser)
```
* additionalCurrencyAmount = 10 USDC
* userTokenAmount = 5 tokens
* Since USDC and tokens are different units, this check is invalid.

4, If 5 tokens + 10 USDC is above `maxTokenAmountPerUser`, the contract wrongly reverts, preventing a valid update.




### Mitigation

Always compare token amounts to token amounts and currency amounts to currency amounts. Update `refundCurrencyAmount` with `request.tokenAmount` wherever required.
Ex: 
Update this:
```Solidity
if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser)
```
To: 
```Solidity
if (userTokenAmount - request.tokenAmount < settings.minTokenAmountPerUser)
```

Similarly, update wherever issue exists.