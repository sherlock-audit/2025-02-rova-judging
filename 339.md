Scruffy Brown Troll

Medium

# [M-01] Already Refunded Participations will Cause Batch Refunds to Revert When called By an Operator

### Summary

A bug in the `batchRefund` function causes the entire refund batch to revert if any included participation has already been refunded. This means that if one user claims an individual refund, subsequent batch refunds that include that refunded participation will fail, preventing other users from receiving their refunds and potentially locking funds in the contract.

### Root Cause

In [Launch.sol:613-615](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L613-L615) the `_processRefund` function does not check if a participation has already been refunded (i.e., tokenAmount and currencyAmount are zero), causing batchRefund to revert when an already refunded participation is processed.








### Internal Pre-conditions

1. A participant needs to call `claimRefund` to set their participation's `tokenAmount` and `currencyAmount` to exactly 0.
2. An operator needs to call `batchRefund` to include a participation whose `tokenAmount` and `currencyAmount` are exactly 0.

### External Pre-conditions

N/A

### Attack Path

1. A user participates in the token sale, creating a participation record with nonzero token and currency amounts.
2. The launch group status is later set to COMPLETED, enabling refunds.
3. The user individually calls `claimRefund`, which processes their refund and sets their participation’s `tokenAmount` and `currencyAmount` to 0.
4. An operator later initiates a batch refund by calling `batchRefund` that includes multiple participation IDs—one of which is the already refunded participation.
5. As the batch refund iterates over the participation records, it encounters the refunded participation (with `tokenAmount` and `currencyAmount` equal to 0).
6. The internal `_processRefund` function detects that the refunded participation is invalid for processing and reverts the entire transaction, preventing the batch refund from proceeding.








### Impact

If even one user's participation has already been refunded individually, then any batch refund call that includes that participation will revert entirely. This means that refunds for other users in the batch won’t be processed, potentially leaving funds locked in the contract. Essentially, the refund process becomes unreliable and can be exploited to block refunds for honest participants.

### PoC

To demonstrate this bug, create a new file in your test folder and call it `Launch.BatchRefundBug.t.sol`. Then, paste the code below inside that file:

```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {Test} from "forge-std/Test.sol";
import {LaunchTestBase, IERC20Events} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    ClaimRefundRequest,
    ParticipationInfo
} from "../src/Types.sol";

contract LaunchBatchRefundMultipleUsersTest is Test, Launch, LaunchTestBase, IERC20Events {
    LaunchGroupSettings public settings;
    ParticipationRequest[] public requests;
    bytes32[] public participationIds;
    address[] public users;

    function setUp() public {
        _setUpLaunch();
        settings = _setupLaunchGroup();

        participationIds = new bytes32[](2);
        participationIds[0] = bytes32(uint256(1)); 
        participationIds[1] = bytes32(uint256(2));
        users = new address[](2);
        users[0] = user1;
        users[1] = user2;

        requests = _setupParticipations(participationIds, users);
    }

    function test_BatchRefund_MultipleUsers_WithOneRefunded() public {
        // Mark the launch group as COMPLETED to allow refunds.
        vm.startPrank(manager);
        launch.setLaunchGroupStatus(testLaunchGroupId, LaunchGroupStatus.COMPLETED);
        vm.stopPrank();

        // User1 claims their refund individually.
        vm.startPrank(user1);
        ClaimRefundRequest memory refundRequestUser1 = _createClaimRefundRequestForParticipation(
            requests[0].launchParticipationId, user1, requests[0].userId
        );
        bytes memory refundSignatureUser1 = _signRequest(abi.encode(refundRequestUser1));
        launch.claimRefund(refundRequestUser1, refundSignatureUser1);
        vm.stopPrank();

        // The operator will now attempt a batch refund for both participation IDs.
        bytes32[] memory refundIds = new bytes32[](2);
        refundIds[0] = requests[0].launchParticipationId; // Already refunded by user1.
        refundIds[1] = requests[1].launchParticipationId; // Not refunded.
        
        vm.startPrank(operator);
        // We expect the batch refund to revert because one participation is already refunded.
        vm.expectRevert(
            abi.encodeWithSelector(InvalidRefundRequest.selector, requests[0].launchParticipationId, requests[0].userId)
        );
        launch.batchRefund(testLaunchGroupId, refundIds);
        vm.stopPrank();
    }

    function _createClaimRefundRequestForParticipation(
        bytes32 launchParticipationId,
        address user,
        bytes32 userId
    ) internal view returns (ClaimRefundRequest memory) {
        return ClaimRefundRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: launchParticipationId,
            userId: userId,
            userAddress: user,
            requestExpiresAt: block.timestamp + 1 hours
        });
    }
}

```

Now run this command in your terminal:

```bash
forge test --mt test_BatchRefund_MultipleUsers_WithOneRefunded -vvvv
```

This test walks through the mentioned attack path as already mentioned above and confirms that the `BatchRefund` function will always revert if there is a user who has already claimed their refund.

### Mitigation

To mitigate this issue, modify the `_processRefund` internal function so that if a participation has already been refunded (i.e. its tokenAmount and currencyAmount are zero), the function simply skips that entry rather than reverting the entire batch.

```diff
+ if (info.tokenAmount == 0 && info.currencyAmount == 0) {
+   return;
+ }
```

The updated  `_processRefund` function would look like this:

```solidity
function _processRefund(bytes32 launchGroupId, bytes32 launchParticipationId, ParticipationInfo storage info)
        private
    {
        if (info.tokenAmount == 0 && info.currencyAmount == 0) {
            return;
        }
        
        // If participation is finalized or has no currency amount or no token amount (cancelled), revert
        if (info.isFinalized || info.currencyAmount == 0 || info.tokenAmount == 0) {
            revert InvalidRefundRequest(launchParticipationId, info.userId);
        }

        // Subtract refunded participation token amount from total tokens requested for user
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(info.userId);
        userTokens.set(info.userId, userTokenAmount - info.tokenAmount);

        // Reset participation info
        uint256 refundCurrencyAmount = info.currencyAmount;
        info.tokenAmount = 0;
        info.currencyAmount = 0;

        // Transfer payment currency from contract to user
        IERC20(info.currency).safeTransfer(info.userAddress, refundCurrencyAmount);

        emit RefundClaimed(
            launchGroupId, launchParticipationId, info.userId, info.userAddress, refundCurrencyAmount, info.currency
        );
    }
```

This checks if both amounts are zero and return early, and only revert if one (but not both) is zero or if the participation is finalized.