Zealous Black Coyote

High

# Incorrect Comparison Logic in updateParticipation()

### Summary

The current implementation of `updateParticipation()` contains a critical flaw due to the incorrect comparison of values in different units (currency vs. tokens). This could lead to users exceeding their token allocation limits, undermining the fairness and security of the protocol. 
```solidity

if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }

```

### Root Cause

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L351-L377
In the `participate()` function, users can participate in a launch group by submitting a participation request. The protocol ensures that the requested token amount (`request.tokenAmount`) falls within the allowed range, i.e., it is greater than or equal to `settings.minTokenAmountPerUser` and less than or equal to `settings.maxTokenAmountPerUser`. 
```solidity
       // Validate user requested token amount is within launch group user allocation limits
        uint256 newUserTokenAmount = userTokenAmount + request.tokenAmount;
        if (newUserTokenAmount > settings.maxTokenAmountPerUser) {
            revert MaxUserTokenAllocationReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }
        if (newUserTokenAmount < settings.minTokenAmountPerUser) {
            revert MinUserTokenAllocationNotReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }

```


The `currencyAmount` is derived from the `request.tokenAmount` using a conversion rate (e.g., `currencyAmount = request.tokenAmount * tokenPriceBps / 10^tokenDecimals`). The participation details are then stored, including `info.tokenAmount = request.tokenAmount` and `info.currencyAmount = currencyAmount`.
```solidity
    function _calculateCurrencyAmount(uint256 tokenPriceBps, uint256 tokenAmount) internal view returns (uint256) {
        return Math.mulDiv(tokenPriceBps, tokenAmount, 10 ** tokenDecimals);
    }

```
```solidity
  // Set participation details for user
        info.userAddress = msg.sender;
        info.userId = request.userId;
        info.tokenAmount = request.tokenAmount;
        info.currencyAmount = currencyAmount;
        info.currency = request.currency;

```

In the `updateParticipation()` function, users can update their requested token amount. If the new `currencyAmount` (`newCurrencyAmount`) is greater than the previous `currencyAmount` (`prevInfo.currencyAmount`), the protocol calculates the additional amount required (`additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount`). 

```solidity
else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }

```

It then verifies whether the updated total token amount (`userTokenAmount + additionalCurrencyAmount`) is less than or equal to `settings.maxTokenAmountPerUser`.
```solidity
  if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }

```

The issue arises because `additionalCurrencyAmount` is derived from the difference in `currencyAmount`, which is based on the `request.tokenAmount` (i.e., `currencyAmount = request.tokenAmount * tokenPriceBps / 10^tokenDecimals`). However, `userTokenAmount` is directly derived from `request.tokenAmount` (i.e., `userTokenAmount` represents the total token amount requested by the user). 

The code incorrectly adds `additionalCurrencyAmount` (a value in currency units) to `userTokenAmount` (a value in token units) and compares the result with `settings.maxTokenAmountPerUser` (a value in token units). This is a **unit mismatch**, as `additionalCurrencyAmount` is in currency units (e.g., USD, ETH), while `userTokenAmount` and `settings.maxTokenAmountPerUser` are in token units.
The "if" branch has the same issue.



### Internal Pre-conditions

     The `tokenPriceBps` is not set to `1 * 10 * (currency.decimals())`.

### External Pre-conditions

    None

### Attack Path

  Users' attempts to update their requested token amount failed.

### Impact

This could lead to users exceeding their token allocation limits, undermining the fairness and security of the protocol. 
If the `tokenPriceBps` is set too high, there is a possibility of overflow in the `if` branch, where `userTokenAmount - refundCurrencyAmount` could exceed the allowed value.

### PoC

In the `setupLaunchGroupWithStatus()` function, the `tokenPriceBps` in the `currencyConfig` is set to `15 * 10 ** (currency.decimals() - 1)`. 
```solidity
 CurrencyConfig memory currencyConfig =
            CurrencyConfig({tokenPriceBps: 15 * 10 ** (currency.decimals()-1), isEnabled: true});
        LaunchGroupSettings memory settings = LaunchGroupSettings({
            finalizesAtParticipation: false,
            startsAt: block.timestamp,
            endsAt: block.timestamp + 1 days,
            maxTokenAllocation: 10000 * 10 ** launch.tokenDecimals(),
            minTokenAmountPerUser: 500 * 10 ** launch.tokenDecimals(),
            maxTokenAmountPerUser: 3000 * 10 ** launch.tokenDecimals(),
            status: status
        });
```

In the `updateParticipation()` function, the following output is added. 
```solidity
function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        // Validate request is intended for this launch and unexpired
        _validateRequest(
            request.launchId, request.launchGroupId, request.chainId, request.requestExpiresAt, request.userAddress
        );
        // Validate launch group is open for participation
        LaunchGroupSettings memory settings = launchGroupSettings[request.launchGroupId];
        _validateTimestamp(settings);
        // Validate request signature is from signer role
        _validateRequestSignature(keccak256(abi.encode(request)), signature);
        // Validate payment currency is enabled for launch group
        uint256 tokenPriceBps = _validateCurrency(request.launchGroupId, request.currency);

        ParticipationInfo storage prevInfo = launchGroupParticipations[request.prevLaunchParticipationId];
        // If launch group finalizes at participation, the participation is considered complete and not updatable
        if (settings.finalizesAtParticipation || prevInfo.isFinalized) {
            revert ParticipationUpdatesNotAllowed(request.launchGroupId, request.prevLaunchParticipationId);
        }

        // Validate participation exists and user, requested currency match
        ParticipationInfo storage newInfo = launchGroupParticipations[request.newLaunchParticipationId];
        if (request.currency != prevInfo.currency) {
            revert CurrencyMismatch(prevInfo.currency, request.currency);
        }
        if (request.userId != prevInfo.userId) {
            revert UserIdMismatch(prevInfo.userId, request.userId);
        }

        // Calculate new payment amount in requested currency based on token price and requested token amount
        uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
        // Get total tokens requested for user for launch group
        console.log("request.tokenAmount:",request.tokenAmount);
        console.log("newCurrencyAmount:",newCurrencyAmount);
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        console.log("userTokenAmount:",userTokenAmount);
        // If new requested token amount is less than old amount, handle refund
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            console.log("prevInfo.currencyAmount:",prevInfo.currencyAmount);
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
            console.log("total amount:",userTokenAmount + additionalCurrencyAmount);
            console.log("maxTokenAmountPerUser:",settings.maxTokenAmountPerUser);
            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }

        // Set participation details for user
        newInfo.currencyAmount = newCurrencyAmount;
        newInfo.currency = request.currency;
        newInfo.userAddress = msg.sender;
        newInfo.userId = request.userId;
        newInfo.tokenAmount = request.tokenAmount;
        // Reset previous participation info
        prevInfo.currencyAmount = 0;
        prevInfo.tokenAmount = 0;

        emit ParticipationUpdated(
            request.launchGroupId,
            request.newLaunchParticipationId,
            request.userId,
            msg.sender,
            request.tokenAmount,
            request.currency
        );
    }

```

Running `forge test --match-test "test_UpdateParticipation_IncreaseAmount" -vvv`, we observe that the condition `userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser` is incorrect.
```solidity
Logs:
  request.tokenAmount: 2000000000000000000000
  newCurrencyAmount: 3000000000000000000000
  userTokenAmount: 1000000000000000000000
  prevInfo.currencyAmount: 1500000000000000000000
  total amount: 2500000000000000000000
  maxTokenAmountPerUser: 3000000000000000000000

```

### Mitigation

Instead of adding `additionalCurrencyAmount` (currency units) to `userTokenAmount` (token units), the protocol should:
1. Convert the `additionalCurrencyAmount` back to tokens using the inverse of the conversion rate:
   ```solidity
   additionalTokenAmount = additionalCurrencyAmount * 10^tokenDecimals / tokenPriceBps;
   ```
2. Add the `additionalTokenAmount` to `userTokenAmount` and compare the result with `settings.maxTokenAmountPerUser`:
   ```solidity
   if (userTokenAmount + additionalTokenAmount > settings.maxTokenAmountPerUser) {
       revert MaxUserTokenAllocationReached(...);
   }
```