Shallow Coral Griffin

Medium

# min, max can be bypassed in updateParticipation when tokenPriceBps has changed

### Summary

In `updateParticipation`, if `tokenPriceBps` differs from when the user first participated, users can request fewer launch tokens while having a higher `newCurrencyAmount`, or request more launch tokens while having a lower `newCurrencyAmount`.

However, it only checks `maxTokenAmountPerUser` when `newCurrencyAmount` is larger than before, and `minTokenAmountPerUser` when it is smaller. However, if `tokenPriceBps` has changed, an increase or decrease in payment tokens does not necessarily mean an increase or decrease in launch tokens. Therefore, min and max should be checked regardless of `newCurrencyAmount`. 


### Root Cause

[https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L351-L377](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L351-L377)

When you call `updateParticipation`, you can adjust the amount requested by either getting the payment token back or paying more. However, if the `tokenPriceBps` is different than when the user first participated, the `newCurrencyAmount` may be larger than before while requesting fewer launch tokens, or conversely, the `newCurrencyAmount` may be smaller than before while requesting more launch tokens.

```solidity
function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
    external
    nonReentrant
    whenNotPaused
    onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
{
    ...

    // Calculate new payment amount in requested currency based on token price and requested token amount
    uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
    // Get total tokens requested for user for launch group
    EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
    (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
    // If new requested token amount is less than old amount, handle refund
@>  if (prevInfo.currencyAmount > newCurrencyAmount) {
        // Calculate refund amount
        uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
        // Validate user new requested token amount is greater than min token amount per user
        if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
@>          revert MinUserTokenAllocationNotReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }
        // Update total tokens requested for user for launch group
        userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
        // Transfer payment currency from contract to user
        IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
@>  } else if (newCurrencyAmount > prevInfo.currencyAmount) {
        // Calculate additional payment amount
        uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
        // Validate user new requested token amount is within launch group user allocation limits
        if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
@>          revert MaxUserTokenAllocationReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }
        // Update total tokens requested for user for launch group
        userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
        // Transfer payment currency from user to contract
        IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
    }

    ...
}
```

The implementation only checks the minimum amount when `prevInfo.currencyAmount > newCurrencyAmount` and the maximum amount when `newCurrencyAmount > prevInfo.currencyAmount`. When prices are equal, it doesn't check either limit. However, if `tokenPriceBps` has changed, an increase or decrease in payment tokens does not necessarily mean an increase or decrease in launch tokens.


### Internal Pre-conditions

1. User calls `participate` with an amount close to minTokenAmountPerUser or maxTokenAmountPerUser
2. Admin changes `tokenPriceBps` of the payment token


### External Pre-conditions

1. The payment token price changes significantly, necessitating tokenPriceBps adjustment


### Attack Path

1. User calls `updateParticipation` 
    1. Request more launch tokens while paying less, bypassing maxTokenAmountPerUser
    2. Request fewer launch tokens while paying more, bypassing minTokenAmountPerUser


### Impact

Users can bypass `minTokenAmountPerUser` or `maxTokenAmountPerUser` restrictions in `updateParticipation` calls.


### PoC

Add it to Launch.UpdateParticipation.t.sol to run it. 

```solidity
function test_PoC_UpdateParticipation_bypass_minmax() public {

    uint256 tokensSold = launch.getUserTokensByLaunchGroup(testLaunchGroupId, testUserId);
    assertEq(tokensSold, 1000 * 10 ** launch.tokenDecimals());

    // Update tokenPriceBps
    vm.startPrank(manager);
    launch.setLaunchGroupCurrency(
        testLaunchGroupId, address(currency), CurrencyConfig({tokenPriceBps: (1 * 10 ** currency.decimals()) / 2, isEnabled: true}) // decrease tokenPriceBps
    );

    settings.maxTokenAmountPerUser = 1000 * 10 ** launch.tokenDecimals();
    launch.setLaunchGroupSettings(testLaunchGroupId, settings);
    vm.stopPrank();

    uint256 newTokenAmount = 1001; // more than maxTokenAmountPerUser
    // Prepare update participation request
    UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(newTokenAmount);
    bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

    vm.startPrank(user1);
    uint256 updatedCurrencyAmount =
        _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, updateRequest.tokenAmount);
    currency.approve(address(launch), updatedCurrencyAmount);

    // Update participation
    launch.updateParticipation(updateRequest, updateSignature);

    // Verify update
    ParticipationInfo memory newInfo = launch.getParticipationInfo(updateRequest.newLaunchParticipationId);
    assertEq(newInfo.tokenAmount, 1001 * 10 ** launch.tokenDecimals());

    vm.stopPrank();
}
```

### Mitigation

Always check `minTokenAmountPerUser` and `maxTokenAmountPerUser` regardless of payment token amount.