Interesting Parchment Sheep

High

# Unit Mixing Bug in Token Accounting

## Summary

A bug exists in the [updateParticipation](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L312) function where token and currency units are incorrectly mixed in both increase and decrease cases of participation updates.

## Impact

Incorrect token balance accounting in both increase and decrease flows

## Description

In the `updateParticipation` function, the contract incorrectly mixes token and currency units in both branches:
When decreasing participation:

```solidity
if (prevInfo.currencyAmount > newCurrencyAmount) {
    uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
    if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
        revert MinUserTokenAllocationNotReached(...);
    }
    userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
}
```

When increasing participation:

```solidity
else if (newCurrencyAmount > prevInfo.currencyAmount) {
    uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
    if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
        revert MaxUserTokenAllocationReached(...);
    }
    userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
}
```

## Proof of Concept
Add this test to `Launch.UpdateParticipationTest.t.sol`

```solidity
  function test_UpdateParticipation_UnitMixing() public {

        vm.prank(manager);
        launch.setLaunchGroupCurrency(
            testLaunchGroupId,
            address(currency),
            CurrencyConfig({
                tokenPriceBps: 1500000, // 1.5 * 10^6 per docs
                isEnabled: true
            })
        );

        uint256 initialTokens = launch.getUserTokensByLaunchGroup(testLaunchGroupId, testUserId);
        assertEq(initialTokens, 1000 * 10**18, "Initial balance should be 1000 tokens");

        // Calculate currency amounts based on 1.5 USDC per token
        uint256 initialCurrency = (1500000 * initialTokens) / 10**18; // 1500 USDC for 1000 tokens
        uint256 newTokenAmount = 500 * 10**18; // Want to update to 500 tokens
        uint256 newCurrency = (1500000 * newTokenAmount) / 10**18; // 750 USDC for 500 tokens
        uint256 refundCurrencyAmount = initialCurrency - newCurrency; // 750 USDC to refund

        emit log_named_uint("Initial token balance (in wei)", initialTokens);
        emit log_named_uint("Refund currency amount (in USDC units)", refundCurrencyAmount);

        uint256 wrongResult = initialTokens - refundCurrencyAmount;

        emit log_named_uint("Mixed unit result (meaningless)", wrongResult);
        emit log_named_uint("What it should be (500e18)", newTokenAmount);

        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(500);
        updateRequest.prevLaunchParticipationId = testLaunchParticipationId;
        updateRequest.launchGroupId = originalParticipationRequest.launchGroupId;
        updateRequest.currency = originalParticipationRequest.currency;
        bytes memory signature = _signRequest(abi.encode(updateRequest));

        vm.expectRevert(
            abi.encodeWithSelector(
                Launch.MinUserTokenAllocationNotReached.selector,
                testLaunchGroupId,
                testUserId,
                initialTokens,
                newTokenAmount
            )
        );
        vm.prank(user1);
        launch.updateParticipation(updateRequest, signature);
    }
```

```solidity
[PASS] test_UpdateParticipation_UnitMixing() (gas: 113426)
Logs:
  Initial token balance (in wei): 1000000000000000000000
  Refund currency amount (in USDC units): 750000000
  Mixed unit result (meaningless): 999999999999250000000
  What it should be (500e18): 500000000000000000000

```