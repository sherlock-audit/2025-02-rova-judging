Pet Navy Osprey

Medium

# Users can lose fund while updating participation

### Summary

Lack of validation in function `updateParticipation()` can cause the users possible to lose funds

### Root Cause

The [function `updateParticipation()`](https://github.com/dpm-labs/rova-contracts/blob/b03e4ede50488a2000bb73d602e28ee4f6c0941f/src/Launch.sol#L312) allows an user to update the participation by using the request and signature provided by the Rova backend system. However, there are points that can cause users unintentionally to lose funds
- Lack of mechanism to check signature replay
- No validate if the previous participation is valid

```solidity
    function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        // Validate request is intended for this launch and unexpired
        _validateRequest(
            request.launchId, request.launchGroupId, request.chainId, request.requestExpiresAt, request.userAddress
        );
        // Validate launch group is open for participation
        LaunchGroupSettings memory settings = launchGroupSettings[request.launchGroupId];
        _validateTimestamp(settings);
        // Validate request signature is from signer role
@>        _validateRequestSignature(keccak256(abi.encode(request)), signature);
/// <<<== signature is not invalidated after verification -> can replay signature

        // Validate payment currency is enabled for launch group
        uint256 tokenPriceBps = _validateCurrency(request.launchGroupId, request.currency);

        ParticipationInfo storage prevInfo = launchGroupParticipations[request.prevLaunchParticipationId];
        // If launch group finalizes at participation, the participation is considered complete and not updatable
@>        if (settings.finalizesAtParticipation || prevInfo.isFinalized) { /// <<<== this only checks if it is finalized 
            revert ParticipationUpdatesNotAllowed(request.launchGroupId, request.prevLaunchParticipationId);
        }

/// <<< === no check if `prev.tokenAmount == 0` or `prev.currencyAmount == 0`

        // Validate participation exists and user, requested currency match
        ParticipationInfo storage newInfo = launchGroupParticipations[request.newLaunchParticipationId];
        if (request.currency != prevInfo.currency) {
            revert CurrencyMismatch(prevInfo.currency, request.currency);
        }
        if (request.userId != prevInfo.userId) {
            revert UserIdMismatch(prevInfo.userId, request.userId);
        }

        // Calculate new payment amount in requested currency based on token price and requested token amount
        uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
        // Get total tokens requested for user for launch group
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        // If new requested token amount is less than old amount, handle refund
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }

        // Set participation details for user
@>        newInfo.currencyAmount = newCurrencyAmount;
        newInfo.currency = request.currency;
        newInfo.userAddress = msg.sender;
        newInfo.userId = request.userId;
@>        newInfo.tokenAmount = request.tokenAmount;
        // Reset previous participation info
@>        prevInfo.currencyAmount = 0;
@>        prevInfo.tokenAmount = 0;

        emit ParticipationUpdated(
            request.launchGroupId,
            request.newLaunchParticipationId,
            request.userId,
            msg.sender,
            request.tokenAmount,
            request.currency
        );
    }
```
The logic checks the new token amount with the current token amount and then settle fund. It can be problem because the current token amount `prev.tokenAmount` and `prev.currencyAmount` are not validated if it is `0` or not. After the first time the function is called, `prevInfo.currency = 0` and `prevInfo.tokenAmount = 0`. If it is continue to be called, the caller needs to spend currency token because the execution goes into the if branch `else if (newCurrencyAmount > prevInfo.currencyAmount)`, and as a result the new participation info is just the same

Here it can exist a scenario that an user submits the transaction `updateParticipation()` more than once. For example: an user submits the transaction as intended, but the transaction takes so long to be finalized and the user submits the same transaction second time. Finally both 2 transactions are mined and the later one cause the user to lose funds.

Note: As a result, the value user tokens sold for launch group `_userTokensByLaunchGroup` can be increased without needing to request backend system.

### Internal Pre-conditions

1. Launch group is active
2. Alice participated with 500 tokens, paid 1000 currency amount

### External Pre-conditions

N/A

### Attack Path

1. Alice requests to update participation to 1000 tokens. Because this is increasing token amount, then Alice approves contract with large amount of currency (let's say maximum)
2. System validates and returns data back to Alice
3. Alice calls `updateParticipation()` with the valid data (let this transaction `tx_1`)
4. Alice waits for a like 10 mins but `tx_1` is not mined. Alices decides to call the function one more time (let this transaction `tx_2`)
5. After some mins, both txs get mined such that `tx_1 -> tx_2`. 
Here, `tx_1` will be a normal transaction, resulting Alice's participation to be 1000 tokens, paid by 2000 currency in total
But `tx_2` is executed which results Alice's participation to be still 1000 tokens, but Alice needs to pay 2000 currency more => participation info's total 1000 tokens, paid by 4000 currency
The total user tokens for the group will be 2000 tokens

Note that, setting account nonces for `tx_1` and `tx_2` is important at this scenario, which is done by wallets (users can be unaware of this).

### Impact

- Users can lose funds in scenarios described above

### PoC

Add the test to `test/Launch.UpdateParticipation.t.sol`
```solidity
    function test_UpdateParticipation_IncreaseAmount_Repeatedly() public {
        // fund user1 
        vm.startPrank(admin.addr);
        currency.transfer(user1, 2000 * 1e18);
        
        // Prepare update participation request
        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(1200); // increase from 1000 to 1200
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.startPrank(user1);
        
        // approve max
        currency.approve(address(launch), 2**256 - 1);

        uint currencyBalanceBefore = currency.balanceOf(user1);

        uint expectedCurrencyPaid = _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, updateRequest.tokenAmount);


        // Update participation
        launch.updateParticipation(updateRequest, updateSignature);

        // tx not mined

        // try to call function again
        launch.updateParticipation(updateRequest, updateSignature);

        uint currencyBalanceAfter = currency.balanceOf(user1);

        assertGe(currencyBalanceAfter, currencyBalanceBefore - expectedCurrencyPaid, "balance lower than expected");
    }
```
Run the test and console shows
```bash
Ran 1 test for test/Launch.UpdateParticipation.t.sol:LaunchUpdateParticipationTest
[FAIL: balance lower than expected: 1600000000000000000000 < 1800000000000000000000] test_UpdateParticipation_IncreaseAmount_Repeatedly() (gas: 293512)
Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 2.22ms (792.29Âµs CPU time)

Ran 1 test suite in 148.90ms (2.22ms CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)

Failing tests:
Encountered 1 failing test in test/Launch.UpdateParticipation.t.sol:LaunchUpdateParticipationTest
[FAIL: balance lower than expected: 1600000000000000000000 < 1800000000000000000000] test_UpdateParticipation_IncreaseAmount_Repeatedly() (gas: 293512)
```

### Mitigation

```diff
    function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        // Validate request is intended for this launch and unexpired
        _validateRequest(
            request.launchId, request.launchGroupId, request.chainId, request.requestExpiresAt, request.userAddress
        );
        // Validate launch group is open for participation
        LaunchGroupSettings memory settings = launchGroupSettings[request.launchGroupId];
        _validateTimestamp(settings);
        // Validate request signature is from signer role
        _validateRequestSignature(keccak256(abi.encode(request)), signature);

        // Validate payment currency is enabled for launch group
        uint256 tokenPriceBps = _validateCurrency(request.launchGroupId, request.currency);

        ParticipationInfo storage prevInfo = launchGroupParticipations[request.prevLaunchParticipationId];
        // If launch group finalizes at participation, the participation is considered complete and not updatable
        if (settings.finalizesAtParticipation || prevInfo.isFinalized) {
            revert ParticipationUpdatesNotAllowed(request.launchGroupId, request.prevLaunchParticipationId);
        }
+      if (prevInfo.tokenAmount == 0 || prevInfo.currencyAmount == 0) revert();
```