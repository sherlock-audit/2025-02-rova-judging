Powerful Sable Lizard

High

# The `updateParticipation` function misuses the `refundCurrencyAmount` variable to validate user new requested token amount and update total tokens

### Summary
In the `updateParticipation` function of the `Launch` contract, it validates new requested token amount and updates the total tokens  requested for user for launch group.
It does this by adding or subtracting `refundCurrencyAmount` from `userTokenAmount`.
However, `refundCurrencyAmount` is the currency token amount and `userTokenAmount` is the launch token amount.
Thus, these variables track the different tokens' amount.
As a result, this causes the incorrect check of the min and max token amount per user, and incorrect updates of the total tokens requested for user for launch group.

### Root Cause

https://github.com/sherlock-audit/2025-02-rova/tree/main/rova-contracts/src/Launch.sol#L355-L377

```solidity
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
@>          if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
@>          userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
@>          if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
@>          userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }
```

In the function, it checks `userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser` and `userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser`.
And, it updates the `userTokens` with `userTokenAmount - refundCurrencyAmount` or `userTokenAmount + refundCurrencyAmount`.
The `userTokenAmount` and `refundCurrencyAmount` variables track the different tokens' amount.

### Internal pre-conditions
None

### External pre-conditions
None

### Attack Path
None

### PoC
Example scenario:
1. `1 project token` is `3 currency token`.
2. `user1` participates in a launch group: 
    - `request.tokenAmount = 100 * 10^18`.
    - `request.currencyAmount = 300 * 10^18`.
    - `userTokens` is 100 * 10^18
3. He updates the token amount to 50 * 10^18.
    - `userTokenAmount = 100 * 10^18`.
    - `refundCurrencyAmount = 150 * 10^18`.
    - `userTokenAmount - refundCurrencyAmount = 100 * 10^18 - 150 * 10^18 = -50 * 10^18`: underflow error

### Impact
As a result, this causes the incorrect check of the min and max token amount per user, and incorrect updates of the total tokens requested for user for launch group.Participation` function.

### Mitigation
```diff
    function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        // Validate request is intended for this launch and unexpired
        _validateRequest(
            request.launchId, request.launchGroupId, request.chainId, request.requestExpiresAt, request.userAddress
        );
        // Validate launch group is open for participation
        LaunchGroupSettings memory settings = launchGroupSettings[request.launchGroupId];
        _validateTimestamp(settings);
        // Validate request signature is from signer role
        _validateRequestSignature(keccak256(abi.encode(request)), signature);
        // Validate payment currency is enabled for launch group
        uint256 tokenPriceBps = _validateCurrency(request.launchGroupId, request.currency);

        ParticipationInfo storage prevInfo = launchGroupParticipations[request.prevLaunchParticipationId];
        // If launch group finalizes at participation, the participation is considered complete and not updatable
        if (settings.finalizesAtParticipation || prevInfo.isFinalized) {
            revert ParticipationUpdatesNotAllowed(request.launchGroupId, request.prevLaunchParticipationId);
        }

        // Validate participation exists and user, requested currency match
        ParticipationInfo storage newInfo = launchGroupParticipations[request.newLaunchParticipationId]; // @audit override another users'  id? replay attack
        if (request.currency != prevInfo.currency) {
            revert CurrencyMismatch(prevInfo.currency, request.currency);
        }
        if (request.userId != prevInfo.userId) {
            revert UserIdMismatch(prevInfo.userId, request.userId);
        }

        // Calculate new payment amount in requested currency based on token price and requested token amount
        uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
        // Get total tokens requested for user for launch group
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        // If new requested token amount is less than old amount, handle refund
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
-           if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
+           if (userTokenAmount - (prevInfo.tokenAmount - request.tokenAmount) < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
-           userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
+           userTokens.set(request.userId, userTokenAmount - (prevInfo.tokenAmount - request.tokenAmount));
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
-           if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
+           if (userTokenAmount + (request.tokenAmount - prevInfo.tokenAmount) > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
-           userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
+           userTokens.set(request.userId, userTokenAmount + (request.tokenAmount - prevInfo.tokenAmount));
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }

        // Set participation details for user
        newInfo.currencyAmount = newCurrencyAmount;
        newInfo.currency = request.currency;
        newInfo.userAddress = msg.sender;
        newInfo.userId = request.userId;
        newInfo.tokenAmount = request.tokenAmount;
        // Reset previous participation info
        prevInfo.currencyAmount = 0;
        prevInfo.tokenAmount = 0;

        emit ParticipationUpdated(
            request.launchGroupId,
            request.newLaunchParticipationId,
            request.userId,
            msg.sender,
            request.tokenAmount,
            request.currency
        );
    }
```
