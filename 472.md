Funny Tangerine Walrus

Medium

# Withdrawals may run OOG as number of supported launch groups increases

## Summary

Rova's withdrawal process may encounter Out of Gas (OOG) errors due to the inefficient mechanism to check completed launch groups, leading to potential fund lockups.

## Vulnerability Details

Rova permits the address with the `WITHDRAWAL_ROLE` to withdraw funds from completed participations.

During this process, it verifies that all launch groups have completed through a loop.

The issue arises when there is a large number of launch groups. Since the `Launch` contract does not provide any methods to remove launch groups that have already completed, this could lead to OOG during withdrawals.

## Root Cause

Inefficient looping mechanisms for verifying the statuses of past and present launch groups can lead to Out of Gas (OOG) errors during withdrawals.

## LOC

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L573

## POC

Include the following test files in the `rova-contracts/test` directory:

1. `Launch.WithdrawGas.t.sol`
2. `LaunchTestBaseGas.t.sol`
3. Allow maximum gas limit by adding the line gas_limit = "18446744073709551615" in `foundry.toml`,  
4. run `forge test --mt test_Withdraw_Gas -vv`


`Launch.WithdrawGas.t.sol`

```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {IAccessControl} from "@openzeppelin/contracts/access/IAccessControl.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {Test} from "forge-std/Test.sol";
import {LaunchTestBaseGas, IERC20Events} from "./LaunchTestBaseGas.t.sol";
import {Launch} from "../src/Launch.sol";
import {LaunchGroupSettings, LaunchGroupStatus} from "../src/Types.sol";
import {console} from "forge-std/console.sol"; // @melville

contract LaunchWithdrawGasTest is Test, Launch, LaunchTestBaseGas, IERC20Events {
    function setUp() public {
        _setUpLaunch();

        for (uint256 i = 0; i < 7000; i++) {
            bytes32 dummyLaunchGroupId = keccak256(abi.encodePacked("dummyLaunchGroup", i));
            LaunchGroupSettings memory settings = _setupLaunchGroupWithStatus(dummyLaunchGroupId, LaunchGroupStatus.PENDING);
            settings.status = LaunchGroupStatus.ACTIVE;
            settings.finalizesAtParticipation = true;
            vm.startPrank(manager);
            launch.setLaunchGroupSettings(dummyLaunchGroupId, settings);
            vm.stopPrank();
        }

        // Setup multiple participations
        bytes32[] memory participationIds = new bytes32[](7000);
        for (uint256 i = 0; i < 7000; i++) {
            participationIds[i] = bytes32(uint256(i + 1));
        }

        _setupParticipations(participationIds, usersArray);

        // Complete the launch group
        vm.startPrank(manager);
        for (uint256 i = 0; i < 7000; i++) {
            bytes32 dummyLaunchGroupId = keccak256(abi.encodePacked("dummyLaunchGroup", i));
            launch.setLaunchGroupStatus(dummyLaunchGroupId, LaunchGroupStatus.COMPLETED);
        }
        vm.stopPrank();
    }

    // @audit POC
    function test_Withdraw_Gas() public {
        vm.startPrank(manager);
        launch.setLaunchGroupStatus(testLaunchGroupId, LaunchGroupStatus.COMPLETED);
        vm.stopPrank();

        uint256 withdrawableAmount = launch.getWithdrawableAmountByCurrency(address(currency));


        uint256 gasBefore = gasleft();
        vm.startPrank(testWithdrawalAddress);
        launch.withdraw(address(currency), withdrawableAmount);
        uint256 gasAfter = gasleft();
        uint256 gasUsed = gasBefore - gasAfter;
        console.log("Gas used for withdraw ", gasUsed);
        vm.stopPrank();
    }

}
```

 `LaunchTestBaseGas.t.sol`

```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {UnsafeUpgrades} from "@openzeppelin-foundry-upgrades/Upgrades.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {Math} from "@openzeppelin/contracts/utils/math/Math.sol";
import {VmSafe} from "forge-std/Vm.sol";
import {Test} from "forge-std/Test.sol";
import {Launch} from "../src/Launch.sol";
import {
    CurrencyConfig,
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    CancelParticipationRequest
} from "../src/Types.sol";

contract MockERC20 is ERC20 {
    constructor() ERC20("Mock Token", "MTK") {
        _mint(msg.sender, 100000000000000 * 10 ** 18);
    }
}

interface IERC20Events {
    event Transfer(address indexed from, address indexed to, uint256 value);
}

abstract contract LaunchTestBaseGas is Test, Launch {
    Launch public launch;
    MockERC20 public currency;

    VmSafe.Wallet public admin = vm.createWallet("admin");
    address public manager = address(1);
    address public operator = address(2);
    address public signer = address(3);
    address public testWithdrawalAddress = address(4);
    address public user1 = address(5);
    address public user2 = address(6);

    address[] public usersArray = new address[](7000);

    // Dummy cuids for testing
    bytes32 public testLaunchId = "cixf02ym000001b66m45ae4k8";
    bytes32 public testLaunchGroupId = "ch72gsb320000udocl363eofy";
    bytes32 public testLaunchParticipationId = "cm6o2sldi00003b74facm5z9n";
    bytes32 public testUserId = "cm6o2tm1300003b74dsss1s7q";

    function _setUpLaunch() public {
        vm.startPrank(admin.addr);

        // Deploy contracts
        _initializeLaunch(admin.addr, testWithdrawalAddress);
        currency = new MockERC20();

        // Setup roles
        launch.grantRole(MANAGER_ROLE, manager);
        launch.grantRole(OPERATOR_ROLE, operator);
        launch.grantRole(SIGNER_ROLE, signer);

        // Fund users
        for (uint256 i = 0; i < 7000; i++) {
            usersArray[i] = address(uint160(i + 100));
            currency.transfer(usersArray[i], 1000 * 10 ** launch.tokenDecimals());
        }
        vm.stopPrank();
    }

    // Helper functions
    function _setupLaunchGroup() internal returns (LaunchGroupSettings memory) {
        return _setupLaunchGroupWithStatus(testLaunchGroupId, LaunchGroupStatus.ACTIVE);
    }

    function _setupLaunchGroupWithStatus(bytes32 launchGroupId, LaunchGroupStatus status)
        internal
        returns (LaunchGroupSettings memory)
    {
        CurrencyConfig memory currencyConfig =
            CurrencyConfig({tokenPriceBps: 1 * 10 ** currency.decimals(), isEnabled: true});
        LaunchGroupSettings memory settings = LaunchGroupSettings({
            finalizesAtParticipation: false,
            startsAt: block.timestamp,
            endsAt: block.timestamp + 1 days,
            // maxTokenAllocation: 10000 * 10 ** launch.tokenDecimals(),
            maxTokenAllocation: 100000000 * 10 ** launch.tokenDecimals(),
            minTokenAmountPerUser: 500 * 10 ** launch.tokenDecimals(),
            maxTokenAmountPerUser: 3000 * 10 ** launch.tokenDecimals(),
            status: status
        });
        vm.startPrank(manager);
        launch.createLaunchGroup(launchGroupId, address(currency), currencyConfig, settings);
        vm.stopPrank();
        return settings;
    }

    function _updateLaunchGroupSettings(LaunchGroupSettings memory settings) internal {
        vm.startPrank(manager);
        launch.setLaunchGroupSettings(testLaunchGroupId, settings);
        vm.stopPrank();
    }

    function _createParticipationRequest() internal view returns (ParticipationRequest memory) {
        return ParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: testLaunchParticipationId,
            userId: testUserId,
            userAddress: user1,
            tokenAmount: 1000 * 10 ** launch.tokenDecimals(),
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }

    function _signRequest(bytes memory encodedRequest) internal view returns (bytes memory) {
        bytes32 structHash = keccak256(encodedRequest);
        bytes32 messageHash = keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", structHash));
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(admin.privateKey, messageHash);
        return abi.encodePacked(r, s, v);
    }

    function _signRequestWithSigner(bytes memory encodedRequest, uint256 privateKey)
        internal
        pure
        returns (bytes memory)
    {
        bytes32 structHash = keccak256(encodedRequest);
        bytes32 messageHash = keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", structHash));
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(privateKey, messageHash);
        return abi.encodePacked(r, s, v);
    }

    function _setupParticipations(bytes32[] memory participationIds, address[] memory users)
        internal
        returns (ParticipationRequest[] memory)
    {
        ParticipationRequest[] memory requests = new ParticipationRequest[](participationIds.length);
        for (uint256 i = 0; i < participationIds.length; i++) {
            ParticipationRequest memory request = ParticipationRequest({
                chainId: block.chainid,
                launchId: testLaunchId,
                launchGroupId: keccak256(abi.encodePacked("dummyLaunchGroup", i)),
                launchParticipationId: participationIds[i],
                userId: bytes32(uint256(i + 1)),
                userAddress: users[i],
                tokenAmount: 1000 * 10 ** launch.tokenDecimals(),
                currency: address(currency),
                requestExpiresAt: block.timestamp + 1 hours
            });

            bytes memory signature = _signRequest(abi.encode(request));

            vm.startPrank(users[i]);
            currency.approve(
                address(launch), _getCurrencyAmount(request.launchGroupId, request.currency, request.tokenAmount)
            );
            launch.participate(request, signature);
            vm.stopPrank();

            requests[i] = request;
        }
        return requests;
    }

    function _createCancelParticipationRequest() internal view returns (CancelParticipationRequest memory) {
        return CancelParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: testLaunchParticipationId,
            userId: testUserId,
            userAddress: user1,
            requestExpiresAt: block.timestamp + 1 hours
        });
    }

    function _getCurrencyAmount(bytes32 launchGroupId, address currencyAddress, uint256 tokenAmount)
        internal
        view
        returns (uint256)
    {
        uint256 tokenPriceBps = launch.getLaunchGroupCurrencyConfig(launchGroupId, currencyAddress).tokenPriceBps;
        return Math.mulDiv(tokenPriceBps, tokenAmount, 10 ** launch.tokenDecimals());
    }

    function _initializeLaunch(address adminAddress, address withdrawalAddress) internal {
        address proxyAddress = UnsafeUpgrades.deployTransparentProxy(
            address(new Launch()),
            adminAddress,
            abi.encodeWithSelector(Launch.initialize.selector, withdrawalAddress, testLaunchId, adminAddress, 18)
        );
        launch = Launch(proxyAddress);
    }
}
```

```terminal
Ran 1 test for test/Launch.Withdraw.t.sol:LaunchWithdrawTest
[PASS] test_Withdraw_Gas_x() (gas: 32439136)
Logs:
  Gas used for withdraw  32396862

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 1.60s (11.25ms CPU time)
```

The POC executes a withdrawal with 7000 completed Launch groups, each having 7000 participants.

The gas consumption in this scenario is approximately 32 million, exceeding the Ethereum block gas limit.

## Impact

Withdrawals will revert (due to OOG) if a large number of launch groups is supported. Funds are stuck in Launch contract.

## Mitigation

To verify the completion status of all launch groups, a possible approach is to maintain a counter that tracks the number of completed launch groups. This counter can then be compared to the total number of launch groups created to confirm that all prior and current launch groups have completed.