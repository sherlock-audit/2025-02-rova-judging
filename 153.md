Creamy Orchid Weasel

Medium

# `tokenPriceBps` Update May Cause Issues


## Summary

If a user participated before the project changed `tokenPriceBps` from `X` to `Y` and then later user updated their participation information, they may be unable to cancel their participation due to an [underflow](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L437) revert.

## Description

The `tokenPriceBps` variable can be updated via the `setLaunchGroupCurrency()` function. If the previous `tokenPriceBps` is greater than the new one, it can lead to an issue under certain conditions. Consider the following example:

The currency amount is calculated using the following [formula](https://github.com/dpm-labs/rova-contracts/blob/main/README.md#how-to-calculate-token-price):

```javascript
// Definitions:
// CTD = Currency Token Decimals 
// PTD = Project Token Decimals
// PT  = Project Token

// P = Intended price conversion

// Note: maxBps is defined as 10 ** tokenDecimals

tokenPriceBps = P * (10 ** CTD);
maxBps = 10 ** PTD;
currencyAmount = (tokenPriceBps * tokenAmount) / maxBps;
```

For example, assume the project token (PT) has 6 decimals and each `1 PT` (1e6) is worth `2 USDC` (2e6). If a user participates in the launch group by purchasing `5 PT` (5e6), the calculation would be:

```javascript
tokenPriceBps = 2e6;
maxBps = 1e6;
tokenAmount = 5e6;
currencyAmount = (2e6 * 5e6) / 1e6 = 10000000; // User pays 10 USDC for 5 PT when tokenPriceBps is 2e6.
```

After this purchase, the user's participation information is as follows:

```javascript
ParticipationInfo.tokenAmount = 5e6;
ParticipationInfo.currencyAmount = 10e6;
userTokens = 5e6;
```

Later, suppose the project updates `tokenPriceBps` for USDC to `1e6` (i.e., each `1 PT` is now worth 1 USDC).

If the user then decides to update their participation via `updateParticipation()` with the following parameter:

```javascript
request.tokenAmount = 10e6; // User now intends to buy 10 PT tokens.
```

Given that `tokenPriceBps` is now `1e6`, the new calculation is:

```javascript
currencyAmount = (1e6 * 10e6) / 1e6 = 10000000; // User pays 10 USDC for 10 PT tokens when tokenPriceBps is 1e6.
```

**The Issue:**

Since the calculated `currencyAmount` remains unchanged (`prevInfo.currencyAmount == newCurrencyAmount`), the conditional branches in the code below will not be executed:

```javascript
function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
    external
    // ...
{

    // ...

    uint256 tokenPriceBps = _validateCurrency(request.launchGroupId, request.currency);
    ParticipationInfo storage prevInfo = launchGroupParticipations[request.prevLaunchParticipationId];
    
    // ...

    uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
    
    EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
    (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);


    // prevInfo.currencyAmount = 10 USDC
    // newCurrencyAmount = 10 USDC
    //
    // as result the if-elseif branch below not executed since "prevInfo.currencyAmount == newCurrencyAmount"
    //
@>  if (prevInfo.currencyAmount > newCurrencyAmount) {
        uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;

        if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
            revert MinUserTokenAllocationNotReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }
        
@>      userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
        IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);

@>  } else if (newCurrencyAmount > prevInfo.currencyAmount) {
        uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
        
        if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
            revert MaxUserTokenAllocationReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }
        
@>      userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
        IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
    }

    newInfo.currencyAmount = newCurrencyAmount;
    newInfo.currency = request.currency;
    newInfo.userAddress = msg.sender;
    newInfo.userId = request.userId;
    newInfo.tokenAmount = request.tokenAmount;
    
    prevInfo.currencyAmount = 0;
    prevInfo.tokenAmount = 0;
    
    // ...
}
```

Because these branches are not executed, the `userTokens` state variable is not updated. As a result, the new participation record (`newInfo`) will have `currencyAmount` set to `10e6` and `tokenAmount` set to `10e6`, while `userTokens` remains at the previous value of `5e6`.

When the user later attempts to cancel their participation, the following code in `cancelParticipation()` fails due to an underflow:

```javascript
function cancelParticipation(CancelParticipationRequest calldata request, bytes calldata signature)
    external
    // ...
{
    // ...

    EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
    (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);

    // userTokenAmount = 5e6
    // info.tokenAmount = 10e6
    // 5e6 - 10e6 => revert
    if (userTokenAmount - info.tokenAmount == 0) {
        // ...
    } 

    // ...
}
```

## Recommended Mitigation

Update the `userTokens` state variable in all cases in `updateParticipation()` function, regardless of the conditions.