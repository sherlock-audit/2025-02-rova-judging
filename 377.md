Festive Eggplant Puma

High

# Mismatch between requested and stored token amount in Launch::updateParticipation

### Summary

In the `Launch::updateParticipation` function, the logic for calculating an updated user's token amount in a launch group is incorrect.

[Launch::updateParticipation](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L312) function:
```javascript
function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
    external
    nonReentrant
    whenNotPaused
    onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
{
    ...
    uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
    // Get total tokens requested for user for launch group
    EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
    (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
    // If new requested token amount is less than old amount, handle refund
    if (prevInfo.currencyAmount > newCurrencyAmount) {
        // Calculate refund amount
        uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
        // Validate user new requested token amount is greater than min token amount per user
=>      if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
            revert MinUserTokenAllocationNotReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }
        // Update total tokens requested for user for launch group
=>      userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
        // Transfer payment currency from contract to user
        IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
    } else if (newCurrencyAmount > prevInfo.currencyAmount) {
        // Calculate additional payment amount
        uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
        // Validate user new requested token amount is within launch group user allocation limits
=>      if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
            revert MaxUserTokenAllocationReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }
        // Update total tokens requested for user for launch group
=>      userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
        // Transfer payment currency from user to contract
        IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
    }
    ...
}
```

The expected behavior is that the userâ€™s token amount in the launch group after updating should be equal to `request.tokenAmount`. However, the function instead uses `userTokenAmount - refundCurrencyAmount` or `userTokenAmount + additionalCurrencyAmount` when checking user allocation limits and updating the stored token amount.

The problem occurs in cases where:  
1. The main token and the payment currency token have different decimals, meaning `userTokenAmount` and `refundCurrencyAmount` (or `additionalCurrencyAmount`) have different decimals.  
2. One main token does not equal one payment currency token, leading to discrepancies between `request.tokenAmount` and the final stored token amount (`userTokenAmount - refundCurrencyAmount` or `userTokenAmount + additionalCurrencyAmount`).

### Root Cause

In the `Launch::updateParticipation` function, the logic for calculating an updated user's token amount in a launch group is incorrect.

### Internal Pre-conditions

1. The main token and the payment currency token have different decimals.  
2. OR 1 main token is not equal to 1 payment currency token. 

### External Pre-conditions

No additional conditions required.

### Impact

This incorrect logic can lead to multiple issues, for example:
- If the main token has 6 decimals and the payment currency token has 18 decimals, the `Launch::updateParticipation` function will revert due to an underflow (`userTokenAmount - refundCurrencyAmount`) or exceed the max allocation per user (`userTokenAmount + additionalCurrencyAmount`). 
- If the main token has 18 decimals and the payment currency token has 6 decimals, `refundCurrencyAmount` and `additionalCurrencyAmount` are extremely small compared to `userTokenAmount`, causing the user's token amount to change only slightly after each update, resulting in an unexpected discrepancy from `request.tokenAmount`.

### Mitigation

Update the `Launch::updateParticipation` function as follows: 

```diff
function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
    external
    nonReentrant
    whenNotPaused
    onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
{
    ...
+   // Get total tokens requested for user for launch group
+   EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
+   (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
+   if (request.tokenAmount < settings.minTokenAmountPerUser) {
+       revert MinUserTokenAllocationNotReached(
+           request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
+       )
+   }

+   if (request.tokenAmount > settings.maxTokenAmountPerUser) {
+       revert MaxUserTokenAllocationReached(
+           request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
+       )
+   }
    uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
-   // Get total tokens requested for user for launch group
-   EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
-   (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
    // If new requested token amount is less than old amount, handle refund
    if (prevInfo.currencyAmount > newCurrencyAmount) {
        // Calculate refund amount
        uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
-       // Validate user new requested token amount is greater than min token amount per user
-       if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
-           revert MinUserTokenAllocationNotReached(
-               request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
-           );
-       }
-       // Update total tokens requested for user for launch group
-       userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
        // Transfer payment currency from contract to user
        IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
    } else if (newCurrencyAmount > prevInfo.currencyAmount) {
        // Calculate additional payment amount
        uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
-       // Validate user new requested token amount is within launch group user allocation limits
-       if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
-           revert MaxUserTokenAllocationReached(
-               request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
-           );
-       }
-       // Update total tokens requested for user for launch group
-       userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
        // Transfer payment currency from user to contract
        IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
    }
+   // Update total tokens requested for user for launch group
+   userTokens.set(request.userId, request.tokenAmount);
    ...
}
```