Fit Ruby Skunk

Medium

# Incorrect tokens check in `updateParticipation` function leads to DoS of this function or incorrect userToken allocation.

### Summary

In `Launch.sol` function `updateParticipation` mixing the tokenAmount and currencyAmount for airthmetic operations leading to wrong calculations for 
`userTokens`.For example [here]() function checks if `if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser)` however `refundCurrencyAmount` will be in units of currency token decimals, leading to incorrect check for `minTokenAmountPerUser`. Further this also set the `userToken` incorrectly as `userTokenAmount - refundCurrencyAmount` instead of  ` userTokenAmount - prevInfo.tokenAmount + request.tokenAmount`
If currencyToken has more deciamls than the laucnhToken then this funciton will always revert due to integer underflow in `userTokenAmount - refundCurrencyAmount` This cause DoS of this function/


### Root Cause

In [`launch.sol:355`](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L355) user's new token allocation is calculated by substrating `refundCurrencyAmount` from `userTokenAmount` which are in two different units. This leads to wrong calucation or integer underflow in when currencyToken has more decimals than `tokenDecimals`.
This leads to DoS of this function due to interger underflow or wrong calculations of user tokens.
Similar instances are presenten at [`launch.sol:361`](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L361) , [`launch.sol:368`](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L368) and [`launch.sol:374`](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L374).


### Internal Pre-conditions

.

### External Pre-conditions

this is always going to happen if currency token has more decimals than token decimals

### Attack Path

## Attack Path
let say launch token has 8 decimal and currency token has 18 decimals and tokenPriceBps is 1e18.
1. user `participate` in launch with 1000e8 token amount by paying 1000e18 currency tokens.
2. user call  `updateParticipation` with request.tokenAmount = 500e8 
    in this call :
        userTokenAmount = 1000e8
        newCurrencyAmount = 500e18
        prevInfo.currencyAmount = 1000e18
        refundCurrencyAmount = 1000e18 - 500e18 = 500e18

        this further checks if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser)
        this is always foing to revert as userTokenAmount -refundCurrencyAmount leads to integer underflow.


### Impact

DoS of updateParticipation if currencyDeciamls > tokenDecimals


### PoC

_No response_

### Mitigation

Update the `updateParticipation` function as per below : 

```diff
    function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        // Validate request is intended for this launch and unexpired
        _validateRequest(
            request.launchId, request.launchGroupId, request.chainId, request.requestExpiresAt, request.userAddress
        );
        // Validate launch group is open for participation
        LaunchGroupSettings memory settings = launchGroupSettings[request.launchGroupId];
        _validateTimestamp(settings);
        // Validate request signature is from signer role
        _validateRequestSignature(keccak256(abi.encode(request)), signature);
        // Validate payment currency is enabled for launch group
        uint256 tokenPriceBps = _validateCurrency(request.launchGroupId, request.currency);

        ParticipationInfo storage prevInfo = launchGroupParticipations[request.prevLaunchParticipationId];
        // If launch group finalizes at participation, the participation is considered complete and not updatable
        if (settings.finalizesAtParticipation || prevInfo.isFinalized) {
            revert ParticipationUpdatesNotAllowed(request.launchGroupId, request.prevLaunchParticipationId);
        }

        // Validate participation exists and user, requested currency match
        ParticipationInfo storage newInfo = launchGroupParticipations[request.newLaunchParticipationId];
        if (request.currency != prevInfo.currency) {
            revert CurrencyMismatch(prevInfo.currency, request.currency);
        }
        if (request.userId != prevInfo.userId) {
            revert UserIdMismatch(prevInfo.userId, request.userId);
        }

        // Calculate new payment amount in requested currency based on token price and requested token amount
        uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
        // Get total tokens requested for user for launch group
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        // If new requested token amount is less than old amount, handle refund
-       if (prevInfo.currencyAmount > newCurrencyAmount) {
+       if (prevInfo.tokenAmount > request.tokenAmount) {
           // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
-           if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
+           if (userTokenAmount - prevInfo.tokenAmount + request.tokenAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
-           userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
+           userTokens.set(request.userId, userTokenAmount - prevInfo.tokenAmount + request.tokenAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
-       } else if (newCurrencyAmount > prevInfo.currencyAmount) {
+       } else if (request.tokenAmount > prevInfo.tokenAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
-           if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
+           if (userTokenAmount - prevInfo.tokenAmount + request.tokenAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
-           userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
+           userTokens.set(request.userId, userTokenAmount - prevInfo.tokenAmount + request.tokenAmount);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }

        // Set participation details for user
        newInfo.currencyAmount = newCurrencyAmount;
        newInfo.currency = request.currency;
        newInfo.userAddress = msg.sender;
        newInfo.userId = request.userId;
        newInfo.tokenAmount = request.tokenAmount;
        // Reset previous participation info
        prevInfo.currencyAmount = 0;
        prevInfo.tokenAmount = 0;

        emit ParticipationUpdated(
            request.launchGroupId,
            request.newLaunchParticipationId,
            request.userId,
            msg.sender,
            request.tokenAmount,
            request.currency
        );
    }


```