Rich Pear Aphid

High

# Wrong Calculations and Checks in `Launch::updateParticipation` Function Breaks All Update Functionality

## Summary

The `Launch::updateParticipation` function contains critical flaws in its handling of token and currency amounts. These errors result in incorrect updates to user participation data, miscalculations of refunds, and invalid enforcement of allocation limits. The root cause stems from using currency amounts instead of token amounts in key calculations and checks, leading to a breakdown of core contract functionality.

## Vulnerability Details

### Root Cause
The function incorrectly uses currency values (e.g.,`refundCurrencyAmount`,  `additionalCurrencyAmount`) to adjust user token allocations stored in `userTokens`. This mismatch of units (currency vs. tokens) corrupts the tracking of user token balances and invalidates allocation limit checks.
https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L312

```solidity
 function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
       ...

        // Calculate new payment amount in requested currency based on token price and requested token amount
        uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
        // Get total tokens requested for user for launch group
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        // If new requested token amount is less than old amount, handle refund
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
@>          uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount; // wrong calculation
            // Validate user new requested token amount is greater than min token amount per user
            // @audit-high 
@>          if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) { // wrong check
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
@>          userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount); // wrong calculation
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
@>          if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) { // wrong check
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
@>          userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount); // wrong calculation
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }

        ...
    }
```

**checks.**

1. **Incorrect Refund Handling**
When a user reduces their participation:

- The refund amount is calculated in currency (`prevInfo.currencyAmount - newCurrencyAmount`).

- The code subtracts this currency value from the user's total token allocation (`userTokenAmount`), which is tracked in tokens.

- **Example**: If 1 token = 2 USDC, reducing from 100 tokens (200 USDC) to 50 tokens (100 USDC) would incorrectly subtract 100 USDC (currency) from the user's token balance (100 tokens), resulting in a nonsensical 0 tokens instead of 50.

2. **Invalid Allocation Checks**
The checks for `minTokenAmountPerUser` and `maxTokenAmountPerUser` use currency amounts instead of token amounts. This allows users to bypass allocation limits or be unjustly restricted.

## Impact

- **Critical Severity:** The miscalculations render the participation update mechanism unusable. Users may:
    - Receive incorrect refunds (overpaid or underpaid).
    - Exceed maximum token allocations, disrupting fair distribution.
    - Be erroneously blocked from valid updates due to corrupted checks.
- **Funds at Risk:** Mismanagement of token allocations could lead to financial losses for users or the protocol.
- **System Integrity:** Core functionality of the launch mechanism is compromised, undermining trust in the platform.

## Proof of Concept

1. **Setup:** Token price = 2 USDC per token. User A participates with 100 tokens (200 USDC).
2. **Update (Decrease):** User A reduces to 50 tokens (100 USDC).
    - Refund amount = 100 USDC (correct).
    - **Bug:** `userTokenAmount` is reduced by 100 (currency) instead of 50 (tokens).
    - **Result:** User's token balance becomes `100 tokens - 100 USDC = 0` tokens (incorrect; should be 50 tokens)
3. **Update (Increase):** User A increases to 150 tokens (300 USDC).
   - **Bug:** Allocation check uses currency (300 USDC) instead of token amount (150).
   - If `maxTokenAmountPerUser = 200 tokens`, the check incorrectly allows 300 USDC (150 tokens) but would block valid increases if the currency exceeds the limit.

## Proof of Code

To demonstrate the issue, we can analyze the test setup and execution that reveals the token/currency amount mismatch:

1. **Initial Test Setup** 
`LaunchTestBase.t.so::_setUpLaunch`

```solidity
// Users are given initial currency balances
currency.transfer(user1, 20000 * 10 ** launch.tokenDecimals());
currency.transfer(user2, 10000 * 10 ** launch.tokenDecimals());
```

2. **Price Configuration**
`LaunchTestBase.t.sol::_setupLaunchGroupWithStatus`

```solidity
// Launch token price is set to 2 currency token per token
CurrencyConfig memory currencyConfig =
    CurrencyConfig({tokenPriceBps: 2 * 10 ** currency.decimals(), isEnabled: true});
```

3. **Test Execution Results**
`Launch.UpdateParticipation.t.sol::test_UpdateParticipation_IncreaseAmount` 
`Launch.UpdateParticipation.t.sol::test_UpdateParticipation_DecreaseAmount`

Just add some console logs, in this tests, for example

```solidity
    function test_UpdateParticipation_IncreaseAmount() public {
        // Prepare update participation request
        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(2000);
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.startPrank(user1);
        uint256 updatedCurrencyAmount =
            _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, updateRequest.tokenAmount);
        currency.approve(address(launch), updatedCurrencyAmount);

        // Expect ParticipationUpdated event
        vm.expectEmit();
        emit ParticipationUpdated(
            updateRequest.launchGroupId,
            updateRequest.newLaunchParticipationId,
            testUserId,
            user1,
            updateRequest.tokenAmount,
            address(currency)
        );

        console.log("updateRequest.tokenAmount", updateRequest.tokenAmount);
        console.log("updatedCurrencyAmount", updatedCurrencyAmount);

        // Update participation
        launch.updateParticipation(updateRequest, updateSignature);

        // Verify update
        ParticipationInfo memory newInfo = launch.getParticipationInfo(updateRequest.newLaunchParticipationId);

        console.log("newInfo.tokenAmount", newInfo.tokenAmount);
        console.log("newInfo.currencyAmount", newInfo.currencyAmount);

      ...
    }
```

The test logs from `test_UpdateParticipation_IncreaseAmount` reveal the token/currency conversion:
```solidity
Logs:
  updateRequest.tokenAmount 2000000000000000000000    // 2000 tokens
  updatedCurrencyAmount 4000000000000000000000       // 4000 currency tokens (2x tokens)
  newInfo.tokenAmount 2000000000000000000000         // Stored: 2000 tokens
  newInfo.currencyAmount 4000000000000000000000      // Stored: 4000 currency tokens
```

## Recommendations

Use Token Amounts for Allocation Adjustments
Replace currency-based calculations with token amounts in critical sections:

```diff
 function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {

       ...

        // Calculate new payment amount in requested currency based on token price and requested token amount
        uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
        // Get total tokens requested for user for launch group
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        // If new requested token amount is less than old amount, handle refund
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
+           uint256 refundTokenAmount = prevInfo.tokenAmount - request.tokenAmount;
            // Validate user new requested token amount is greater than min token amount per user
-           if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) { 
+           if (userTokenAmount - refundTokenAmount < settings.minTokenAmountPerUser) { 
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
-           userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
+           userTokens.set(request.userId, userTokenAmount - refundTokenAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount; 
            // Validate user new requested token amount is within launch group user allocation limits
-           if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
+           if (userTokenAmount + request.tokenAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
-           userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
+           userTokens.set(request.userId, userTokenAmount + request.tokenAmount);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }

       ...

```

## Conclusion

The identified flaws critically undermine the integrity of the participation update mechanism. Immediate remediation is required to ensure accurate tracking of user allocations and prevent financial losses. Post-fix, rigorous testing should be conducted to verify all token/currency conversions and allocation checks.
