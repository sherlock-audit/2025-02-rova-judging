Clumsy Pear Cormorant

High

# Incorrectly using delta currency amount instead of delta token amounts in `updateParticipaion()`

### Summary

Incorrectly using the delta currency amount (difference between the previous participation amount, and the updated participation amount) instead of delta token amount will lead to unexpected reverts and incorrect values set for the token amount, the user should receive for his participation.

### Root Cause

In [Launch.sol](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol), two validation checks in [updateParticipation()](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L312) incorrectly compare token limits against currency amounts:

Minimum Token Check (Line 355):
```solidity
if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser)
```
Maximum Token Check (Line 368):
```solidity
if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser)
```
The validation compares mismatched units - token limits against currency amounts. This creates an invalid comparison between different decimals and exchange rates, leading to incorrect validation results and possible false reverts.

The checks should instead compare token amounts with token amounts, requiring conversion of currency deltas to their token equivalents before comparison.

Moreover, when updating the user's total token amount,  again the currency deltas are used instead of their token equivalents.
Old currency amount is greater than new currency amount (Line 361):
```solidity
           userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
```
New currency amount is greater than old currency amount (Line 374):
```solidity
 userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
```

### Internal Pre-conditions

1. Token price shouldn't be 1:1 with the currency
2. The launch should not be finalized on participation

### External Pre-conditions

N/A

### Attack Path

1. User participates in a launch group
2. User updates his participation via `updateParticipation()` with correct values
2.1 The checks doesn't pass, and we receive revert even though the values are correct
2.2 The checks pass, the user receive more/less(depending on the price of the tokens) than he should

### Impact

The user will not be able to update his participation, even though he has provided correct values due to unexpected reverts, or he will be able to update his participation but will receive more/less(depending on the price of the tokens) than he should, because of the incorrect set.

### PoC

```solidity
   function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        // Validate request is intended for this launch and unexpired
        _validateRequest(
            request.launchId, request.launchGroupId, request.chainId, request.requestExpiresAt, request.userAddress
        );
        // Validate launch group is open for participation
        LaunchGroupSettings memory settings = launchGroupSettings[request.launchGroupId];
        _validateTimestamp(settings);
        // Validate request signature is from signer role
        _validateRequestSignature(keccak256(abi.encode(request)), signature);
        // Validate payment currency is enabled for launch group
        uint256 tokenPriceBps = _validateCurrency(request.launchGroupId, request.currency);

        ParticipationInfo storage prevInfo = launchGroupParticipations[request.prevLaunchParticipationId];
        // If launch group finalizes at participation, the participation is considered complete and not updatable
        if (settings.finalizesAtParticipation || prevInfo.isFinalized) {
            revert ParticipationUpdatesNotAllowed(request.launchGroupId, request.prevLaunchParticipationId);
        }

        // Validate participation exists and user, requested currency match
        ParticipationInfo storage newInfo = launchGroupParticipations[request.newLaunchParticipationId];
        if (request.currency != prevInfo.currency) {
            revert CurrencyMismatch(prevInfo.currency, request.currency);
        }
        if (request.userId != prevInfo.userId) {
            revert UserIdMismatch(prevInfo.userId, request.userId);
        }

        // Calculate new payment amount in requested currency based on token price and requested token amount
        uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
        // Get total tokens requested for user for launch group
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        // If new requested token amount is less than old amount, handle refund
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
@>          if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
@>         userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
@>          if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
@>         userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }

        // Set participation details for user
        newInfo.currencyAmount = newCurrencyAmount;
        newInfo.currency = request.currency;
        newInfo.userAddress = msg.sender;
        newInfo.userId = request.userId;
        newInfo.tokenAmount = request.tokenAmount;
        // Reset previous participation info
        prevInfo.currencyAmount = 0;
        prevInfo.tokenAmount = 0;

        emit ParticipationUpdated(
            request.launchGroupId,
            request.newLaunchParticipationId,
            request.userId,
            msg.sender,
            request.tokenAmount,
            request.currency
        );
    }
```
It's visible even by the variable names that there is mismatch in the currencies.

### Mitigation

```diff
- if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) 
+ if (userTokenAmount - prevInfo.tokenAmount +  request.tokenAmount < settings.minTokenAmountPerUser) 
...
- userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
+ userTokens.set(request.userId, userTokenAmount - prevInfo.tokenAmount + request.tokenAmount);
...
- if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser)
+ if (userTokenAmount - prevInfo.tokenAmount +  request.tokenAmount > settings.maxTokenAmountPerUser)
...
- userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
+ userTokens.set(request.userId, userTokenAmount - prevInfo.tokenAmount + request.tokenAmount);
```