Perfect Syrup Elk

Medium

# Wrong calculation inside updateParticipation leads to incorrect _userTokensByLaunchGroup amount

### Summary

Function `updateParticipation` incorrectly calculates `userTokensByLaunchGroup` which leads user, reaching `maxTokenAllocationPerUser` quicker or allowing user to **exceed** the `maxTokenAllocationPerUser`.

### Root Cause

Function `updateParticipation` checks and modifies mapping `_userTokensByLaunchGroup` (which is used to determine whether user exceeded `maxTokenAllocationPerUser`) for specific user. This mapping stores information how many sale tokens user "bought". For example `_userTokensByLaunchGroup` for specific userId inside `participate` is incremented by `request.tokenAmount` which represents how many sale tokens user wants to buy. However in `updateParticipation` `_userTokensByLaunchGroup` for specific userId is incremented/decremented by additionalCurrencyAmount/refundCurrencyAmount which represent not amount of sale token, but amount of currency token that user needs to deposit/gets refunded. This leads to incorrect calculation which can benefit or harm the user depending on the price of currency token in sale token.

links to mentioned code:
https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L293
https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L355-L361
https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L368-L374

### Internal Pre-conditions

Active launch group that does not finalize on participation has to be created on  the contract by admin


### External Pre-conditions

User has to have active participation position that does not finalize on participation and signature that allows them to call updateParticipation.

### Attack Path

1. Let's assume user has position that does not finalize on participation with tokenAmount = 1000. 
2. Price of sale token in currency token is 10 (1 saleToken = 10 currency Token).  
3. User requests signature from backend where new token amount is equal to 2000
4. User expects that their updated participation will hold 2000 sale tokens, they will pay additional 10000 currency token ((new token amount - token amount ) * 10 = 10_000)
5. User also expects that their registered amount of sale tokens in this launch group inside `_userTokensByLaunchGroup` will be equal to 2000
6. User calls updateParticipation providing signature and UpdateParticipationRequest struct
7. Call executes succesfully but user realizes that their registered amount of sale tokens in this launch group inside `_userTokensByLaunchGroup` is 11_000, not expected 2000

Additional Note: If saleToken is worth more than currency token, then  registered amount of sale tokens in this launch group inside `_userTokensByLaunchGroup` will be less than expected

### Impact

Internal accounting of `_userTokensByLaunchGroup`  is broken leading to **users** exceeding `maxTokenAllocationPerUser` too quickly or too slowly.

### PoC

The POC shows 2 scenarios, where `_userTokensByLaunchGroup`  is greater and smaller than expected. POC shows scenario where user increases their position, but the incorrect calculation exists also when users decreases their position.

```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {IERC20Errors} from "@openzeppelin/contracts/interfaces/draft-IERC6093.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {Test} from "forge-std/Test.sol";
import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {LaunchGroupSettings, LaunchGroupStatus, ParticipationRequest, UpdateParticipationRequest, ParticipationInfo, CurrencyConfig} from "../src/Types.sol";
import {console} from "lib/forge-std/src/console.sol";

contract POCincorrectUserAmountPerLaunchGroup is Test, Launch, LaunchTestBase {
    LaunchGroupSettings public settings;
    ParticipationRequest public originalParticipationRequest;

    function _setUp1() public {
        _setUpLaunch();

        // Setup initial participation
        _setupLaunchGroup2();

        vm.startPrank(admin.addr);
        currency.transfer(user1, 20000 * 10 ** launch.tokenDecimals());
        vm.stopPrank();

        originalParticipationRequest = _createParticipationRequest();
        bytes memory signature = _signRequest(
            abi.encode(originalParticipationRequest)
        );

        vm.startPrank(user1);
        currency.approve(
            address(launch),
            _getCurrencyAmount(
                originalParticipationRequest.launchGroupId,
                originalParticipationRequest.currency,
                originalParticipationRequest.tokenAmount
            )
        ); /// request for 1000 tokens
        launch.participate(originalParticipationRequest, signature);

        vm.stopPrank();
    }

    function _setUp2() public {
        _setUpLaunch();

        // Setup initial participation
        _setupLaunchGroup3();

        vm.startPrank(admin.addr);
        currency.transfer(user1, 20000 * 10 ** launch.tokenDecimals());
        vm.stopPrank();

        originalParticipationRequest = _createParticipationRequest();
        bytes memory signature = _signRequest(
            abi.encode(originalParticipationRequest)
        );

        vm.startPrank(user1);
        currency.approve(
            address(launch),
            _getCurrencyAmount(
                originalParticipationRequest.launchGroupId,
                originalParticipationRequest.currency,
                originalParticipationRequest.tokenAmount
            )
        ); /// request for 1000 tokens
        launch.participate(originalParticipationRequest, signature);

        vm.stopPrank();
    }

    function test_UpdateParticipation_userTokensByLaunchGroup_is_more_than_expected()
        public
    {
        _setUp1();
        // 1 sale token = 10 currency token
        // Prepare update participation request
        // new token amount
        uint256 newTokenAmount = 2000;
        UpdateParticipationRequest
            memory updateRequest = _createUpdateParticipationRequest(
                newTokenAmount
            ); /// request that updates tokens sold for user in this launch group to 2000
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.startPrank(user1);
        uint256 updatedCurrencyAmount = _getCurrencyAmount(
            updateRequest.launchGroupId,
            updateRequest.currency,
            updateRequest.tokenAmount
        );
        currency.approve(address(launch), updatedCurrencyAmount);

        uint256 userTokensByLaunchGroup = launch.getUserTokensByLaunchGroup(
            updateRequest.launchGroupId,
            updateRequest.userId
        );

        // Update participation
        // User tries to update their participation to 2000 tokens, but contract registers userTokensByLaunchGroup as 11_000 tokens
        //due to the fact that each sale token is worth 10 curr tokens and function increases/decreases userTokensByLaunchGroup by currency tokens not amount of sale tokens
        launch.updateParticipation(updateRequest, updateSignature);

        uint256 userTokensByLaunchGroupAfterUpdate = launch
            .getUserTokensByLaunchGroup(
                updateRequest.launchGroupId,
                updateRequest.userId
            );

        assert(userTokensByLaunchGroup == 1000 * 10 ** 18); //Amount of userTokensByLaunchGroup before update
        assert(userTokensByLaunchGroupAfterUpdate != newTokenAmount * 10 ** 18); //userTokensByLaunchGroupAfterUpdate is diffrent than expected which should be 2000
        assert(
            userTokensByLaunchGroupAfterUpdate ==
                userTokensByLaunchGroup +
                    10 *
                    (newTokenAmount * 10 ** 18 - userTokensByLaunchGroup)
        ); // userTokensByLaunchGroupAfterUpdated is actually equal to initial sale token amount + amount of additional currency token that had to be transfered to the contract

        // Verify update
        ParticipationInfo memory newInfo = launch.getParticipationInfo(
            updateRequest.newLaunchParticipationId
        );
        _verifyParticipationInfo(newInfo, updateRequest);
        ParticipationInfo memory oldInfo = launch.getParticipationInfo(
            updateRequest.prevLaunchParticipationId
        );
        assertEq(oldInfo.currencyAmount, 0);
        assertEq(oldInfo.tokenAmount, 0);

        // Verify total unique participants by launch group
        assertEq(
            launch.getNumUniqueParticipantsByLaunchGroup(testLaunchGroupId),
            1
        );

        vm.stopPrank();
    }

    function test_UpdateParticipation_userTokensByLaunchGroup_is_less_than_expected()
        public
    {
        _setUp2(); //setup where 1 sale token = 0.5 currency token

        // Prepare update participation request
        // new token amount
        uint256 newTokenAmount = 2000;
        UpdateParticipationRequest
            memory updateRequest = _createUpdateParticipationRequest(
                newTokenAmount
            ); /// request that updates tokens sold for user in this launch group to 2000
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.startPrank(user1);
        uint256 updatedCurrencyAmount = _getCurrencyAmount(
            updateRequest.launchGroupId,
            updateRequest.currency,
            updateRequest.tokenAmount
        );
        currency.approve(address(launch), updatedCurrencyAmount);

        uint256 userTokensByLaunchGroup = launch.getUserTokensByLaunchGroup(
            updateRequest.launchGroupId,
            updateRequest.userId
        );

        // Update participation
        // User tries to update their participation to 2000 tokens, but contract registers userTokensByLaunchGroup as 15000 tokens
        //due to the fact that each sale token is worth 0.5 curr tokens and function increases/decreases userTokensByLaunchGroup by currency tokens not amount of sale tokens
        launch.updateParticipation(updateRequest, updateSignature);

        uint256 userTokensByLaunchGroupAfterUpdate = launch
            .getUserTokensByLaunchGroup(
                updateRequest.launchGroupId,
                updateRequest.userId
            );

        assert(userTokensByLaunchGroup == 1000 * 10 ** 18); //Amount of userTokensByLaunchGroup before update
        assert(userTokensByLaunchGroupAfterUpdate != newTokenAmount * 10 ** 18); //userTokensByLaunchGroupAfterUpdate is diffrent than expected which should be 2000
        assert(
            userTokensByLaunchGroupAfterUpdate ==
                userTokensByLaunchGroup +
                    (5 *
                        (newTokenAmount * 10 ** 18 - userTokensByLaunchGroup)) /
                    10
        ); // userTokensByLaunchGroupAfterUpdated is actually equal to initial sale token amount + amount of additional currency token that had to be transfered to the contract
        // Verify update
        ParticipationInfo memory newInfo = launch.getParticipationInfo(
            updateRequest.newLaunchParticipationId
        );
        _verifyParticipationInfo(newInfo, updateRequest);
        ParticipationInfo memory oldInfo = launch.getParticipationInfo(
            updateRequest.prevLaunchParticipationId
        );
        assertEq(oldInfo.currencyAmount, 0);
        assertEq(oldInfo.tokenAmount, 0);

        // Verify total unique participants by launch group
        assertEq(
            launch.getNumUniqueParticipantsByLaunchGroup(testLaunchGroupId),
            1
        );

        vm.stopPrank();
    }

    function _setupLaunchGroup2()
        internal
        returns (LaunchGroupSettings memory)
    {
        return
            _setupLaunchGroupWithStatus2(
                testLaunchGroupId,
                LaunchGroupStatus.ACTIVE
            );
    }

    function _setupLaunchGroupWithStatus2(
        bytes32 launchGroupId,
        LaunchGroupStatus status
    ) internal returns (LaunchGroupSettings memory) {
        CurrencyConfig memory currencyConfig = CurrencyConfig({
            tokenPriceBps: 10 * 10 ** currency.decimals(), //One Sale token is Worth 10 Currency Token
            isEnabled: true
        });
        LaunchGroupSettings memory _settings = LaunchGroupSettings({
            finalizesAtParticipation: false,
            startsAt: block.timestamp,
            endsAt: block.timestamp + 1 days,
            maxTokenAllocation: 21000 * 10 ** launch.tokenDecimals(),
            minTokenAmountPerUser: 500 * 10 ** launch.tokenDecimals(),
            maxTokenAmountPerUser: 11000 * 10 ** launch.tokenDecimals(),
            status: status
        });
        vm.startPrank(manager);
        launch.createLaunchGroup(
            launchGroupId,
            address(currency),
            currencyConfig,
            _settings
        );
        vm.stopPrank();
        return _settings;
    }

    function _setupLaunchGroup3()
        internal
        returns (LaunchGroupSettings memory)
    {
        return
            _setupLaunchGroupWithStatus3(
                testLaunchGroupId,
                LaunchGroupStatus.ACTIVE
            );
    }

    function _setupLaunchGroupWithStatus3(
        bytes32 launchGroupId,
        LaunchGroupStatus status
    ) internal returns (LaunchGroupSettings memory) {
        CurrencyConfig memory currencyConfig = CurrencyConfig({
            tokenPriceBps: 5 * 10 ** (currency.decimals() - 1), //One Sale token is Worth 0.5 Currency Token
            isEnabled: true
        });
        LaunchGroupSettings memory _settings = LaunchGroupSettings({
            finalizesAtParticipation: false,
            startsAt: block.timestamp,
            endsAt: block.timestamp + 1 days,
            maxTokenAllocation: 21000 * 10 ** launch.tokenDecimals(),
            minTokenAmountPerUser: 500 * 10 ** launch.tokenDecimals(),
            maxTokenAmountPerUser: 11000 * 10 ** launch.tokenDecimals(),
            status: status
        });
        vm.startPrank(manager);
        launch.createLaunchGroup(
            launchGroupId,
            address(currency),
            currencyConfig,
            _settings
        );
        vm.stopPrank();
        return _settings;
    }

    function _verifyParticipationInfo(
        ParticipationInfo memory info,
        UpdateParticipationRequest memory updateRequest
    ) internal view {
        assertEq(info.userAddress, user1);
        assertEq(info.userId, testUserId);
        assertEq(info.tokenAmount, updateRequest.tokenAmount);
        assertEq(
            info.currencyAmount,
            _getCurrencyAmount(
                updateRequest.launchGroupId,
                updateRequest.currency,
                updateRequest.tokenAmount
            )
        );
        assertEq(info.currency, address(currency));
        assertEq(info.isFinalized, false);
    }

    function _createUpdateParticipationRequest(
        uint256 newTokenAmount
    ) internal view returns (UpdateParticipationRequest memory) {
        uint256 launchTokenDecimals = launch.tokenDecimals();
        return
            UpdateParticipationRequest({
                chainId: block.chainid,
                launchId: testLaunchId,
                launchGroupId: testLaunchGroupId,
                prevLaunchParticipationId: testLaunchParticipationId,
                newLaunchParticipationId: "newLaunchParticipationId",
                userId: testUserId,
                userAddress: user1,
                tokenAmount: newTokenAmount * 10 ** launchTokenDecimals,
                currency: address(currency),
                requestExpiresAt: block.timestamp + 1 hours
            });
    }
}

```

### Mitigation

Use delta of sale token amount when updating `_userTokensByLaunchGroup`  not currency amount