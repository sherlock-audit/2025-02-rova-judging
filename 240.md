Blurry Rose Alpaca

Medium

# Cancelled Participation Causes Batch Refund DoS in Launch Contract

### Summary



Missing validation in batchRefund will cause denial of service for launch participants as any cancelled participation in the batch will cause the entire refund operation to revert, preventing valid participants from receiving their refunds. This occurs because the _processRefund function strictly reverts on cancelled participations instead of skipping them.

### Root Cause

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L613-L615

In Launch.sol the `_processRefund` function contains an overly strict validation check:
```solidity
function _processRefund(bytes32 launchGroupId, bytes32 launchParticipationId, ParticipationInfo storage info) private {
    // If participation is finalized or has no currency amount or no token amount (cancelled), revert
    if (info.isFinalized || info.currencyAmount == 0 || info.tokenAmount == 0) {
        revert InvalidRefundRequest(launchParticipationId, info.userId);
    }
    ...
}
```
The function reverts when encountering cancelled participations (those with currencyAmount = 0 and tokenAmount = 0) instead of gracefully skipping them.

### Internal Pre-conditions

- Launch group must be in COMPLETED status
- At least one user must have cancelled their participation via cancelParticipation()
- Operator must have OPERATOR_ROLE to execute batchRefund
- Multiple users must have unfinalized participations pending refund

### External Pre-conditions

None

### Attack Path

- Launch group is created and active
- Multiple users participate in the launch group
- User A cancels their participation using `cancelParticipation()`
  - Sets their `currencyAmount` and `tokenAmount` to 0
- Launch group is marked as `COMPLETED`
- Operator attempts to process batch refunds including User A's cancelled participation
- `_processRefund` encounters User A's cancelled participation and reverts
- All other legitimate refunds in the batch fail
- Users must claim refunds individually or wait for a filtered batch

### Impact

Broken Batch Functionality: Operators cannot efficiently process refunds in batches if there is a cancelled participation in the group.
It affects protocol operational efficiency rather than user funds because users have a working alternative through `claimRefund`

### PoC

modify the setup in the `LaunchBatchRefund.t.sol`  to allow 4 users to participate. 
```solidity 
 function setUp() public {
        _setUpLaunch();

        settings = _setupLaunchGroup();

        // Setup multiple participations
        participationIds = new bytes32[](4);
        participationIds[0] = bytes32(uint256(1));
        participationIds[1] = bytes32(uint256(2));
        participationIds[2] = bytes32(uint256(3));
        participationIds[3] = bytes32(uint256(4));

        users = new address[](4);
        users[0] = user1;
        users[1] = user2;
        users[2] = user3;
        users[3] = user4;

        requests = _setupParticipations(participationIds, users);
    }

```

Also make sure to add user3 and user4 to the `LaunchTestBase.t.sol` and mint them some tokens.
```solidity
    address public user3 = address(7);
    address public user4 = address(8);

<img width="617" alt="Image" src="https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/5be1f8d8-7783-4aff-8127-d8d21d5bdfd9" />

  function _setUpLaunch() public {

                        ....

   currency.transfer(user3, 1000 * 10 ** launch.tokenDecimals());
        currency.transfer(user4, 1000 * 10 ** launch.tokenDecimals());

                      ....
}
```
The main test
run forge test --match-test test_BatchRefund_RevertOnCancelledParticipation -vvvv

```solidity
function test_BatchRefund_RevertOnCancelledParticipation() public {
        // SETUP: Track initial balances
        uint256[] memory initialBalances = new uint256[](4);
        for(uint i = 0; i < 4; i++) {
            ParticipationInfo memory info = launch.getParticipationInfo(requests[i].launchParticipationId);
            console.log("User", i+1, "initial participation amount:", info.currencyAmount);
            initialBalances[i] = currency.balanceOf(requests[i].userAddress);
            console.log("User", i+1, "initial wallet balance:", initialBalances[i]);
        }
    
        // User2 cancels their participation
        vm.startPrank(requests[1].userAddress);
        CancelParticipationRequest memory cancelRequest = _createCancelParticipationRequest();
        cancelRequest.userId = requests[1].userId;
        cancelRequest.userAddress = requests[1].userAddress;
        cancelRequest.launchParticipationId = requests[1].launchParticipationId;
        bytes memory cancelSignature = _signRequest(abi.encode(cancelRequest));
        launch.cancelParticipation(cancelRequest, cancelSignature);
        vm.stopPrank();
    
        // Verify User2's cancellation
        console.log("=== After User2 Cancellation ===");
        ParticipationInfo memory cancelledInfo = launch.getParticipationInfo(requests[1].launchParticipationId);
        assertEq(cancelledInfo.currencyAmount, 0, "Cancelled participation should have 0 currency amount");
        
        // Verify other users' participations unchanged
        for(uint i = 0; i < 4; i++) {
            if(i != 1) { // Skip User2
                ParticipationInfo memory info = launch.getParticipationInfo(requests[i].launchParticipationId);
                assertEq(info.currencyAmount, 1000 * 10**18, "Non-cancelled participations should be unchanged");
            }
        }
    
        // Complete launch group
        vm.startPrank(manager);
        launch.setLaunchGroupStatus(testLaunchGroupId, LaunchGroupStatus.COMPLETED);
        vm.stopPrank();
    
        // Try batch refund - should revert due to User2's cancelled participation
        vm.startPrank(operator);
        vm.expectRevert(
            abi.encodeWithSelector(
                InvalidRefundRequest.selector,
                requests[1].launchParticipationId,
                requests[1].userId
            )
        );
        launch.batchRefund(testLaunchGroupId, participationIds);
        vm.stopPrank();
    
        // Verify all participations remain unchanged
        console.log("=== Final State ===");
        for(uint i = 0; i < 4; i++) {
            ParticipationInfo memory info = launch.getParticipationInfo(requests[i].launchParticipationId);
            uint256 currentBalance = currency.balanceOf(requests[i].userAddress);
            
            if(i == 1) { // User2 (cancelled)
                assertEq(info.currencyAmount, 0, "Cancelled participation should remain at 0");
                assertEq(currentBalance, initialBalances[i] + 1000 * 10**18, "Cancelled user should have received refund");
            } else {
                assertEq(info.currencyAmount, 1000 * 10**18, "Other participations should remain unchanged");
                assertEq(currentBalance, initialBalances[i], "Other users' balances should not change");
            }
            console.log("User", i+1, "final participation amount:", info.currencyAmount);
            console.log("User", i+1, "final wallet balance:", currentBalance);
        }
    }
```

### Mitigation

Filter out cancelled participation in the `batchRefund` before `_processFund` is called
```solidity
function batchRefund(bytes32 launchGroupId, bytes32[] calldata launchParticipationIds)
    external
    onlyRole(OPERATOR_ROLE)
    nonReentrant
    whenNotPaused
    onlyLaunchGroupStatus(launchGroupId, LaunchGroupStatus.COMPLETED)
{
    for (uint256 i = 0; i < launchParticipationIds.length; i++) {
        ParticipationInfo storage info = launchGroupParticipations[launchParticipationIds[i]];
        
        // Skip cancelled participations rather than reverting the entire batch
        if (info.currencyAmount == 0 || info.tokenAmount == 0) {
            continue;
        }
        
        _processRefund(launchGroupId, launchParticipationIds[i], info);
    }
}
```