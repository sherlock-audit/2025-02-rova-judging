Lively Flint Scorpion

Medium

# Incorrect use of the `refundCurrencyAmount` variable in the `Launch.updateParticipation` function

### Summary

The `Launch.updateParticipation` function misuses the `refundCurrencyAmount` variable as the protocol token amount in some statements.As a result, this causes incorrect check of the token amount per user or incorrect track of the `userTokens` variable.

### Root Cause

In the `Launch.updateParticipation` function, it validates new requested token amount is greater than min token amount per user from [L355](https://github.com/sherlock-audit/2025-02-rova/tree/main/rova-contracts/src/Launch.sol#L355) and sets the `userTokens` variable with `userTokenAmount - refundCurrencyAmount`.

```solidity
L349:       (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
            [...]
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
L355:       if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
L361:       userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
```

Here, `userTokenAmount` is the launch token amount and `refundCurrencyAmount` is the currency token amount.
Thus, these variables track the different tokens' amount and this causes the DoS of the `updateParticipation` function or incorrect track of the `userTokens` variable.

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

Let's consider the following scenario:
- project token and currency token's decimal: 18
- Intended price conversion (P): For “1 project token = 2 currency token”
- Alice participates with `request.tokenAmount = 1000e18` in a launch group of auction mode.
currencyAmount = (tokenPriceBps * tokenAmount) / (10^PTD) = 2000e18
Alice's `userTokens` is 1000e18
- Alice is going to update the requested token amount to 499e18.
    - userTokenAmount = 1000e18 from L349.
    - refundCurrencyAmount = 1002e18.
    - userTokenAmount - refundCurrencyAmount = 1000e18 - 1002e18 = -2e18 < 0
As a result, this will revert by the underflow error.

### Impact

- This causes the incorrect track of the `userTokens` variable.
- This causes incorrect check of the token amount per user.

### PoC

Change the following code to test
```diff
File: test\LaunchTestBase.t.sol
    function _setupLaunchGroupWithStatus(bytes32 launchGroupId, LaunchGroupStatus status)
        internal
        returns (LaunchGroupSettings memory)
    {
        CurrencyConfig memory currencyConfig =
-           CurrencyConfig({tokenPriceBps: 1 * 10 ** currency.decimals(), isEnabled: true});
+           CurrencyConfig({tokenPriceBps: 2 * 10 ** currency.decimals(), isEnabled: true});

File: test\Launch.UpdateParticipation.t.sol
    function test_UpdateParticipation_DecreaseAmount() public {
        // Prepare update participation request
-       UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(500);
+       UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(499);
```

Then, run the following command:
```bash
    forge test --match-test test_UpdateParticipation_DecreaseAmount -vvvv
```

The result is as following:
```bash
    │   ├─ [48982] Launch::updateParticipation(UpdateParticipationRequest({ chainId: 31337 [3.133e4], launchId: 0x636978663032796d3030303030316236366d34356165346b3800000000000000, launchGroupId: 0x6368373267736233323030303075646f636c333633656f667900000000000000, prevLaunchParticipationId: 0x636d366f32736c646930303030336237346661636d357a396e00000000000000, newLaunchParticipationId: 0x6e65774c61756e636850617274696369706174696f6e49640000000000000000, userId: 0x636d366f32746d31333030303033623734647373733173377100000000000000, userAddress: 0x0000000000000000000000000000000000000005, tokenAmount: 499000000000000000000 [4.99e20], currency: 0xDDd9A038D57372934f1b9c52bd8621F5ED4268DF, requestExpiresAt: 3601 }), 0x85ce7c0d45d2b1514733a58275916ce6a0dafeb6866ca5c64a3131615485974976cf127972c280d67a5bef857b944be4a605d88df79c1ceac0ca3fec09b783751b) [delegatecall]
    │   │   ├─ [3000] PRECOMPILES::ecrecover(0x3778dcdce1f09c3a4cb195b3bb73152ef6749ba3d7c1bd455cc952ced278efde, 27, 60522435531298307581028977126271170402846302992455214114457417946658211469129, 53738780980907963595266853745282636167724458898415530521338597239301739086709) [staticcall]
    │   │   │   └─ ← [Return] 0x000000000000000000000000aa10a84ce7d9ae517a52c6d5ca153b369af99ecf
    │   │   └─ ← [Revert] panic: arithmetic underflow or overflow (0x11)
    │   └─ ← [Revert] panic: arithmetic underflow or overflow (0x11)
    └─ ← [Revert] log != expected log

Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 5.37ms (999.80µs CPU time)
```

### Mitigation

```diff
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
+           uint256 refundTokenAmount = prevInfo.tokenAmount - request.tokenAmount;
            // Validate user new requested token amount is greater than min token amount per user
-           if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
+           if (userTokenAmount - refundTokenAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
-           userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
+           userTokens.set(request.userId, userTokenAmount - refundTokenAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
+           uint256 additionalTokenAmount = request.tokenAmount - prevInfo.tokenAmount;
            // Validate user new requested token amount is within launch group user allocation limits
-           if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
+           if (userTokenAmount + additionalTokenAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
-           userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
+           userTokens.set(request.userId, userTokenAmount + additionalTokenAmount);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }
```
