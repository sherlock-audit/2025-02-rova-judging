Keen Tawny Terrier

High

# `_userTokensByLaunchGroup` is not correctly updated in the `Launch::updateParticipation` function

### Summary

The `_userTokensByLaunchGroup` variable is not correctly updated in the `Launch::updateParticipation` function. Specifically, the function incorrectly sets `_userTokensByLaunchGroup[request.launchGroupId]` for `request.userId`, leading to miscalculations in the recorded token amount. This miscalculation occurs due to the improper subtraction and addition of `refundCurrencyAmount` and `additionalCurrencyAmount`, which are in currency units rather than token units. As a result, users can end up with an incorrect number of launch tokens, potentially leading to excess tokens beyond what they have paid for.

### Root Cause

The incorrect calculations arise due to a mismatch between currency and token amounts when updating `_userTokensByLaunchGroup`. The function incorrectly performs:
- [`userTokenAmount - refundCurrencyAmount` when `prevInfo.currencyAmount > newCurrencyAmount`](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L361)
- [`userTokenAmount + additionalCurrencyAmount` when `newCurrencyAmount > prevInfo.currencyAmount`](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L374)

Since `refundCurrencyAmount` and `additionalCurrencyAmount` are denominated in currency tokens, while `userTokenAmount` represents launch tokens, this leads to incorrect values in `_userTokensByLaunchGroup`. The validation checks for minimum and maximum token amounts also inherit this flaw, leading to further inconsistencies.

### Internal Pre-conditions

A user who participated in the token launch have a signed `UpdateParticipationRequest` request to update participation.

### Attack Path

Attack 1: Price of launch token to currency token is greater than 1
1. A user participates in the token launch by calling the `participate` function, receiving an initial allocation of launch tokens.
2. The user then calls the `updateParticipation` function with an increased `tokenAmount`.
3. Due to the incorrect update logic, `_userTokensByLaunchGroup` is set incorrectly, leading to an over-allocation of launch tokens.
4. The user ends up with more launch tokens allocated than they actually paid for.
5. This could result in an unfair advantage and potential loss for the project owners or other participants.

Attack 2: Price of launch token to currency token is less than 1
1. A user participates in the token launch by calling the `participate` function, receiving an initial allocation of launch tokens.
2. The user then calls the `updateParticipation` function with a decreased `tokenAmount`.
3. Due to the incorrect refund calculation, the user receives a refund for more tokens than they should.
4. Despite the refund, their launch token allocation is only slightly reduced.
5. The user effectively pays less than the required amount for the launch tokens, creating an unfair financial advantage and potential economic imbalance in the contract.

### Impact

- Users can exploit this issue to receive more launch tokens than they should.
- Over-allocation of launch tokens can lead to economic imbalances in the contract.
- The project's tokenomics may be disrupted due to excess issuance of launch tokens.
- This could result in financial losses for other participants or project owners.

### PoC

**Example 1**
- Launch token to currency token price: 1 launch token = 100 currency tokens
- Update from 1 launch token to 20
- Initial values:
  ```solidity
  userTokenAmount = 1
  prevInfo.currencyAmount = 100
  request.tokenAmount = 20
  newCurrencyAmount = 2000
  additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount = 1900
  userTokens.set(request.userId, 1 + 1900);
  ```
- Result: User paid for 19 launch tokens (20 in total), but `userTokens` was set to 1901.

**Example 2**
- Launch token to currency token price: 1 launch token = 0.01 currency tokens
- Update from 100 launch tokens to 2000
- Initial values:
  ```solidity
  userTokenAmount = 100
  prevInfo.currencyAmount = 1
  request.tokenAmount = 2000
  newCurrencyAmount = 20
  additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount = 19
  userTokens.set(request.userId, 100 + 19);
  ```
- Result: User paid for 1900 launch tokens (2000 in total), but `userTokens` was set to 119.

**Example 3**
- Launch token to currency token price: 1 launch token = 0.01 currency tokens
- Update from 100 launch tokens to 1
- Initial values:
  ```solidity
  userTokenAmount = 100
  prevInfo.currencyAmount = 1
  request.tokenAmount = 1
  newCurrencyAmount = 0.01
  refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount = 0.99
  userTokens.set(request.userId, 100 - 0.99);
  ```
- Result: User got a refund for 99 launch tokens, but `userTokens` remained at 99.01.

### Mitigation

To fix this issue, update `_userTokensByLaunchGroup` with the correct values and adjust the min/max token amount per user checks accordingly:

```diff
uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
// Get total tokens requested for user for launch group
EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
(, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
// If new requested token amount is less than old amount, handle refund
if (prevInfo.currencyAmount > newCurrencyAmount) {
    // Calculate refund amount
    uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
    // Validate user new requested token amount is greater than min token amount per user
-   if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
+   if (userTokenAmount - prevInfo.tokenAmount + request.tokenAmount < settings.minTokenAmountPerUser) {
        revert MinUserTokenAllocationNotReached(
            request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
        );
    }
    // Update total tokens requested for user for launch group
-   userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
+   userTokens.set(request.userId, userTokenAmount - prevInfo.tokenAmount + request.tokenAmount);
    // Transfer payment currency from contract to user
    IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
} else if (newCurrencyAmount > prevInfo.currencyAmount) {
    // Calculate additional payment amount
    uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
    // Validate user new requested token amount is within launch group user allocation limits
-   if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
+   if (userTokenAmount - prevInfo.tokenAmount + request.tokenAmount > settings.maxTokenAmountPerUser) {
        revert MaxUserTokenAllocationReached(
            request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
        );
    }
    // Update total tokens requested for user for launch group
-   userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
+   userTokens.set(request.userId, userTokenAmount - prevInfo.tokenAmount + request.tokenAmount);
    // Transfer payment currency from user to contract
    IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
}
```