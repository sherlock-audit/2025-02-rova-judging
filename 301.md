Crazy Chambray Flamingo

High

# `Launch::updateParticipation` incorrectly update the total tokens requested for user by the `currencyAmount`

### Summary

by calling `updateParticipation`, an user can increase or reduce their allocated `tokenAmount`, but when using said function the logic incorrectly update the `tokenAmount` with `currencyAmount`.

### Root Cause

the root cause is in [Launch.sol#L348-L377](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L348-L377):

```Solidity
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        // If new requested token amount is less than old amount, handle refund
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
@>          userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
 @>         userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }
```

first, the logic check the total token user would have by calling `_userTokensByLaunchGroup[request.launchGroupId]`
and then it compares the `prevInfo.currencyAmount > newCurrencyAmount`, there is no problem by comparing the currency amount because this value are linear with the token amount an user would buy.
the main issue is at the line marked, notice that it incorrectly use the `refundCurrencyAmount` or `additionalCurrencyAmount` value for setting the total token user would have later after launch. because the two value mentioned only account for the amount to refund/pay if the user have lower/higher new total tokens.
so the function logic incorrectly mixed the total token with the amount of currency that should be refunded/paid.


### Internal Pre-conditions

1. manager create launchGroup by calling `Launch::createLaunchGroup`
2. the setting used is `settings.finalizesAtParticipation = true`
3. user can buy max 10e18 token with the price of 1e6 each (or 1 USDC each)

### External Pre-conditions

-

### Attack Path

1. user wants to buy 5e18 token, and get the required signature from protocol
2. user call `Launch::participate` and buy 5e18 token, and paid `5e18*1e6/1e18 = 5e6` (5 USDC)
3. user wants to change the total token to 10e18 token, they get the required signature from protocol
4. user call `Launch::updateParticipation` with the correct parameter
5. `newCurrencyAmount` = `10e18*1e6/1e18 = 10e6` (10 USDC)
6.  `prevInfo.currencyAmount` = `5e18*1e6/1e18 = 5e6` (5 USDC)
7. `newCurrencyAmount > prevInfo.currencyAmount` is true, so `additionalCurrencyAmount` is set to the diff in price = 10e6 - 5e6 = 5e6
8. [Launch.sol#L374](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L374) then set the user `userTokens` into `userTokenAmount + additionalCurrencyAmount` = `5e18 + 5e6`
9. user transfer the diff: 5e6 (5 USDC) but only increase their amount of token by 5e6 and not the expected 5e18.

### Impact

user expected to increase their token by 5e18 after paying but only increased by 5e6, so by calculation the user loss is `1 - 5e6/5e18` = 99% of any arbitrary amount currency the user use when calling `Launch::updateParticipation`

even if the `ParticipationInfo.tokenAmount` showing correct value, the value of `_userTokensByLaunchGroup` which would be incorrectly updated in the attack path above is the one that used in core function call like `updateParticipation`, `cancelParticipation` and `_processRefund`. if the amount is smaller than the `ParticipationInfo.tokenAmount`, then the function where the `_userTokensByLaunchGroup` is deducted by `ParticipationInfo.tokenAmount` would revert because of underflow. the instance of the potential revert are [Launch.sol#L355](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L355) [Launch.sol#L440](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L440) [Launch.sol#L620](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L620)

### PoC

apply this git diff so the decimals used is the same as USDC (6 decimals):

```diff
diff --git a/rova-contracts/test/LaunchTestBase.t.sol b/rova-contracts/test/LaunchTestBase.t.sol
index 72d4daf..4f30a11 100644
--- a/rova-contracts/test/LaunchTestBase.t.sol
+++ b/rova-contracts/test/LaunchTestBase.t.sol
@@ -20,6 +20,9 @@ contract MockERC20 is ERC20 {
     constructor() ERC20("Mock Token", "MTK") {
         _mint(msg.sender, 1000000 * 10 ** 18);
     }
+    function decimals() public pure override returns (uint8) {
+        return 6;
+    }
 }
```

apply this git diff for the test:

```diff
diff --git a/rova-contracts/test/Launch.UpdateParticipation.t.sol b/rova-contracts/test/Launch.UpdateParticipation.t.sol
index f77edee..d42760d 100644
--- a/rova-contracts/test/Launch.UpdateParticipation.t.sol
+++ b/rova-contracts/test/Launch.UpdateParticipation.t.sol
@@ -3,7 +3,7 @@ pragma solidity ^0.8.22;

 import {IERC20Errors} from "@openzeppelin/contracts/interfaces/draft-IERC6093.sol";
 import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
-import {Test} from "forge-std/Test.sol";
+import "forge-std/Test.sol";
 import {LaunchTestBase} from "./LaunchTestBase.t.sol";
 import {Launch} from "../src/Launch.sol";
 import {
@@ -41,6 +41,48 @@ contract LaunchUpdateParticipationTest is Test, Launch, LaunchTestBase {
         vm.stopPrank();
     }

+    function test_PoC_UpdateIncorrectAmount() public {
+        // from setUp the original participation amount is 1000e18
+        // Prepare update participation request
+        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(2000);
+        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));
+
+        vm.startPrank(user1);
+        uint256 updatedCurrencyAmount =
+            _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, updateRequest.tokenAmount);
+        currency.approve(address(launch), updatedCurrencyAmount);
+
+        // Expect ParticipationUpdated event
+        vm.expectEmit();
+        emit ParticipationUpdated(
+            updateRequest.launchGroupId,
+            updateRequest.newLaunchParticipationId,
+            testUserId,
+            user1,
+            updateRequest.tokenAmount,
+            address(currency)
+        );
+
+        // Update participation
+        launch.updateParticipation(updateRequest, updateSignature);
+
+        // Verify update
+        ParticipationInfo memory newInfo = launch.getParticipationInfo(updateRequest.newLaunchParticipationId);
+        _verifyParticipationInfo(newInfo, updateRequest);
+        ParticipationInfo memory oldInfo = launch.getParticipationInfo(updateRequest.prevLaunchParticipationId);
+        assertEq(oldInfo.currencyAmount, 0);
+        assertEq(oldInfo.tokenAmount, 0);
+
+        // Verify total unique participants by launch group
+        assertEq(launch.getNumUniqueParticipantsByLaunchGroup(testLaunchGroupId), 1);
+
+        uint256 tokenSoldToUser = launch.getUserTokensByLaunchGroup(updateRequest.launchGroupId, newInfo.userId);
+
+        // assert that the user has the correct amount of tokens in the _userTokensByLaunchGroup and in ParticipationInfo
+        assertEq(tokenSoldToUser, newInfo.tokenAmount);
+
+        vm.stopPrank();
+    }
```

run the following command `forge t --mt test_PoC_UpdateIncorrectAmount -vv` the result would fail:

```bash
Failing tests:
Encountered 1 failing test in test/Launch.UpdateParticipation.t.sol:LaunchUpdateParticipationTest
[FAIL: assertion failed: 1000000000001000000000 != 2000000000000000000000] test_PoC_UpdateIncorrectAmount() (gas: 281395)
```


### Mitigation

fix the logic by using `newInfo.tokenAmount` instead and validate the new request for min/max token amount per user:

```diff
diff --git a/rova-contracts/src/Launch.sol b/rova-contracts/src/Launch.sol
index 5166200..0721f99 100644
--- a/rova-contracts/src/Launch.sol
+++ b/rova-contracts/src/Launch.sol
@@ -352,26 +359,22 @@ contract Launch is
             // Calculate refund amount
             uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
             // Validate user new requested token amount is greater than min token amount per user
-            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
+            if (request.tokenAmount < settings.minTokenAmountPerUser) {
                 revert MinUserTokenAllocationNotReached(
                     request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                 );
             }
-            // Update total tokens requested for user for launch group
-            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
             // Transfer payment currency from contract to user
             IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
         } else if (newCurrencyAmount > prevInfo.currencyAmount) {
             // Calculate additional payment amount
             uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
             // Validate user new requested token amount is within launch group user allocation limits
-            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
+            if (request.tokenAmount > settings.maxTokenAmountPerUser) {
                 revert MaxUserTokenAllocationReached(
                     request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                 );
             }
-            // Update total tokens requested for user for launch group
-            userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
             // Transfer payment currency from user to contract
             IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
         }
@@ -382,6 +385,7 @@ contract Launch is
         newInfo.userAddress = msg.sender;
         newInfo.userId = request.userId;
         newInfo.tokenAmount = request.tokenAmount;
+        userTokens.set(request.userId, newInfo.tokenAmount);
         // Reset previous participation info
         prevInfo.currencyAmount = 0;
         prevInfo.tokenAmount = 0;
```