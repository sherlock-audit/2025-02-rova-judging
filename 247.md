Pet Navy Osprey

High

# Users tokens are wrongly validated and updated

### Summary

Incorrect implementation of function `updateParticipation()` will cause the users token validated and updated improperly

### Root Cause

The [function `updateParticipation()`](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L312) allows users to update participation in launch group which is not finalized at participation. The function calculates the new currency amount with the new requested token amount. If the new currency amount is less than the old currency amount, the user is refunded. Otherwise, the user needs to provide more currency tokens.

The problem arises when `userTokenAmount` is directly compared and added/subtracted with currency amount. This is incorrect that these 2 values have different units (decimals).

```solidity
    function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        /// CODES
      
        // Calculate new payment amount in requested currency based on token price and requested token amount
@>        uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount); // <-- this in currency decimals
        // Get total tokens requested for user for launch group
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        // If new requested token amount is less than old amount, handle refund
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
@>            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) { // <-- subtract token amount by currency amount
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
@>            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount); // update wrong value for user tokens
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
@>            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) { // <--- similar as the check above 
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
@>            userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount); // << --- incorrect value
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }
    }
```

### Internal Pre-conditions

NA

### External Pre-conditions

NA

### Attack Path

1. Launch is with a token `A` having 18 decimals
2. Currency USDT (8 decimals) is enabled, finalizesAtParticipation = false
3. An user calls `participate()` with 100 amount of token `A` (assume that it passes the min/max limits)
4. An user calls `updateParticipation()` with 150 amount of token `A`
==> this will result that the user token by the launch group is not 150 (150 is the expected amount)

### Impact

- Incorrect tracking user tokens by group
- The min/max validation in the flow of updating participation can be bypassed
- Affect `claimRefund()` flow such that it can be unable to refund

### PoC

Add the test below to file `test/Launch.UpdateParticipation.t.sol`. Note that the mock contract `DecimalMockERC20` is added for customizing token decimals
```solidity
contract DecimalMockERC20 is MockERC20{
  function decimals() public pure override returns(uint8){
    return 8;
  }
}
contract LaunchUpdateParticipationTest is Test, Launch, LaunchTestBase {
    /// ....
    function test_UpdateParticipation_diffDecimals() public {
      DecimalMockERC20 _currency = new DecimalMockERC20();

      _currency.transfer(user1, 20000 * 10 ** 8);
      _currency.transfer(user2, 10000 * 10 ** 8);

      currency = MockERC20(_currency);

      CurrencyConfig memory currencyConfig =
            CurrencyConfig({tokenPriceBps: 5 * 10 ** 8, isEnabled: true});
        
        testLaunchGroupId = "abcxyz";
        testLaunchParticipationId = "zxc";
      
       LaunchGroupSettings memory settings = LaunchGroupSettings({
            finalizesAtParticipation: false,
            startsAt: block.timestamp,
            endsAt: block.timestamp + 1 days,
            maxTokenAllocation: 10000 * 10 ** launch.tokenDecimals(),
            minTokenAmountPerUser: 500 * 10 ** launch.tokenDecimals(),
            maxTokenAmountPerUser: 3000 * 10 ** launch.tokenDecimals(),
            status: LaunchGroupStatus.ACTIVE
        });
        vm.startPrank(manager);
        launch.createLaunchGroup(testLaunchGroupId, address(_currency), currencyConfig, settings);
        vm.stopPrank();

        ParticipationRequest memory request = ParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: testLaunchParticipationId,
            userId: testUserId,
            userAddress: user1,
            tokenAmount: 1000 * 10 ** launch.tokenDecimals(),
            currency: address(_currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
        bytes memory signature = _signRequest(abi.encode(request));

        vm.startPrank(user1);
        uint newAmount = _getCurrencyAmount(
                request.launchGroupId,
                request.currency,
                request.tokenAmount
            );
        _currency.approve(
            address(launch),
            newAmount
        );

        // participate first
        launch.participate(request, signature);

        // update the participation
        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(100);
        
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.startPrank(user1);

        uint256 updatedCurrencyAmount =
            _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, updateRequest.tokenAmount);

        currency.approve(address(launch), updatedCurrencyAmount);

        launch.updateParticipation(updateRequest, updateSignature);

        // check the user token by launch group id
        assertEq(launch.getUserTokensByLaunchGroup(request.launchGroupId, testUserId), updateRequest.tokenAmount, "incorrect user tokens");
    }
```
Run the test and it failed
```bash
Ran 1 test for test/Launch.UpdateParticipation.t.sol:LaunchUpdateParticipationTest
[FAIL: incorrect user tokens: 999999999550000000000 != 100000000000000000000] test_UpdateParticipation_diffDecimals() (gas: 1240154)
Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 9.47ms (2.17ms CPU time)

Ran 1 test suite in 160.53ms (9.47ms CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)

Failing tests:
Encountered 1 failing test in test/Launch.UpdateParticipation.t.sol:LaunchUpdateParticipationTest
[FAIL: incorrect user tokens: 999999999550000000000 != 100000000000000000000] test_UpdateParticipation_diffDecimals() (gas: 1240154)
```

### Mitigation

- The user token amount should be checked with the requested token amount