Scrawny Clear Cheetah

High

# Incorrect token amount update in the `updateParticipation()` function

### Summary

In the `ParticipationInfo` struct, `tokenAmount` represents the amount of tokens a user intends to purchase, whereas `currencyAmount` specifies the amount of currency tokens the user will spend.

However, incorrect handling of token and currency amounts during participation updates may result in users losing their currency tokens or the protocol receiving fewer currency tokens than anticipated.

### Root Cause

The `updateParticipation()` function adjust `tokenAmount` as following:

[`Launch::updateParticipation()`](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L351-L377)
```solidity
    function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        ...
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            ...
@>          userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
            ...
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            ...
@>          userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
            ...
        }
        ...
    }
```

However, `refundCurrencyAmount` and `additionalCurrencyAmount` represent the amount of currency tokens to be adjusted, which are incorrectly applied to the user's token amount.

### Internal pre-conditions

The decimals of the launch token should be different from the one of the currency token.

### External pre-conditions

*None*

### Attack Path

*None*

### Impact

Incorrect handling of token and currency amounts during participation updates may result in users losing their currency tokens or the protocol receiving fewer currency tokens than anticipated when the decimals of the launch token and the currency token are not equal.

### PoC

The following test case utilizes mock USDC with 6 decimals as currency token:

```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {console} from "forge-std/console.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {IERC20Errors} from "@openzeppelin/contracts/interfaces/draft-IERC6093.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {Test} from "forge-std/Test.sol";
import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    UpdateParticipationRequest,
    ParticipationInfo,
    CurrencyConfig
} from "../src/Types.sol";

contract MockUSDC is ERC20 {
    constructor() ERC20("Mock Token", "MTK") {
        _mint(msg.sender, 1000000 * 10 ** decimals());
    }

    function decimals() public view override returns (uint8) {
        return 6;
    }
}

contract LaunchPocH01Test is Test, Launch, LaunchTestBase {
    LaunchGroupSettings public settings;
    ParticipationRequest public originalParticipationRequest;
    MockUSDC usdc;    

    function setUp() public {
        _setUpLaunch();

        usdc = new MockUSDC();

        // Fund users
        usdc.transfer(user1, 2000 * 10 ** usdc.decimals());
        usdc.transfer(user2, 1000 * 10 ** usdc.decimals());

        // Setup initial participation
        CurrencyConfig memory currencyConfig =
            CurrencyConfig({tokenPriceBps: 1 * 10 ** usdc.decimals(), isEnabled: true});
        settings = LaunchGroupSettings({
            finalizesAtParticipation: false,
            startsAt: block.timestamp,
            endsAt: block.timestamp + 1 days,
            maxTokenAllocation: 10000 * 10 ** launch.tokenDecimals(),
            minTokenAmountPerUser: 500 * 10 ** launch.tokenDecimals(),
            maxTokenAmountPerUser: 3000 * 10 ** launch.tokenDecimals(),
            status: LaunchGroupStatus.ACTIVE
        });
        vm.startPrank(manager);
        launch.createLaunchGroup(testLaunchGroupId, address(usdc), currencyConfig, settings);
        vm.stopPrank();
    }

    // @audit-poc
    function test_UpdateParticipation_H01_IncorrectUpdate() public {
        originalParticipationRequest = _createParticipationRequestWithUsdc();
        bytes memory signature = _signRequest(abi.encode(originalParticipationRequest));

        vm.startPrank(user1);
        usdc.approve(
            address(launch),
            _getCurrencyAmount(
                originalParticipationRequest.launchGroupId,
                originalParticipationRequest.currency,
                originalParticipationRequest.tokenAmount
            )
        );
        // Pays 1,000 USDC for 1,000 launch tokens
        launch.participate(originalParticipationRequest, signature);
        assertEq(usdc.balanceOf(user1), 1000e6);

        vm.stopPrank();

        // Prepare update participation request
        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(2000);
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.startPrank(user1);
        uint256 updatedCurrencyAmount =
            _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, updateRequest.tokenAmount);
        usdc.approve(address(launch), updatedCurrencyAmount);

        // Update participation
        launch.updateParticipation(updateRequest, updateSignature);

        assertEq(
            launch.getUserTokensByLaunchGroup(updateRequest.launchGroupId, updateRequest.userId),
            1000 * 10 ** launch.tokenDecimals() + 1000 * 10 ** usdc.decimals()
        );
    }

    function _createUpdateParticipationRequest(uint256 newTokenAmount)
        internal
        view
        returns (UpdateParticipationRequest memory)
    {
        return UpdateParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            prevLaunchParticipationId: testLaunchParticipationId,
            newLaunchParticipationId: "newLaunchParticipationId",
            userId: testUserId,
            userAddress: user1,
            tokenAmount: newTokenAmount * 10 ** launch.tokenDecimals(),
            currency: address(usdc),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }

    function _createParticipationRequestWithUsdc() internal view returns (ParticipationRequest memory) {
        return ParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: testLaunchParticipationId,
            userId: testUserId,
            userAddress: user1,
            tokenAmount: 1000 * 10 ** launch.tokenDecimals(),
            currency: address(usdc),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }
}
```

As can be seen from the test case, the user's token amount should be `2000e18` after the update, but it is `1000e18 + 1000e6`.

### Mitigation

```diff
    function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        ...
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
+           uint256 tokenAmounToSubtract = prevInfo.tokenAmount - request.tokenAmount;
            ...
-           userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
+           userTokens.set(request.userId, userTokenAmount - tokenAmounToSubtract);
            ...
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
+           uint256 tokenAmounToAdd = request.tokenAmount - prevInfo.tokenAmount;
            ...
-           userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
+           userTokens.set(request.userId, userTokenAmount + tokenAmounToAdd);
            ...
        }
        ...
    }
```
