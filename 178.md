Unique Purple Guppy

High

# User tokens incorrectly calculated in updateParticipation function of Launch.sol

### Summary

Critical/High Issue -

updateParticipation function of Launch.sol is incorrectly calculating user tokens. 

The function is adding/subtracting additionalCurrencyAmount/refundCurrencyAmount amount from userTokens. 

This is a high/critical bug as it results in incorrect calculation of user tokens. 

Tests created by protocol were unable to catch this issue because currency and tokens are pegged 1-1 i.e tokenPriceBps in currency configuration is set to 1 * 10**18. Just change it to 3*10**18 and the issue will be caught in the update participants tests (as shown in POC).



### Root Cause

In function updateParticipation of Launch.sol, refundCurrencyAmount is subtracted (in case of decrease in tokens) or additionalCurrencyAmount added in case of increase in tokens. This is incorrect. 

refundCurrencyAmount/additionalCurrencyAmounts are calculated by multiplying tokenPriceBps with usertokens. They cannot be used to determine the final usertokens. This is a critical bug which results in incorrect calculation of user tokens. 

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L360-L376

![Image](https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/a5fac857-4ceb-4433-89a9-639728925158)

### Internal Pre-conditions

Manager Role must set the currency config of Launchgroup such that tokenPriceBps should NOT be 1 * 10 ** 18. Instead it should be 2 * 10 ** 18 or 3 * 10 ** 18.

This issue can then be uncovered.

### External Pre-conditions

NA

### Attack Path

**STEP 1** - Set up a launch using the same set up as done in the protocol test scripts. Finalize at Participation should be false as we want to update participation

**STEP 2** - Confirm that token price BPS from default currency set up is 1 * 10**18

**STEP 3** - Update token price BPS in the currency set up to be 2 * 10**18

**STEP 4** - Confirm that token price BPS from default currency set up is 2 * 10**18

**STEP 5** - Fund user with more currency tokens

**STEP 6** - Add participation for user 1 with 1000 tokens

**STEP 7** - Check user token after original participation.

**STEP 8** - Update participation for user with 1200 tokens.

**STEP 9** - Check user token after  participation update. Note that we  updated user tokens to 1200 so we expect 1200 tokens but the result is 1600 tokens

### Impact

User Tokens calculation will be incorrect resulting in loss to protocol and users.

### PoC

```solidity
// Copy this POC and create a test contract in the test directory of repo. And just run the test with -vvv. Observe logs and follow the code.
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {IERC20Errors} from "@openzeppelin/contracts/interfaces/draft-IERC6093.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {Test, console} from "forge-std/Test.sol";
import {VmSafe} from "forge-std/Vm.sol";
import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {LaunchGroupSettings, LaunchGroupStatus, ParticipationRequest, UpdateParticipationRequest, ParticipationInfo, CurrencyConfig} from "../src/Types.sol";

contract LaunchUpdateParticipationIssueTest is Test, Launch, LaunchTestBase {
    LaunchGroupSettings public settings;
    ParticipationRequest public originalParticipationRequest;

    // STEP 1 - Set up a launch using the same set up as done in the protocol test scripts. Finalize at Participation is false as we want to update participation
    function setUp() public {
        console.log("** Setting Up Launch and Launch Group**");
        _setUpLaunch();
        settings = _setupLaunchGroup();
    }

    function testThatTokenIsIncorrectlyUpdated() public {
        // STEP 2 - Confirm that token price BPS from default currency set up is 1 * 10**18
        console.log(
            "** Fetching Token Price BPS from default currency set up **"
        );

        CurrencyConfig memory launchGroupcurrency = launch
            .getLaunchGroupCurrencyConfig(testLaunchGroupId, address(currency));

        console.log(
            "** Token Price BPS from default Currency set up is - ",
            launchGroupcurrency.tokenPriceBps
        );

        // STEP 3 - Update token price BPS in the currency set up to be 2 * 10**18
        console.log("** Updating token price BPS to 2 * 10**18 **");
        CurrencyConfig memory updatedCurrencyConfig = CurrencyConfig({
            tokenPriceBps: 3 * 10 ** currency.decimals(),
            isEnabled: true
        });

        vm.startPrank(manager);

        launch.setLaunchGroupCurrency(
            testLaunchGroupId,
            address(currency),
            updatedCurrencyConfig
        );

        vm.stopPrank();

        // STEP 4 - Confirm that token price BPS from default currency set up is 2 * 10**18
        console.log(
            "** Fetching Token Price BPS from updated currency set up **"
        );
        launchGroupcurrency = launch.getLaunchGroupCurrencyConfig(
            testLaunchGroupId,
            address(currency)
        );
        console.log(
            "** Token Price BPS from updated Currency set up is - ",
            launchGroupcurrency.tokenPriceBps
        );

        // STEP 5 - Fund user with more currency tokens
        console.log("** Funding user with more currency tokens **");
        vm.startPrank(admin.addr);
        currency.transfer(user1, 7000 * 10 ** launch.tokenDecimals());
        vm.stopPrank();

        // STEP 6 - Add participation for user 1 with 1000 tokens
        console.log("** Adding participation for user 1 with 1000 tokens **");

        originalParticipationRequest = _createParticipationRequest();
        bytes memory signature = _signRequest(
            abi.encode(originalParticipationRequest)
        );

        vm.startPrank(user1);
        currency.approve(
            address(launch),
            _getCurrencyAmount(
                originalParticipationRequest.launchGroupId,
                originalParticipationRequest.currency,
                originalParticipationRequest.tokenAmount
            )
        );
        launch.participate(originalParticipationRequest, signature);

        vm.stopPrank();

        // STEP 7 - Check user token after original participation.
        console.log("** Checking user token after orignal participation **");
        uint256 originalTokenAmountForUser = launch.getUserTokensByLaunchGroup(
            testLaunchGroupId,
            originalParticipationRequest.userId
        );

        console.log(
            "** Original Token Amount For User -",
            originalTokenAmountForUser
        );

        // STEP 8 - Update participation for user with 1200 tokens.
        console.log("** Updating participation for user 1 with 1200 tokens **");
        uint256 updatedTokenAmount = 1200;
        uint256 launchTokenDecimals = launch.tokenDecimals();
        UpdateParticipationRequest
            memory updateRequest = UpdateParticipationRequest({
                chainId: block.chainid,
                launchId: testLaunchId,
                launchGroupId: testLaunchGroupId,
                prevLaunchParticipationId: testLaunchParticipationId,
                newLaunchParticipationId: "newLaunchParticipationId",
                userId: testUserId,
                userAddress: user1,
                tokenAmount: updatedTokenAmount * 10 ** launchTokenDecimals,
                currency: address(currency),
                requestExpiresAt: block.timestamp + 1 hours
            });
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.startPrank(user1);
        uint256 updatedCurrencyAmount = _getCurrencyAmount(
            updateRequest.launchGroupId,
            updateRequest.currency,
            updateRequest.tokenAmount
        );
        currency.approve(address(launch), updatedCurrencyAmount);

        launch.updateParticipation(updateRequest, updateSignature);

        // STEP 9 - Check user token after  participation update. Note that we  updated user tokens to 1200 so we expect 1200 tokens but the result is 1600 tokens

        uint256 updatedTokenAmountForUser = launch.getUserTokensByLaunchGroup(
            testLaunchGroupId,
            originalParticipationRequest.userId
        );

        console.log(
            "** Updated Token Amount For User -",
            updatedTokenAmountForUser
        );

        vm.stopPrank();
    }
}



```

### Mitigation

Rectify updateParticipation function of Launch.sol to user token amount to calculate user tokens.