Melodic Plastic Caterpillar

High

# [High] - Incorrect calculation for token assignment in `updateParticipation` function

### Summary

The function `updateParticipation` updates a user`s token amount. However, while updating the new amount of token the cost of the token is added/subtracted to the token amount.

### Root Cause

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L361

In `Launch.sol:361` the refund amount gets subtracted from the number of tokens

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L374

and  in `Launch.sol:374`  the additional amount gets added to the tokens

### Internal Pre-conditions

1. `tokenPriceBps`should not be equal to `1 * 10 ** currency.decimals()`, i.e. price of 1 token should not be equal to 1 of whatever currency used in the `launchGroup`
2. `launchGroup` does not finalize at participation

### External Pre-conditions

-

### Attack Path

1. User participates in a `launchGroup` that does not finalize at participation
2. User updates his participation

### Impact

The user gets assigned wrong amount of tokens after updating his participation.

### PoC

```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {IERC20Errors} from "@openzeppelin/contracts/interfaces/draft-IERC6093.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {Test} from "forge-std/Test.sol";
import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    UpdateParticipationRequest,
    ParticipationInfo,
    CurrencyConfig
} from "../src/Types.sol";

contract LaunchUpdateParticipationTest is Test, Launch, LaunchTestBase {
    LaunchGroupSettings public settings;
    ParticipationRequest public originalParticipationRequest;

    function setUp() public {
        _setUpLaunch();

        // Setup initial participation
        // 1 token = 0.1 of currency
        CurrencyConfig memory currencyConfig =
            CurrencyConfig({tokenPriceBps: 1 * 10 ** (currency.decimals()-1), isEnabled: true});
        settings = LaunchGroupSettings({
            finalizesAtParticipation: false,
            startsAt: block.timestamp,
            endsAt: block.timestamp + 1 days,
            maxTokenAllocation: 10000 * 10 ** launch.tokenDecimals(),
            minTokenAmountPerUser: 500 * 10 ** launch.tokenDecimals(),
            maxTokenAmountPerUser: 3000 * 10 ** launch.tokenDecimals(),
            status: LaunchGroupStatus.ACTIVE
        });
        vm.startPrank(manager);
        launch.createLaunchGroup(testLaunchGroupId, address(currency), currencyConfig, settings);
        vm.stopPrank();
        // initially bought 1000 tokens
        originalParticipationRequest = ParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: testLaunchParticipationId,
            userId: testUserId,
            userAddress: user1,
            tokenAmount: 1000 * 10 ** launch.tokenDecimals(),
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
        bytes memory signature = _signRequest(abi.encode(originalParticipationRequest));

        vm.startPrank(user1);
        currency.approve(
            address(launch),
            _getCurrencyAmount(
                originalParticipationRequest.launchGroupId,
                originalParticipationRequest.currency,
                originalParticipationRequest.tokenAmount
            )
        );
        launch.participate(originalParticipationRequest, signature);

        vm.stopPrank();
    }


    function test_POC() public {

        // Prepare update participation request
        // trying to reduce tokens to 5000
        UpdateParticipationRequest memory request = _createUpdateParticipationRequest(500);
        bytes memory signature = _signRequest(abi.encode(request));

        vm.startPrank(user1);
        // Update participation
        launch.updateParticipation(request, signature);
        vm.expectRevert();
        // request.tokenAmount = 500
        // actual token in database = Initial number of tokens - (price of tokens * request.tokenAmount)
        // = 1000 - (0.1*500)
        // = 950 
        assertEq(request.tokenAmount,launch.getUserTokensByLaunchGroup(testLaunchGroupId, testUserId));
        vm.stopPrank();
    }

    function _createUpdateParticipationRequest(uint256 newTokenAmount)
        internal
        view
        returns (UpdateParticipationRequest memory)
    {
        uint256 launchTokenDecimals = launch.tokenDecimals();
        return UpdateParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            prevLaunchParticipationId: testLaunchParticipationId,
            newLaunchParticipationId: "newLaunchParticipationId",
            userId: testUserId,
            userAddress: user1,
            tokenAmount: newTokenAmount * 10 ** launchTokenDecimals,
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }
}

```

### Mitigation

Set `userTokens` for that user as the `request.tokenAmount`
`userTokens.set(request.userId, request.tokenAmount);`