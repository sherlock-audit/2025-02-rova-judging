Original Myrtle Hedgehog

Medium

# Launch::updateParticipation Misused Currency Amount And Token Amount

### Summary

When calculating the new userTokenAmount, the updateParticipation() function misused curency amount and token amount.

Severity: M
Likehood: H

### Root Cause

In Launch.sol line 351-377. there is an incorrect mixing of tokenAmount and currencyAmount at the following 4 places.

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L355

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L361

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L368

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L374



### Internal Pre-conditions

The currency/token ratio is NOT 1:1

### External Pre-conditions

N/A

### Attack Path

User calls Launch::updateParticipation

### Impact


### 1. Corrupted Data in _userTokensByLaunchGroup
The incorrect token amount stored in _userTokensByLaunchGroup impacts the following functions: _processRefund, getUserTokensByLaunchGroup, updateParticipation, cancelParticipation.

### 2. Bypassing or Blocking Participation Restrictions
Due to the incorrect calculations, the contract may:
	* 	Block legitimate users from updating their participation request when they should be allowed.
	* 	Allow attackers to bypass the minTokenAmountPerUser and maxTokenAmountPerUser checks.

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L355

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L368

### 3. Potential Underflow Errors
If the currency/token ratio is greater than 1 (e.g., 4 currency = 1 token), underflow errors can occur in this calculation:
```solidity
if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
```


### PoC

To reproduce the issue, modify Launch.UpdateParticipation.t.sol and set the currency/token ratio to 1:4 at the beginning:

```solidity
...
import {console} from "forge-std/console.sol";

contract LaunchUpdateParticipationTest is Test, Launch, LaunchTestBase {
	...
	
    function test_UpdateParticipation_UserTokensByLaunchGroup() public {
        // remove previous participation request, and update the tokenPrice
        vm.startPrank(manager);
        // 1 currency = 4 token
        launch.setLaunchGroupCurrency(
            testLaunchGroupId,
            address(currency),
            CurrencyConfig({tokenPriceBps: 1 * 10 ** currency.decimals() / 4, isEnabled: true})
        );
        vm.stopPrank();

        bytes32 groupId = testLaunchGroupId;
        bytes32 user2Id = "2222222232222222222222222";
        bytes32 user2LaunchParticipationId = "123123ldi00003b74facm5z9n";
        bytes32 user2NewLaunchParticipationId = "456567ldi00003b74facm5z9n";

        uint256 numToken = launch.getUserTokensByLaunchGroup(groupId, user2Id);
        assertEq(numToken, 0);

        // add participation request
        ParticipationRequest memory participationRequest = _createParticipationRequest();
        participationRequest.userAddress = user2;
        participationRequest.userId = user2Id;
        participationRequest.launchParticipationId = user2LaunchParticipationId;
        bytes memory signature = _signRequest(abi.encode(participationRequest));
        vm.startPrank(user2);
        currency.approve(
            address(launch),
            _getCurrencyAmount(
                participationRequest.launchGroupId, participationRequest.currency, participationRequest.tokenAmount
            )
        );
        launch.participate(participationRequest, signature);

        uint256 tokenAmountBefore = launch.getUserTokensByLaunchGroup(groupId, user2Id);
        assertEq(tokenAmountBefore, 1000 * 10 ** launch.tokenDecimals());

        // update participation request
        uint256 newTokenAmount2 = 800;
        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(newTokenAmount2);
        updateRequest.userAddress = user2;
        updateRequest.userId = user2Id;
        updateRequest.prevLaunchParticipationId = user2LaunchParticipationId;
        updateRequest.newLaunchParticipationId = user2NewLaunchParticipationId;
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));
        vm.startPrank(user2);
        currency.approve(
            address(launch),
            _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, updateRequest.tokenAmount)
        );

        launch.updateParticipation(updateRequest, updateSignature);
        uint256 tokenAmountAfter = launch.getUserTokensByLaunchGroup(groupId, user2Id);
        console.log("tokenAmountAfter", tokenAmountAfter);
        assertEq(tokenAmountAfter, newTokenAmount2 * (10 ** launch.tokenDecimals()));
    }
	...
}
```

Run
```bash
forge test --mt test_UpdateParticipation_UserTokensByLaunchGroup -vv
```

It will fail at the last line
```solidity
assertEq(tokenAmountAfter, newTokenAmount2 * (10 ** launch.tokenDecimals()));
```



### Mitigation

To fix this issue, update the following lines in Launch.sol:

Launch.sol line 355
```solidity
-	if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
+	if (request.tokenAmount < settings.minTokenAmountPerUser) {
```

Launch.sol line 361
```solidity
-    userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
+    userTokens.set(request.userId, request.tokenAmount);
```

Launch.sol line 368
```solidity
-	if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
+	if (request.tokenAmount > settings.maxTokenAmountPerUser) {
```

Launch.sol line 374
```solidity
-	userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
+	userTokens.set(request.userId, request.tokenAmount);
```