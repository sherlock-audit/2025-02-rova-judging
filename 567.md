Innocent Seaweed Wren

Medium

# Lack of check on `currencyAmount` can lead to users getting `tokenAmount` for free.

### Summary

 If the launch group settings is not constraining `minTokenAmountPerUser` enough, users can participate for free, ie receive some `tokenAmount` without paying any `currency` amount.

### Root Cause

In `participate`, users specify the `tokenAmount` they want to receive. The `currency` amount they must pay is computed [here](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L265)

```solidity
        // Calculate payment amount in requested currency based on token price and requested token amount
        uint256 currencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
...

/// @notice Calculate currency payment amount based on bps and token amount
    function _calculateCurrencyAmount(uint256 tokenPriceBps, uint256 tokenAmount) internal view returns (uint256) {
        return Math.mulDiv(tokenPriceBps, tokenAmount, 10 ** tokenDecimals);
    }
```

The issue is that there is no check on the value of `currencyAmount`. For low decimal currency such as `USDC` (which is explicitly accepted by the protocol), it is possible to specify a `tokenAmount` that is low enough to make `Math.mulDiv(tokenPriceBps, tokenAmount, 10 ** tokenDecimals);` return `0` due to truncation.

### Internal Pre-conditions

Two pre-conditions:

1- that in the launch group settings, `minTokenAmountPerUser` is low enough for the rounding to happen.

I asked the sponsor if there was an inherent limit on `minTokenAmountPerUser`, and they confirmed there is none. So a manager can set any `minTokenAmountPerUser ` they want, as low as `1`. This condition is hence possible.

The sponsor exact words:
```text
2. minTokenAmountPerUser will always be > 0 and maxTokenAmountPerUser needs to be > than minTokenAmountPerUser but no limit.
```

2. that in the `LaunchGroupSettings`, `finalizesAtParticipation` is set to `true`. Because for launch groups that finalize after, `currencyAmount == 0` would revert in `finalizeWinner` because of [this check](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L542)

### External Pre-conditions

None

### Attack Path

1. A launch group that finalizes at participation accepting `USDC` is launched, where the managers allow a large freedom to the amount of tokens minted (`minTokenAmountPerUser` is set to a very low value,`maxTokenAmountPerUser` to a very high one)
2. Users call `participate` specifying a `tokenAmount` low enough to cause a truncation at `_calculateCurrencyAmount`. This result in them receiving some `tokenAmount` without paying any `currency`
3. Because users can participate multiple times, the user can repeat it.

### Impact

That launch group is essentially broken given that anyone can perform this to get a free `tokenAmount`.
Note that on `Base`, this barely costs any gas to perform.

As per the sponsor:

```text
> tokenAmount - This would come from user input.
Before signing, our backend would validate that the tokenAmount is within the minimum and maximum token amount per user for the launch group.
```

So the backend does not perform any other check than ensuring `tokenAmount >minTokenAmountPerUser`, which passes in this case. There is no validation on whether the `tokenAmount` would result in a zero `currencyAmount`.

The backend checks performed (as per the sponsor):

```text
Backend checks before generating the signatures for user participation requests:

* Verify user authentication using access token in request headers
* Validate user is not blocked previously on Rova and IP geoblocks
* Ensure user has completed KYC
* Make sure user address is not sanctioned (Chainalysis API)
* Project based eligibility check if any (this will be determined by the token sale project)
* Make sure user has no other pending transactions or reached their allocation limits. This is for better error UX, since the contract expected to have validation to prevent users from crossing their max token allocation as well. 
```

There is no mention of a built-in mechanism that would prevent the user from repeating the operation.
But because it does require the user to manually fetch a signature for every `participate` call, it does limit the actual financial gain a user can make from this exploit.
However, it does brick the entire launch group given that some users are getting free tokens. This negatively impacts:
- the manager (receives less currency than expected)
- legitimate users (who are less incentivized to participate given that the launch group can be gamed)

### PoC

We are showing how a user can participate to get a `tokenAmount` for free.

First, make these changes in `LaunchTestBase.t.sol`

- give `6` decimals to the MockERC20 (to simulate USDC)
```diff
contract MockERC20 is ERC20 {
    constructor() ERC20("Mock Token", "MTK") {
        _mint(msg.sender, 1000000 * 10 ** 18);
    }
+    function decimals() public view override(ERC20) returns (uint8) {
 +       return 6;
  +  }
}

```

And add these two functions
```solidity
function _setupLaunchGroupWithStatusFlexible(bytes32 launchGroupId, LaunchGroupStatus status)
        internal
        returns (LaunchGroupSettings memory)
    {
        CurrencyConfig memory currencyConfig =
            CurrencyConfig({tokenPriceBps: 1 * 10 ** currency.decimals(), isEnabled: true});
        LaunchGroupSettings memory settings = LaunchGroupSettings({
            finalizesAtParticipation: false,
            startsAt: block.timestamp,
            endsAt: block.timestamp + 1 days,
            maxTokenAllocation: 10000 * 10 ** launch.tokenDecimals(),
            minTokenAmountPerUser: 1,
            maxTokenAmountPerUser: type(uint256).max,
            status: status
        });
        vm.startPrank(manager);
        launch.createLaunchGroup(launchGroupId, address(currency), currencyConfig, settings);
        vm.stopPrank();
        return settings;
    }
...
function _createParticipationRequestExploit() internal view returns (ParticipationRequest memory) {
        return ParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: testLaunchParticipationId,
            userId: testUserId,
            userAddress: user1,
            tokenAmount: 9 * 10 ** 11,
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }
```

Then add this test in `Launch.Participate.t.sol`, and run it

```solidity
function test_Bug_Zero_Currency() public {
        // Setup new launch group
        bytes32 launchGroupId = bytes32(uint256(1));
        LaunchGroupSettings memory settings = _setupLaunchGroupWithStatusFlexible(launchGroupId, LaunchGroupStatus.PENDING);
        settings.finalizesAtParticipation = true;
        vm.startPrank(manager);
        launch.setLaunchGroupSettings(launchGroupId, settings);
        launch.setLaunchGroupStatus(launchGroupId, LaunchGroupStatus.ACTIVE);
        vm.stopPrank();

        // Prepare participation request
        ParticipationRequest memory request = _createParticipationRequestExploit();
        request.launchGroupId = launchGroupId;
        bytes memory signature = _signRequest(abi.encode(request));

        vm.startPrank(user1);
        //@audit - we hardcode the approval, it does not matter anyway
        currency.approve(address(launch), 1e6);

        // Expect ParticipationRegistered event
        vm.expectEmit();
        emit ParticipationRegistered(
            request.launchGroupId, request.launchParticipationId, testUserId, user1, 0, address(currency)
        );

        // Participate
        launch.participate(request, signature);

        // Verify participation
        ParticipationInfo memory info = launch.getParticipationInfo(request.launchParticipationId);
        assertEq(info.userAddress, user1);
        assertEq(info.userId, testUserId);
        assertEq(info.tokenAmount, request.tokenAmount);
        //@audit - free participation
        assertEq(info.currencyAmount, 0);
        assertEq(info.currency, address(currency));
        assertEq(info.isFinalized, true);

        // Verify total unique participants by launch group
        assertEq(launch.getNumUniqueParticipantsByLaunchGroup(request.launchGroupId), 1);

        // Verify total tokens sold
        assertEq(launch.getTokensSoldByLaunchGroup(request.launchGroupId), request.tokenAmount);

        // Verify total withdrawable amount
        //@audit - the launch did not receive any payment
        assertEq(launch.getWithdrawableAmountByCurrency(address(currency)), 0);


        // Verify user tokens
        //@audit - but the user receives a `tokenAmount`
        assertEq(launch.getUserTokensByLaunchGroup(request.launchGroupId, testUserId), request.tokenAmount);

        vm.stopPrank();
    }
```

### Mitigation

Prevent any rounding of `currencyAmount`

```diff
/// @notice Calculate currency payment amount based on bps and token amount
    function _calculateCurrencyAmount(uint256 tokenPriceBps, uint256 tokenAmount) internal view returns (uint256) {
-        return Math.mulDiv(tokenPriceBps, tokenAmount, 10 ** tokenDecimals);
+       uint256 result = Math.mulDiv(tokenPriceBps, tokenAmount, 10 ** tokenDecimals);
+         if (result == 0) revert InvalidAmount();
+        return result;
    }
```