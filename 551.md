Square Sand Octopus

Medium

# too many groupids will make withdraw function doesn't work

### Summary

creating too many launch groups will cause a denial of service (DoS) for withdraw role as the contract will fail to process withdrawals due to gas limitations.

### Root Cause

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L572-L579
The choice to check all launch groups is a mistake as ti will cause a denial of service (DoS) for withdraw role

### Internal Pre-conditions

1. over 7000 group ids are created

### External Pre-conditions

none

### Attack Path

1. over 7000 group id are created
2. Withdrawal role can't call the withdraw function

### Impact

In case it's an attack path: The withdrawal role suffers an approximate loss of 100% locked tokens in the contract.

### PoC

```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {IAccessControl} from "@openzeppelin/contracts/access/IAccessControl.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {Test} from "forge-std/Test.sol";
import {LaunchTestBase, IERC20Events} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {LaunchGroupSettings, LaunchGroupStatus, ParticipationRequest} from "../src/Types.sol";
import "forge-std/console.sol";
import {
    CurrencyConfig
} from "../src/Types.sol";
contract LaunchWithdrawTest is Test, Launch, LaunchTestBase, IERC20Events {
    function setUp() public {
        _setUpLaunch();

        // how many launchGroupIds we set
        uint count = 7000;
        bytes32[] memory launchGroupIds = new bytes32[](count);
        
        // one launch group, one participation
        bytes32[] memory participationIds = new bytes32[](count);
        address[] memory users = new address[](count);

        vm.pauseGasMetering();
        for (uint160 i = 0; i < count; i++) {
            bytes32 launchGroupId = generateLauchGroupId(i);
            launchGroupIds[i] = launchGroupId;
            LaunchGroupSettings memory setting = _setupLaunchGroupWithStatusOverride(launchGroupId, LaunchGroupStatus.PENDING);
            setting.status = LaunchGroupStatus.ACTIVE;
            setting.finalizesAtParticipation = true;
            vm.startPrank(manager);
            launch.setLaunchGroupSettings(launchGroupId, setting);
            vm.stopPrank();
            participationIds[i] = bytes32(uint256(i));
            users[i] = vm.addr(uint160(i + 1000000));
            vm.startPrank(users[i]);
            currency.mint(1000 * 10 ** launch.tokenDecimals());
            vm.stopPrank();
        }
       
        _setupParticipationsOverride(participationIds, users, launchGroupIds);

        // Complete the launch group
        vm.startPrank(manager);
        for (uint i = 0; i < count; i++) {
            launch.setLaunchGroupStatus(generateLauchGroupId(i), LaunchGroupStatus.COMPLETED);
        }
        vm.stopPrank();
        vm.resumeGasMetering();
    }

    function _setupParticipationsOverride(bytes32[] memory participationIds, address[] memory users, bytes32[] memory launchGroupIds)
        internal
        returns (ParticipationRequest[] memory)
    {
        ParticipationRequest[] memory requests = new ParticipationRequest[](participationIds.length);
        for (uint256 i = 0; i < participationIds.length; i++) {
            ParticipationRequest memory request = ParticipationRequest({
                chainId: block.chainid,
                launchId: testLaunchId,
                launchGroupId: launchGroupIds[i],
                launchParticipationId: participationIds[i],
                userId: bytes32(uint256(i + 1)),
                userAddress: users[i],
                tokenAmount: 1000 * 10 ** launch.tokenDecimals(),
                currency: address(currency),
                requestExpiresAt: block.timestamp + 1 hours
            });

            bytes memory signature = _signRequest(abi.encode(request));

            vm.startPrank(users[i]);
            currency.approve(
                address(launch), _getCurrencyAmount(request.launchGroupId, request.currency, request.tokenAmount)
            );
            launch.participate(request, signature);
            vm.stopPrank();

            requests[i] = request;
        }
        return requests;
    }

    function test_Withdraw_FullAmount() public {
        uint256 withdrawableAmount = launch.getWithdrawableAmountByCurrency(address(currency));
        assertEq(currency.balanceOf(testWithdrawalAddress), 0);
        assertEq(currency.balanceOf(address(launch)), withdrawableAmount);

        vm.startPrank(testWithdrawalAddress);
        // Verify events
        vm.expectEmit(true, true, true, true);
        emit Transfer(address(launch), testWithdrawalAddress, withdrawableAmount);
        vm.expectEmit(true, true, true, true);
        emit Withdrawal(testWithdrawalAddress, address(currency), withdrawableAmount);

        // Withdraw
        launch.withdraw(address(currency), withdrawableAmount);

        assertEq(currency.balanceOf(testWithdrawalAddress), withdrawableAmount);
        assertEq(currency.balanceOf(address(launch)), 0);
        assertEq(launch.getWithdrawableAmountByCurrency(address(currency)), 0);
    }

    function generateLauchGroupId(uint count) internal pure returns (bytes32) {
        return bytes32(abi.encodePacked("ch72gsb320000ud", vm.toString(count)));
    }

    function _setupLaunchGroupWithStatusOverride(bytes32 launchGroupId, LaunchGroupStatus status)
    internal returns (LaunchGroupSettings memory)
    {
        CurrencyConfig memory currencyConfig =
            CurrencyConfig({tokenPriceBps: 1 * 10 ** currency.decimals(), isEnabled: true});
        LaunchGroupSettings memory setting = LaunchGroupSettings({
            finalizesAtParticipation: false,
            startsAt: block.timestamp,
            endsAt: block.timestamp + 1 days,
            maxTokenAllocation: 10000 * 10 ** launch.tokenDecimals() * 10000,
            minTokenAmountPerUser: 500 * 10 ** launch.tokenDecimals(),
            maxTokenAmountPerUser: 3000 * 10 ** launch.tokenDecimals(),
            status: status
        });
        vm.startPrank(manager);
        launch.createLaunchGroup(launchGroupId, address(currency), currencyConfig, setting);
        vm.stopPrank();
        return setting;
    }    
}

```

name the file Launch.Withdraw.poc.t.sol and copy it to the test folder.
Then run the command 
```shell
$ forge test --match-path test/Launch.Withdraw.poc.t.sol
```
you can find the gas is over 30_000_000 in the screenshot which could cause  a denial of service (DoS)

![Image](https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/a9b8966f-cbc2-4712-a7c2-d88dc82d4213)

### Mitigation

Manually passing in the launchGroupIds parameter might be a better choice.






