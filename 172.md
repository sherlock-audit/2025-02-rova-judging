Scrawny Merlot Bat

High

# Incorrect logic in `updateParticipation` will cause incorrect Token Allocation for Users as the function updates user `Token Amount` based on `Currency Amount`

### Summary

In the `updateParticipation` function, the user `token amount` is mixed with the `currency amount` when calculating the refund and updating the total tokens requested for the user. This leads to incorrect updates of the user's token amount and potential issues with user token allocation limits.

### Root Cause

The `updateParticipation` function incorrectly uses `refundCurrencyAmount`  instead of `token amount difference`  when updating the user's token amount and checking against the minimum token amount per user. 

### Internal Pre-conditions

tokenPriceBps is other than 1. 

### External Pre-conditions

The admin creates a launchGroup and  sets launcGroupSettings-launchGroupCurrency
The user  makes a call to `updateParticipation()`

### Attack Path

1. The user makes a call to `updateParticipation()` with a new token amount that results in a refund.
2. The function calculates `refundCurrencyAmount` based on the difference in currency amounts.
3. The function incorrectly uses `refundCurrencyAmount` to update the user's token amount and check against the minimum token amount per user.
4. This leads to incorrect updates of the user's token amount and potential issues with user token allocation limits.


### Impact

Incorrect updates of the user's token amount can lead to users being unable to participate further or receive incorrect refunds. This can result in loss of funds or incorrect token allocations.

### PoC

Some evidence that token amount should be used instead of currency amount:
In the Launch.sol::participate() method all updates are done by using `token amount` not `currency amount`(see audit Tags below)
```solidity
  function participate(ParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
   
   
       ...
        // If launch group does not finalize at participation, users should perform updates instead
        // This is checked by checking if the user has already requested tokens under the launch group
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        if (userTokenAmount > 0) {
            if (!settings.finalizesAtParticipation) {
                revert MaxUserParticipationsReached(request.launchGroupId, request.userId);
            }
        }

        uint256 newUserTokenAmount = userTokenAmount + request.tokenAmount; //------------>@audit token amount is used

      ...SNIP

        // Calculate payment amount in requested currency based on token price and requested token amount
        uint256 currencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);

        ParticipationInfo storage info = launchGroupParticipations[request.launchParticipationId];

        if (settings.finalizesAtParticipation) {
            // Validate launch group max token allocation has not been reached
            (, uint256 currTotalTokensSold) = _tokensSoldByLaunchGroup.tryGet(request.launchGroupId);
            if (settings.maxTokenAllocation < currTotalTokensSold + request.tokenAmount) { //------------>@audit max Allocation is done by token amount
                revert MaxTokenAllocationReached(request.launchGroupId);
            }
            
            (, uint256 withdrawableAmount) = _withdrawableAmountByCurrency.tryGet(request.currency);
            _withdrawableAmountByCurrency.set(request.currency, withdrawableAmount + currencyAmount);
            // Mark participation as finalized
            info.isFinalized = true;
            // Update total tokens sold for launch group
            _tokensSoldByLaunchGroup.set(request.launchGroupId, currTotalTokensSold + request.tokenAmount); //------------>@audit update of totalToken amount by launch group is done by `token amount`
        }
        // Set participation details for user
        info.userAddress = msg.sender;
        info.userId = request.userId;
        info.tokenAmount = request.tokenAmount;
        info.currencyAmount = currencyAmount;
        info.currency = request.currency;

        userTokens.set(request.userId, newUserTokenAmount); //------------>@userTokens updated by `token amount`
        // Transfer payment currency from user to contract
        IERC20(request.currency).safeTransferFrom(msg.sender, address(this), currencyAmount);

    
    }
```
but in updateParticipate() `currency amount` is mixed with `token amount`.
```solidity

    function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
     

      //SNIP
   
        uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
        
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
      
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            
            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) { //--------------->@ audit currency amount is used instead of token amount
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
         
            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount); //--------------->@audit user tokens is updated by currency amount, the difference between token amount should be used
        
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
           
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
           
            userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);//--------------->@audit The same logic used here as well
           
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }

      //SNIP
    }
```
The test in Launch.UpdateParticipation.t.sol are passing because [tokenPriceBps](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/test/LaunchTestBase.t.sol#L75C29-L75C73) is set to  `1` . However tokenPriceBps can be initially set other than `1` and also can be  updated in `setLaunchGroupCurrency`.

### Mitigation

Calculate refund amount and update the token requested by the user based on `token amount difference` not `currency amount difference`

```solidity
// Calculate refund amount
uint256 refundTokenAmount = prevInfo.tokenAmount - request.tokenAmount;

if (userTokenAmount - refundTokenAmount < settings.minTokenAmountPerUser) {
    revert MinUserTokenAllocationNotReached(
        request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
    );
}
// Update total tokens requested for user for launch group
userTokens.set(request.userId, userTokenAmount - refundTokenAmount);
```