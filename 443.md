Loud Flint Leopard

High

# Incorrect Token Allocation in `updateParticipation`

### Summary

The `updateParticipation` function incorrectly updates the `userTokens` map (which tracks **project tokens**) using **currency amounts** (e.g., ETH/USDC) instead of **token deltas**. This creates a chain of corrupted state across all functions that read/write `userTokens`, leading to systemic accounting errors.

### Root Cause

In [`Launch.sol:361`](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L361) and [`Launch.sol:374`](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#374) , the `updateParticipation` function updates `userTokens` using `currencyAmount` instead of `tokenAmount`. As a result, a user's total token balance is increased based on the payment amount rather than the number of tokens they should actually receive.

#### Key Code Snippet (Simplified):
```solidity
// updateParticipation()
uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
if (prevInfo.currencyAmount > newCurrencyAmount) {
   uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
    // Refund logic uses currency delta
    userTokens.set(userTokenAmount - refundCurrencyAmount); // ❌ Uses currency, not tokens
} else {
    uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
    // Additional payment uses currency delta
    userTokens.set(userTokenAmount + additionalCurrencyAmount); // ❌
}
```

### Internal Pre-conditions

1. The launch group must allow `updateParticipation` before finalization.
2. A user must have already participated in the launch.
3. A user must submit an `UpdateParticipationRequest` to increase their token allocation.

### External Pre-conditions

1. The price of the token in terms of currency must be greater than 1 (e.g., 1 token costs more than 1 currency unit) or lesser than 1.


### Attack Path

1. The user initially participates in the launch, acquiring a certain amount of tokens.
2. The user submits an `UpdateParticipationRequest` with a higher `currencyAmount`.
3. The `updateParticipation` function incorrectly updates `userTokens` using `currencyAmount` instead of `tokenAmount`.
4. The user ends up with more tokens than they should, exploiting the incorrect calculation.

### Impact

All functions that interact with `userTokens` inherit corrupted data:

| Function             | Impact                                                                                     |
|----------------------|-------------------------------------------------------------------------------------------|
| `participate(), updateParticipation()`      | Incorrectly blocks/permits new participations due to invalid `userTokenAmount`.           |
| `cancelParticipation`| Miscalculates remaining tokens, potentially underflowing or violating min allocation.     |
| `_processRefund()`   | Subtracts wrong token amount, leading to incorrect refunds and token distribution errors. |

Which could lead to:
- Excess token distribution, inflating supply beyond intended limits.
- Financial losses for the project due to token misallocation.
- Potential manipulation by malicious users who repeatedly update their participation to exploit the bug.
- Financial losses for users who want to refund or cancel after preforming updates.

### PoC

```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {Test} from "forge-std/Test.sol";
import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {
    LaunchGroupSettings,
    ParticipationRequest,
    UpdateParticipationRequest,
    ParticipationInfo,
    CurrencyConfig
} from "../src/Types.sol";

contract LaunchUpdateParticipationTest is Test, LaunchTestBase {
    LaunchGroupSettings public settings;
    ParticipationRequest public originalParticipationRequest;

    function setUp() public {
        _setUpLaunch();

         // Fund user
         vm.prank(admin.addr);
         currency.transfer(user1, 2000e18);

        // Setup launch group and currency config with price x2
        settings = _setupLaunchGroup();
        CurrencyConfig memory currencyConfig =
            CurrencyConfig({tokenPriceBps: 2 * 10 ** currency.decimals(), isEnabled: true});
        vm.prank(manager);
        launch.setLaunchGroupCurrency(testLaunchGroupId,  address(currency), currencyConfig);
        // Setup initial participation
        originalParticipationRequest = _createParticipationRequest();
        bytes memory signature = _signRequest(abi.encode(originalParticipationRequest));

        vm.startPrank(user1);
        currency.approve(
            address(launch),
            type(uint256).max
        );
        launch.participate(originalParticipationRequest, signature);

        vm.stopPrank();
    }

    function test_UpdateParticipation_PoC_LossOfFunds() public {
        // Prepare update participation request
        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(2000);
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        // Verify user token by launch group with his original participation token amount
        ParticipationInfo memory currInfo = launch.getParticipationInfo(updateRequest.prevLaunchParticipationId);
        uint256 currUserTokenAmount = launch.getUserTokensByLaunchGroup(updateRequest.launchGroupId, updateRequest.userId);
        assertEq(currInfo.tokenAmount, currUserTokenAmount);

        vm.startPrank(user1);

        // Update participation
        launch.updateParticipation(updateRequest, updateSignature);

        // Verify update
        currInfo = launch.getParticipationInfo(updateRequest.newLaunchParticipationId);
        assertEq(currInfo.currencyAmount, 4000e18);
        assertEq(currInfo.tokenAmount, 2000e18);

        // Verify user token by launch group with participation token amount after updating
        currUserTokenAmount = launch.getUserTokensByLaunchGroup(updateRequest.launchGroupId, updateRequest.userId);
        // POC: user has more token
        assertNotEq(currInfo.tokenAmount, currUserTokenAmount);
        // updateParticipation has updated user token wiht +2000e18 (= 3000e18), the amount of currency
        assertEq(currUserTokenAmount, 3000e18);
        // while it should add 1000e18, = 2000e18
        assertEq(currInfo.tokenAmount, 2000e18);

        vm.stopPrank();
    }


    function _createUpdateParticipationRequest(uint256 newTokenAmount)
        internal
        view
        returns (UpdateParticipationRequest memory)
    {
        uint256 launchTokenDecimals = launch.tokenDecimals();
        return UpdateParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            prevLaunchParticipationId: testLaunchParticipationId,
            newLaunchParticipationId: "newLaunchParticipationId",
            userId: testUserId,
            userAddress: user1,
            tokenAmount: newTokenAmount * 10 ** launchTokenDecimals,
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }

}
```

### Mitigation

Modify `updateParticipation` to update `userTokens` using `tokenAmount` instead of `currencyAmount`

**Fix Commit Example:**  
```diff
- uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
- userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
+ uint256 newTokenAmount = userTokenAmount - prevInfo.tokenAmount + request.tokenAmount;
+ userTokens.set(request.userId, newTokenAmount);
``` 
Also, enforce token-based allocation checks of max/min token amount limit for user:
```sol
if (newTokenAmount > settings.maxTokenAmountPerUser) {
    revert MaxLimitExceeded();
}
```