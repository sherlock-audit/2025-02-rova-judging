Fierce Ash Wombat

High

# Precision Truncation Vulnerability in `_calculateCurrencyAmount` Allowing Attacker To Participate In Launch Pools Without Sending Currency

### Summary

This vulnerability arises from the precision truncation issue in `Launch::_calculateCurrencyAmount` function.
The precision truncation in `Math.muldiv` implemented in this function, means that for any values supplied by the user, where the result is a number less than a zero, solidity rounds off the number to zero.

In solidity, `Math.muldiv(x, y, denominator)` performs a multiplication followed by a division, ensuring precision.
This means:

```javascript

    currencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);

    currencyAmount =  tokenPriceBps x tokenAmount  
                   ─────────────────────────────  
                            tokenDecimals

```
 
For the currencyAmount to round down to zero, the result muct be a positive number less than 1 wei.
This will depend on the values set for `tokenPriceBps` and `tokenDecimals`
However, this contract makes use of `ERC20::decimals` for precision, so we will always assume that `tokenDecimals` will always be `1 * 10 ** 18`
 
To arrive at a value less than zero, an attacker has to set tokenAmount precisely depending on the value of tokenPriceBps

Example combinations:

Case 1: Small tokenPriceBps and tokenAmount
```javascript
    tokenPriceBps = 1
    tokenAmount = 1

        1 x 1
      ───────── = 10 ^ -18  ≈ 0
       10 ^18
    currencyAmount = 0
```

Case 2: Small tokenPriceBps with low amount
```javascript
    tokenPriceBps = 10
    tokenAmount = 10

        10 x 10
      ───────── = 10 ^ -16  ≈ 0
       10 ^18
    currencyAmount = 0
```

Case 3: Larger amounts, but within threshold

```javascript
    tokenPriceBps = 10^9
    tokenAmount = 10^9

        10^9 x 10^9
      ───────────── = 10 ^ -1  ≈ 0.1
       10 ^18
    currencyAmount = 0
```

Case 4: Edge case to show impact

```javascript
    tokenPriceBps = 10^12
    tokenAmount = 10^6-1

        10^12 x 10^6
      ─────────────     ≈ 0.999999
       10 ^18
    currencyAmount = 0
```

The result of this. means an attacker can fill a whole launch pool without paying any currencytokens to the contract.

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L265



### Root Cause

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L596-L598

`Math.muldiv` will truncate any values less than 0 to zero.

### Internal Pre-conditions

Launch pool is active
Admin has set tokenPriceBps


### External Pre-conditions

None

### Attack Path

1. Manager creates a launch group
2. Attacker calculates a value for `tokenAmount` that will result in `currencyAmount` being rounded to zero
3. Attacker calls `participate` from multiple addresses to consume the `maxTokenAllocation`for the pool

### Impact

An attacker can join a launch pool without sending any currency tokens to the contract
Since this is a low gas transaction, an attacker can prevent any other legitimate users from participating in the launch by setting up many addresses to participate and consume the `maxTokenAllocation` without sending any funds - the attacker does not suffer any financial losses.

### PoC
1. Create a launch group
2. set tokenPriceBps = 10^12 and tokenDecimals to 10^18
3. Create a participation request with tokenAmount = 10^6-1
4. Participate.

### Mitigation

Revert on zero `currencyAmount` values.

In `Launch::participate` function

```diff
    function participate(ParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        // Validate request is intended for this launch and unexpired
        _validateRequest(
            request.launchId, request.launchGroupId, request.chainId, request.requestExpiresAt, request.userAddress
        );
        LaunchGroupSettings memory settings = launchGroupSettings[request.launchGroupId];

        // Validate launch group is open for participation
        _validateTimestamp(settings);

        // Validate request signature is from signer role
        _validateRequestSignature(keccak256(abi.encode(request)), signature);

        // Validate payment currency is enabled for launch group
        uint256 tokenPriceBps = _validateCurrency(request.launchGroupId, request.currency);

        // Do not allow replay of launch participation ID
        if (launchGroupParticipations[request.launchParticipationId].userId != bytes32(0)) {
            revert ParticipationAlreadyExists(request.launchParticipationId);
        }

        // If launch group does not finalize at participation, users should perform updates instead
        // This is checked by checking if the user has already requested tokens under the launch group
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        if (userTokenAmount > 0) {
            if (!settings.finalizesAtParticipation) {
                revert MaxUserParticipationsReached(request.launchGroupId, request.userId);
            }
        }

        // Validate user requested token amount is within launch group user allocation limits
        uint256 newUserTokenAmount = userTokenAmount + request.tokenAmount;
        if (newUserTokenAmount > settings.maxTokenAmountPerUser) {
            revert MaxUserTokenAllocationReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }
        if (newUserTokenAmount < settings.minTokenAmountPerUser) {
            revert MinUserTokenAllocationNotReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }

        // Calculate payment amount in requested currency based on token price and requested token amount
        uint256 currencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);

++      require(currencyAmount > 0, "Currency amount too low");

        // Store participation info for user
        ParticipationInfo storage info = launchGroupParticipations[request.launchParticipationId];

        // If launch group finalizes at participation, the participation is considered complete and not updatable
        if (settings.finalizesAtParticipation) {
            // Validate launch group max token allocation has not been reached
            (, uint256 currTotalTokensSold) = _tokensSoldByLaunchGroup.tryGet(request.launchGroupId);
            if (settings.maxTokenAllocation < currTotalTokensSold + request.tokenAmount) {
                revert MaxTokenAllocationReached(request.launchGroupId);
            }
            // Update total withdrawable amount for payment currency
            (, uint256 withdrawableAmount) = _withdrawableAmountByCurrency.tryGet(request.currency);
            _withdrawableAmountByCurrency.set(request.currency, withdrawableAmount + currencyAmount);
            // Mark participation as finalized
            info.isFinalized = true;
            // Update total tokens sold for launch group
            _tokensSoldByLaunchGroup.set(request.launchGroupId, currTotalTokensSold + request.tokenAmount);
        }
        // Set participation details for user
        info.userAddress = msg.sender;
        info.userId = request.userId;
        info.tokenAmount = request.tokenAmount;
        info.currencyAmount = currencyAmount;
        info.currency = request.currency;

        // Update total tokens requested for user for launch group
        userTokens.set(request.userId, newUserTokenAmount);
        // Transfer payment currency from user to contract
        IERC20(request.currency).safeTransferFrom(msg.sender, address(this), currencyAmount);

        emit ParticipationRegistered(
            request.launchGroupId,
            request.launchParticipationId,
            request.userId,
            msg.sender,
            currencyAmount,
            request.currency
        );
    }

```
