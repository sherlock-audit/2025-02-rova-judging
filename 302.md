Tame Sapphire Mammoth

Medium

# set payment_amount to 0 to participate with token_amount

### Summary

users can pay funds through the fund() function to participate in a token sale. however, the fund() function does not validate the payment_amount parameter against any minimum or maximum limits. consequently, a user can set payment_amount to 0 and specify a desired token_amount to participate in the sale

### Root Cause

```move
    /// Fund tokens with signature verification
    public entry fun fund(
        user: &signer,
        signature_bytes: vector<u8>,
        public_key_bytes: vector<u8>,
        launch_participation_id: vector<u8>,
        token_amount: u64,
        payment_amount: u64
    ) acquires SaleConfig, Roles {
        let user_addr = signer::address_of(user);
        let sale_config = borrow_global<SaleConfig>(@rova_sale_addr);

        // Verify sale is active
        let time_now = timestamp::now_seconds();
        assert!(time_now >= sale_config.starts_at && time_now <= sale_config.ends_at, error::invalid_state(ESALE_NOT_ACTIVE));

        // Check sale is not paused
        assert!(!sale_config.paused, error::invalid_state(ESALE_PAUSED));

        // Verify launch participation id hasn't been used (prevent replay)
        assert!(
            !has_launch_participation_id(launch_participation_id),
            error::invalid_argument(EINVALID_LAUNCH_PARTICIPATION_ID)
        );
        
        // Verify signature
        let signature = ed25519::new_signature_from_bytes(signature_bytes);
        let unvalidated_public_key = ed25519::new_unvalidated_public_key_from_bytes(public_key_bytes);
        let message = vector::empty<u8>();
        vector::append(&mut message, bcs::to_bytes(&user_addr));
        vector::append(&mut message, bcs::to_bytes(&payment_amount));
        vector::append(&mut message, bcs::to_bytes(&token_amount));
        vector::append(&mut message, bcs::to_bytes(&launch_participation_id));

        assert!(
            ed25519::signature_verify_strict(
                &signature,
                &unvalidated_public_key,
                message
            ),
            error::invalid_argument(EINVALID_SIGNATURE)
        );

        // Verify signer is authorized
        let derived_address = derive_address(&unvalidated_public_key); 
        let roles = borrow_global<Roles>(@rova_sale_addr);
        only_role_address(derived_address, roles.signer_role);

        // Transfer payment
        let coin = coin::withdraw<AptosCoin>(user, payment_amount);
        aptos_account::deposit_coins(@rova_sale_addr, coin);

        // Register launch_participation_id as used
        let sale_config = borrow_global_mut<SaleConfig>(@rova_sale_addr);
        table::add(&mut sale_config.launch_participation_registry, launch_participation_id, true);

        // Emit funding event
        event::emit(
            FundingEvent {
                user: user_addr,
                amount: payment_amount,
                tokens: token_amount
            }
        );
    }
// https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-movement-contracts/sources/rova_sale.move#L125L188
```
because the transfer payment logic doesn't accurately validate the payment_amount value beforehand, a user can effectively set it to 0

```move
// https://github.com/aptos-labs/aptos-core/blob/1e4f706449dee6c989b9f84b81361dce50bd9c88/aptos-move/framework/aptos-framework/sources/aptos_account.move#L117L134
    public fun deposit_coins<CoinType>(to: address, coins: Coin<CoinType>) acquires DirectTransferConfig {
        if (!account::exists_at(to)) {
            create_account(to);
            spec {
                assert coin::spec_is_account_registered<AptosCoin>(to);
                assume aptos_std::type_info::type_of<CoinType>() == aptos_std::type_info::type_of<AptosCoin>() ==>
                    coin::spec_is_account_registered<CoinType>(to);
            };
        };
        if (!coin::is_account_registered<CoinType>(to)) {
            assert!(
                can_receive_direct_coin_transfers(to),
                error::permission_denied(EACCOUNT_DOES_NOT_ACCEPT_DIRECT_COIN_TRANSFERS),
            );
            coin::register<CoinType>(&create_signer(to));
        };
        coin::deposit<CoinType>(to, coins)
    }
```
deposit_coins() 

```move
// https://github.com/aptos-labs/aptos-core/blob/1e4f706449dee6c989b9f84b81361dce50bd9c88/aptos-move/framework/aptos-framework/sources/coin.move#L855L887
    /// Deposit the coin balance into the recipient's account and emit an event.
    public fun deposit<CoinType>(
        account_addr: address,
        coin: Coin<CoinType>
    ) acquires CoinStore, CoinConversionMap, CoinInfo {
        if (exists<CoinStore<CoinType>>(account_addr)) {
            let coin_store = borrow_global_mut<CoinStore<CoinType>>(account_addr);
            assert!(
                !coin_store.frozen,
                error::permission_denied(EFROZEN),
            );
            if (std::features::module_event_migration_enabled()) {
                event::emit(
                    CoinDeposit { coin_type: type_name<CoinType>(), account: account_addr, amount: coin.value }
                );
            } else {
                event::emit_event<DepositEvent>(
                    &mut coin_store.deposit_events,
                    DepositEvent { amount: coin.value },
                );
            };
            merge(&mut coin_store.coin, coin);
        } else {
            let metadata = paired_metadata<CoinType>();
            if (option::is_some(&metadata) && can_receive_paired_fungible_asset(
                account_addr,
                option::destroy_some(metadata)
            )) {
                primary_fungible_store::deposit(account_addr, coin_to_fungible_asset(coin));
            } else {
                abort error::not_found(ECOIN_STORE_NOT_PUBLISHED)
            };
        }
    }

// https://github.com/aptos-labs/aptos-core/blob/1e4f706449dee6c989b9f84b81361dce50bd9c88/aptos-move/framework/aptos-framework/sources/coin.move#L1071L1083
 public fun merge<CoinType>(dst_coin: &mut Coin<CoinType>, source_coin: Coin<CoinType>) {
        spec {
            assume dst_coin.value + source_coin.value <= MAX_U64;
        };
        spec {
            update supply<CoinType> = supply<CoinType> - source_coin.value;
        };
        let Coin { value } = source_coin;
        spec {
            update supply<CoinType> = supply<CoinType> + value;
        };
        dst_coin.value = dst_coin.value + value;
    }
```
the deposit_coins() function validates the provided "to" address and creates it if it doesn't exist. subsequently, it calls the coin::deposit() function

in the coin::deposit() function, without any validation on the coin, it calls the merge() function to merge the data and update the balance. even within the merge() function, it doesn't check if the coin.value is 0, and simply adds it to the previous balance

### Internal Pre-conditions

A sale must be active for users to participate

### External Pre-conditions

there are hardly any preconditions

### Attack Path

when calling the fund() function, set payment_amount to 0

### Impact

users can participate in the token sale without paying any balance

### PoC

```move
#[test_only]
module rova_sale_addr::rova_sale_tests {
    use std::bcs;
    use std::signer;
    use std::vector;
    use aptos_std::ed25519::{Self};
    use aptos_std::from_bcs;
    use aptos_framework::account;
    use aptos_framework::coin;
    use aptos_framework::aptos_coin::{Self, AptosCoin};
    use aptos_framework::aptos_account::{Self};
    use aptos_framework::timestamp;
    use rova_sale_addr::rova_sale::{Self};

    // Error constants
    const ENOT_ADMIN: u64 = 1;
    const EINVALID_SIGNATURE: u64 = 5;
    const EINVALID_SALE_PERIOD: u64 = 7;
    const ESALE_NOT_ACTIVE: u64 = 8;

    // Test accounts
    const ADMIN: address = @rova_sale_addr;
    const USER: address = @0x456;
    const SIGNER: address = @0x123;
    const WITHDRAWAL_ADDR: address = @withdrawal_addr;

    #[test(admin = @rova_sale_addr, framework = @aptos_framework, user = @0x456)]
    public entry fun test_fund(admin: &signer, framework: &signer, user: &signer) {
        // Setup sale
        setup_sale_config(admin, framework);

        // Generate signature
        let launch_participation_id = b"cm6zl5lha00003b712h28v7cv";
        let token_amount = 100;
        let payment_amount = 0;
        let (_signer_addr, signature_bytes, public_key_bytes) = generate_signature(admin, user, launch_participation_id, token_amount, payment_amount);

        // Fund
        let user_addr = signer::address_of(user);
        rova_sale::fund(user, signature_bytes, public_key_bytes, launch_participation_id, token_amount, payment_amount);
    }

    fun setup_test(admin: &signer, framework: &signer) {
        // Start time
        timestamp::set_time_has_started_for_testing(framework);
        
        // Initialize module
        rova_sale::init_module_for_test(admin);

        // Initialize AptosCoin
        let (burn_cap, mint_cap) = aptos_coin::initialize_for_test(framework);
        let coins = coin::mint<AptosCoin>(1000000000, &mint_cap);
        // aptos_account::deposit_coins(signer::address_of(admin), coins);
        aptos_account::deposit_coins(USER, coins);
        coin::destroy_burn_cap(burn_cap);
        coin::destroy_mint_cap(mint_cap);
    }

    fun setup_sale_config(admin: &signer, framework: &signer) {
        // Setup
        setup_test(admin, framework);

        // Set sale period
        let start_time = timestamp::now_seconds();
        let end_time = start_time + 1000;
        rova_sale::set_sale_period(admin, start_time, end_time);

        // Unpause
        rova_sale::set_pause(admin, false);
    }

    fun generate_signature(
        admin: &signer,
        user: &signer,
        launch_participation_id: vector<u8>,
        token_amount: u64,
        payment_amount: u64
    ): (address, vector<u8>, vector<u8>) {
        let user_addr = signer::address_of(user);
        // Create message
        let message = vector::empty<u8>();
        vector::append(&mut message, bcs::to_bytes(&user_addr));
        vector::append(&mut message, bcs::to_bytes(&payment_amount));
        vector::append(&mut message, bcs::to_bytes(&token_amount));
        vector::append(&mut message, bcs::to_bytes(&launch_participation_id));

        // Sign message using signer
        let (sk, pk) = ed25519::generate_keys();
        let signature = ed25519::sign_arbitrary_bytes(&sk, message);
        let signature_bytes = ed25519::signature_to_bytes(&signature);
        let public_key_bytes = ed25519::validated_public_key_to_bytes(&pk);
    
        // Add signer
        let signer_addr = from_bcs::to_address(ed25519::validated_public_key_to_authentication_key(&pk));
        rova_sale::manage_role(admin, 1, signer_addr, true);

        (signer_addr, signature_bytes, public_key_bytes)
    }
}
```

### Mitigation

_No response_