Funny Glass Aardvark

High

# Participant ends up Paying Less for Tokens Allocation

### Summary

Currency amount is calculated using the Launch Contract tokenDecimals at https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L62

A group launches a token which can have more than one currency, with a possibility of different tokenDecimals for these currencies.
And the token they wish to allocate has a decimal different from the Launch tokenDecimals value as well.

If a user selects a currency and participates in such group, they can end up getting tokens allocation even when paying less.

### Root Cause

In https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L596-L599

The function calculates using the Launch tokenDecimals value.

The choice to set one token Decimal for all launch groups, when there is no check that the launch group token to be allocated, or/and the currency to be used must have the same decimals as the set tokenDecimals, will cause users to be allocated tokens, by paying less the price for that amount of token.

### Internal Pre-conditions

1. Launch contract is deployed with tokenDecimals set to 18.
2. Manager creates a launchGroup with a currency and token to be allocated of different decimal values (USDC for example is an ERC20 token with decimals 6, token to be allocated has 6 decimals, token price for the currency is set to 1e12)

### External Pre-conditions

No external protocols used.

### Attack Path

1. Launch contract is deployed with tokenDecimals set to 18.
2. Manager creates a launchGroup with a currency (sets currency price to token to 1e12) and token to be allocated of different decimal (USDC for example is an ERC20 token with decimals 6) and if token to be allocated has 6 decimals.
3. Participant wishes to buy 1 amount of tokens using such currency.
4. The participation goes through, but user1 ends up paying 1 (note, not 1e12, but 1) currencyToken based on _calculateCurrencyAmount function (https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L596-L599) calculation, and https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L295 will transfer 1 (not 1e12 but 1) currencyToken (as the calculation works with the assigned launch tokenDecimals which is 18 in this case) for the amount of tokens participant is allocated (which is suppose to be 1e6 token allocation at the price of 1e12).

### Impact

The Launch group ends up with less currency token, as users can get maxTokenAmountPerUser allocation, by paying less.

### PoC

Inside remappings.txt

add - @openzeppelin-foundry-upgrades-mocks=lib/openzeppelin-foundry-upgrades/lib/forge-std/src/mocks

inside test folder, create a new file and name it Launch.WrongCurrencyAmounCharge.t.sol and paste the code below

/// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.22;

import {IERC20Errors} from "@openzeppelin/contracts/interfaces/draft-IERC6093.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import {UnsafeUpgrades} from "@openzeppelin-foundry-upgrades/Upgrades.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {Math} from "@openzeppelin/contracts/utils/math/Math.sol";
import {VmSafe} from "forge-std/Vm.sol";
import {Test} from "forge-std/Test.sol";
import {MockERC20} from "@openzeppelin-foundry-upgrades-mocks/MockERC20.sol";
import {Launch} from "../src/Launch.sol";
import {
    CancelParticipationRequest,
    LaunchGroupSettings,
    LaunchGroupStatus,
    ParticipationRequest,
    ParticipationInfo,
    CurrencyConfig
} from "../src/Types.sol";

interface IERC20Events {
    event Transfer(address indexed from, address indexed to, uint256 value);
}

contract MockerERC20 is MockERC20 {
    constructor() {
        initialize("Mock Token", "MTK", 6);
        _mint(msg.sender, 1000000 * 10 ** 6);
    }
}

contract LaunchParticipateTest is Test, Launch {
    Launch public launch;
    MockERC20 public currency;

    VmSafe.Wallet public admin = vm.createWallet("admin");
    address public manager = address(1);
    address public operator = address(2);
    address public signer = address(3);
    address public testWithdrawalAddress = address(4);
    address public user1 = address(5);
    address public user2 = address(6);

    // Dummy cuids for testing
    bytes32 public testLaunchId = "cixf02ym000001b66m45ae4k8";
    bytes32 public testLaunchGroupId = "ch72gsb320000udocl363eofy";
    bytes32 public testLaunchParticipationId = "cm6o2sldi00003b74facm5z9n";
    bytes32 public testUserId = "cm6o2tm1300003b74dsss1s7q";

    function _setUpLaunch() public {
        vm.startPrank(admin.addr);

        // Deploy contracts
        _initializeLaunch(admin.addr, testWithdrawalAddress);
        currency = new MockerERC20();

        // Setup roles
        launch.grantRole(MANAGER_ROLE, manager);
        launch.grantRole(OPERATOR_ROLE, operator);
        launch.grantRole(SIGNER_ROLE, signer);

        // Fund users
        currency.transfer(user1, 1 * 10 ** 6);
        currency.transfer(user2, 1 * 10 ** 6);
        vm.stopPrank();
    }

    function _setupLaunchGroup() internal returns (LaunchGroupSettings memory) {
        return _setupLaunchGroupWithStatus(testLaunchGroupId, LaunchGroupStatus.ACTIVE);
    }

    function _setupLaunchGroupWithStatus(bytes32 launchGroupId, LaunchGroupStatus status)
        internal
        returns (LaunchGroupSettings memory)
    {
        CurrencyConfig memory currencyConfig =
            CurrencyConfig({tokenPriceBps: 1 * 10 ** 12, isEnabled: true});
            LaunchGroupSettings memory settings = LaunchGroupSettings({
            finalizesAtParticipation: true,
            startsAt: block.timestamp,
            endsAt: block.timestamp + 1 days,
            maxTokenAllocation: 10 * 10 ** 6,
            minTokenAmountPerUser: 5 * 10 ** 5,
            maxTokenAmountPerUser: 1 * 10 ** 6,
            status: status
        });
        vm.startPrank(manager);
        launch.createLaunchGroup(launchGroupId, address(currency), currencyConfig, settings);
        vm.stopPrank();
        return settings;
    }

    function _signRequest(bytes memory encodedRequest) internal view returns (bytes memory) {
        bytes32 structHash = keccak256(encodedRequest);
        bytes32 messageHash = keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", structHash));
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(admin.privateKey, messageHash);
        return abi.encodePacked(r, s, v);
    }

    function _createParticipationRequest() internal view returns (ParticipationRequest memory) {
        return ParticipationRequest({
            chainId: block.chainid,
            launchId: testLaunchId,
            launchGroupId: testLaunchGroupId,
            launchParticipationId: testLaunchParticipationId,
            userId: testUserId,
            userAddress: user1,
            tokenAmount: 1 * 10 ** 6,
            currency: address(currency),
            requestExpiresAt: block.timestamp + 1 hours
        });
    }

    function setUp() public {
        _setUpLaunch();
    }

    function test_WrongCurrencyChargedForTokenAllocation() public {
        // Setup launch group
        _setupLaunchGroup();

        // Prepare participation request
        ParticipationRequest memory request = _createParticipationRequest();
        bytes memory signature = _signRequest(abi.encode(request));

        vm.startPrank(user1);
        uint256 currencyAmount = _getCurrencyAmount(request.launchGroupId, request.currency, request.tokenAmount);
        currency.approve(address(launch), currencyAmount);

        // Expect ParticipationRegistered event
        vm.expectEmit();
        emit ParticipationRegistered(
            request.launchGroupId, request.launchParticipationId, testUserId, user1, currencyAmount, address(currency)
        );

        // Participate
        launch.participate(request, signature);

        ParticipationInfo memory info = launch.getParticipationInfo(request.launchParticipationId);
        assertEq(info.userAddress, user1);
        assertEq(info.userId, testUserId);
        assertEq(info.tokenAmount, request.tokenAmount);
        assertEq(info.currencyAmount, currencyAmount);
        assertEq(info.currency, address(currency));
        assertEq(info.isFinalized, true);

        assertEq(currency.balanceOf(address(launch)), 1e12);
        assertEq(launch.getUserTokensByLaunchGroup(testLaunchGroupId, testUserId), request.tokenAmount);
        vm.stopPrank();

    }

    function _getCurrencyAmount(bytes32 launchGroupId, address currencyAddress, uint256 tokenAmount)
        internal
        view
        returns (uint256)
    {
        uint256 tokenPriceBps = launch.getLaunchGroupCurrencyConfig(launchGroupId, currencyAddress).tokenPriceBps;
        return Math.mulDiv(tokenPriceBps, tokenAmount, 10 ** launch.tokenDecimals());
    }

    function _initializeLaunch(address adminAddress, address withdrawalAddress) internal {
        address proxyAddress = UnsafeUpgrades.deployTransparentProxy(
            address(new Launch()),
            adminAddress,
            abi.encodeWithSelector(Launch.initialize.selector, withdrawalAddress, testLaunchId, adminAddress, 18)
        );
        launch = Launch(proxyAddress);
    }
}

***
run this in project terminal (forge t Launch.WrongCurrencyCharge.t.sol -vvvvv)

You will get the result below 

<img width="882" alt="Image" src="https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/4705a8d6-fe7b-4a2a-a7e0-c038326045ba" />

The currency balance of the user1 is subtracted just by 1, instead of 1e12.
The currency balance of the launchContract is 1 instead of 1e12, which is the price for one token to be allocated.
The tokens allocated to the user1 updates to 1, user1 pays 1 instead of 1e12.


### Mitigation

I recommend the protocol makes the _calculateCurrencyAmount function calculate with the selected currency token decimals (by using request.currency.decimals(), instead of the launch.sol state variable - tokenDecimals()).

Or simply make sure all launch group has tokens allocated in 18 decimals, currency token in 18 decimals.