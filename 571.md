Colossal Cider Starling

High

# Allocation per user is incorrectly calculated in "updateParticipation()"

### Summary

Allocation per user is incorrectly calculated in `updateParticipation()` - currency amount (refunded or additional) treated as project token amount, which is wrong, because allocation checks and updates should be applied based on new `PT` (requested project token) amount. Also `newCurrencyAmount` will never be equal to `request.tokenAmount`, because it is calculated based on requested amount and `tokenPriceBps`.

### Root Cause

[Link](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L346-L377)

Requested user tokens are stored in `_userTokensByLaunchGroup[request.launchGroupId]`. During participation updation, the variable should be increased/decreased by difference between old `userTokenAmount` and new `request.tokenAmount`.

```solidity
        uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
        // Get total tokens requested for user for launch group
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        // If new requested token amount is less than old amount, handle refund
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }
```
However, differrense between `prevInfo.currencyAmount` and `newCurrencyAmount` is used instead. Also it used to check user allocation limits, which is also incorrect.

### Internal Pre-conditions

User needs to call `updateParticipation()` with new `request.tokenAmount`.
Intended price conversion should be != 1.
Project and currency tokens should have different decimals.

### External Pre-conditions

None

### Attack Path

1. Assume that intended price conversion = 0.1, max token allocation per user = 1500;
2. Amelie calls `participate()` with requested 1000 `PT` and paid 100 `USDT` as currency amount;
3. Amelie calls `updateParticipation()` with new requested 1500 `PT` and paid 50 `USDT` as `additionalCurrencyAmount`;
4. Her new allocation should be 1500 `PT`, but it was calculated incorrectly:
```solidity
userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
//userTokens = userTokenAmount + additionalCurrencyAmount = 1000 + 50 = 1050
```
5. Now Amelie can create another participation in the same group with additional 450 `PT`, while her real allocation will be 1950 `PT`, which breaks allocation limits.

### Impact

Allocation per user will be always incorrectly calculated in `updateParticipation()`. In some cases, `_userTokensByLaunchGroup` will be smaller than actual than actual requested tokens, allowing users to request above the allocation limits. In other cases,  `_userTokensByLaunchGroup` will be bigger than actual than actual requested tokens, do not allowing users to create eligible participations. Also, lines `355`and `368` might revert even if new requested token amount is within launch group user allocation limits, because it uses currency amount instead of project token amount.

### PoC

_No response_

### Mitigation

Check allocation limits and update `_userTokensByLaunchGroup` based on new requested token amount instead of currency amount