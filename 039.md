Quick Sangria Bat

High

# Launch.sol :: updateParticipation() uses `refundCurrencyAmount` instead of `request.tokenAmount` to update the user's position, leading to incorrect accounting.

### Summary

`updateParticipation()` is designed to modify an existing position. However, instead of using `request.tokenAmount` (the launch token) to update the user's launchpadToken balance, it incorrectly utilizes `newCurrencyAmount` (the currency payment). This results in inaccurate accounting of the user's LaunchToken balance.

### Root Cause

Let's start by specifying that `tokenAmount` refers to the **Launchpad Token**, while `currencyAmount` represents the token used to purchase the **Launchpad Token**.  

When a user wants to participate in the launchpad, they call the [participate()](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L215-L305).
```solidity
function participate(ParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        // Validate request is intended for this launch and unexpired
        _validateRequest(
            request.launchId, request.launchGroupId, request.chainId, request.requestExpiresAt, request.userAddress
        );
        LaunchGroupSettings memory settings = launchGroupSettings[request.launchGroupId];

        // Validate launch group is open for participation
        _validateTimestamp(settings);

        // Validate request signature is from signer role
        _validateRequestSignature(keccak256(abi.encode(request)), signature);

        // Validate payment currency is enabled for launch group
        uint256 tokenPriceBps = _validateCurrency(request.launchGroupId, request.currency);

        // Do not allow replay of launch participation ID
        //@audit using participate cant modify it because this
        if (launchGroupParticipations[request.launchParticipationId].userId != bytes32(0)) {
            revert ParticipationAlreadyExists(request.launchParticipationId);
        }

        // If launch group does not finalize at participation, users should perform updates instead
        // This is checked by checking if the user has already requested tokens under the launch group
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        if (userTokenAmount > 0) {
            if (!settings.finalizesAtParticipation) {
                revert MaxUserParticipationsReached(request.launchGroupId, request.userId);
            }
        }
        // Validate user requested token amount is within launch group user allocation limits
        uint256 newUserTokenAmount = userTokenAmount + request.tokenAmount;
@>      if (newUserTokenAmount > settings.maxTokenAmountPerUser) {
            revert MaxUserTokenAllocationReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }
@>      if (newUserTokenAmount < settings.minTokenAmountPerUser) {
            revert MinUserTokenAllocationNotReached(
                request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
            );
        }

        // Calculate payment amount in requested currency based on token price and requested token amount
@>      uint256 currencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);

///code...
```
As you can see, it first checks if the `newUserTokenAmount` (launchpadToken) to be purchased falls within the range defined by the `maxTokenAmountPerUser` and `minTokenAmountPerUser` parameters. If it does, it calculates the amount of **currency** the user needs to pay (`currencyAmount`) for that number of `launchpadToken`. 

Now, let's look at [updateParticipation()](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L312-L397), which is used to modify an existing position.
```solidity
function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
      
        ///code...

        // Calculate new payment amount in requested currency based on token price and requested token amount
@>      uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
        // Get total tokens requested for user for launch group
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        // If new requested token amount is less than old amount, handle refund
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
@>          uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
@>          if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
@>          userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }

        ///code...
    }
```
First, the function calculates how much **currency** is needed to pay for the new `request.tokenAmount` (Launchpad Token). It then compares this amount with the previous request. The issue arises inside the condition `prevInfo.currencyAmount > newCurrencyAmount` that is used for the example, but the same problem also occurs when `newCurrencyAmount > prevInfo.currencyAmount`.

The function correctly calculates the refund amount that needs to be returned to the user in `refundCurrencyAmount`. However, the issue arises in the following line, where it subtracts `userTokenAmount - refundCurrencyAmount` to check if the new **Launchpad Token** position meets the minimum required amount of Launchpad Token.  
This calculation is incorrect because `userTokenAmount` represents the **Launchpad Token**, while `refundCurrencyAmount` is the **currency used to purchase Launchpad Tokens**. Since this check should only apply to the **Launchpad Token**, the subtraction is invalid.

Next, when calling `userTokens.set()`, the same issue occurs. The function updates the user's position for the **Launchpad Token** but incorrectly calculates it using `userTokenAmount - refundCurrencyAmount`.  
Since `userTokenAmount` represents the **Launchpad Token** and `refundCurrencyAmount` represents the **currency used for the purchase**, subtracting them leads to incorrect accounting, resulting in an inaccurate token balance.

The same issue occurs in the `else if (newCurrencyAmount > prevInfo.currencyAmount)` condition.

### Internal Pre-conditions

None.

### External Pre-conditions

The user updates their position.

### Attack Path

None.

### Impact

The updated positions will be incorrectly accounted for in the `launchpadToken` balance.

### PoC

To better understand the problem, let's look at an example. Assume both the **launchpadToken** and **currencyToken** each have 18 decimals, and the price is 2 **launchpadToken** for 1 **currencyToken**, with the price remaining unchanged and `minTokenAmountPerUser = 500`. First, we'll explore the incorrect implementation, followed by the correct one.

### Incorrect Implementation:

1. The user calls `participate()` with 2000 **launchpadToken** and pays 1000 **currencyToken**.
2. The user updates their position by calling `updateParticipation()` with 1000 **launchpadToken**, so `newCurrencyAmount` will be 500 (based on the 2:1 ratio). The `refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount = 1000 - 500 = 500`.
3. The user’s position is updated using `userTokenAmount - refundCurrencyAmount = 2000 - 500 = 1500`. This is incorrect because it should be 1000, not 1500.
4. The user receives 500 **currencyToken**, but the **launchpadToken** balance is not correctly adjusted, it doesn't decrease by half as expected.

### Correct Implementation:

1. The user calls `participate()` with 2000 **launchpadToken** and pays 1000 **currencyToken**.
2. The user updates their position by calling `updateParticipation()` with 1000 **launchpadToken**, so `newCurrencyAmount` will be 500 (based on the 2:1 ratio). The `refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount = 1000 - 500 = 500`.
3. The user’s position is correctly updated using `userTokenAmount - request.tokenAmount = 2000 - 1000 = 1000`.
4. The user receives 500 **currencyToken**, and the **launchpadToken** balance is correctly decreased by half.

This issue can be exploited by a malicious user to receive more **launchpadToken** while paying fewer **currencyToken**, as demonstrated in the incorrect example.

Moreover, incorrect accounting in [_processRefund()](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L620) could cause an underflow if `userTokenAmount < info.tokenAmount`, preventing the user from refunding their assets.  

### Mitigation

To solve the problem, use `request.tokenAmount` instead of `refundCurrencyAmount` to check the minimum or maximum `tokenAmount` and to recalculate the user's balance of `tokenAmount`.
```diff
function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        // Validate request is intended for this launch and unexpired
        _validateRequest(
            request.launchId, request.launchGroupId, request.chainId, request.requestExpiresAt, request.userAddress
        );
        // Validate launch group is open for participation
        LaunchGroupSettings memory settings = launchGroupSettings[request.launchGroupId];
        _validateTimestamp(settings);
        // Validate request signature is from signer role
        _validateRequestSignature(keccak256(abi.encode(request)), signature);
        // Validate payment currency is enabled for launch group
        uint256 tokenPriceBps = _validateCurrency(request.launchGroupId, request.currency);

        ParticipationInfo storage prevInfo = launchGroupParticipations[request.prevLaunchParticipationId];
        // If launch group finalizes at participation, the participation is considered complete and not updatable
        if (settings.finalizesAtParticipation || prevInfo.isFinalized) {
            revert ParticipationUpdatesNotAllowed(request.launchGroupId, request.prevLaunchParticipationId);
        }

        // Validate participation exists and user, requested currency match
        ParticipationInfo storage newInfo = launchGroupParticipations[request.newLaunchParticipationId];
        if (request.currency != prevInfo.currency) {
            revert CurrencyMismatch(prevInfo.currency, request.currency);
        }
        if (request.userId != prevInfo.userId) {
            revert UserIdMismatch(prevInfo.userId, request.userId);
        }

        // Calculate new payment amount in requested currency based on token price and requested token amount
        uint256 newCurrencyAmount = _calculateCurrencyAmount(tokenPriceBps, request.tokenAmount);
        // Get total tokens requested for user for launch group
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[request.launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(request.userId);
        // If new requested token amount is less than old amount, handle refund
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
-           if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
+           if (userTokenAmount - request.tokenAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
-            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
+            userTokens.set(request.userId, userTokenAmount - request.tokenAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
-           if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
+           if (userTokenAmount + request.tokenAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
-           userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
+           userTokens.set(request.userId, userTokenAmount + request.tokenAmount);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }

        // Set participation details for user
        newInfo.currencyAmount = newCurrencyAmount;
        newInfo.currency = request.currency;
        newInfo.userAddress = msg.sender;
        newInfo.userId = request.userId;
        newInfo.tokenAmount = request.tokenAmount;
        // Reset previous participation info
        prevInfo.currencyAmount = 0;
        prevInfo.tokenAmount = 0;

        emit ParticipationUpdated(
            request.launchGroupId,
            request.newLaunchParticipationId,
            request.userId,
            msg.sender,
            request.tokenAmount,
            request.currency
        );
    }
```