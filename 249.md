Attractive Midnight Halibut

High

# Updating paritcipations breaks the system

### Summary

When participations get updated, the `_userTokensByLaunchGroup` storage variable is wrongly updated. 

### Root Cause

The  following [if-else](https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L351-L377) statement in `Launch.updatePariticipation()` mixes up currency token amounts with the launch token amounts. After updating the request, the new `userTokens` value must be adjusted by `request.tokenAmount` (increasing or decreasing). 

In the `if` case, they are set as `userTokenAmount - refundCurrencyAmount`. This is wrong as `userTokenAmount` is the amount of launch tokens the user will have receive after the launch ended and `refundCurrencyAmount` is the amount of currency token the user will be refunded. The token amounts are also mixed when checking if the resulting `tokenAmount` is in the allowed range.

The same is true for the `else` case.

### Internal Pre-conditions

For this problem to happen, there are 2 internal preconditions. If either one of them exists, the problem will occur.
 - The price of 1 launch token is not 1 currency token.
 - The launch token and the currency token have different decimals.

### External Pre-conditions

None.

### Attack Path

1. The price for one launch token is 2 xTokens
2. User enters the launch for 1000 launch tokens by paying 2000 xTokens
3. User calls `updateParticipation` to increase their launch tokens to 2000.
4. `additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount` => `additionalCurrencyAmount = 4000 - 2000 = 2000`
5. `userTokens = userTokenAmount + additionalCurrencyAmount` => `userTokens = 1000 + 2000`.
6.  Transfer 2000 xTokens from the user.
7.  `userTokens = userTokenAmount + additionalCurrencyAmount` => `userTokens = 1000 + 2000 = 3000`
8. The user paid `4000` xTokens and should have received `2000` launch tokens, but they now have `3000`.

### Impact

There are many different impacts: DOS because  mixed amounts cause validations to fail and mathematical operations to underflow; stealing more tokens than allowed (the example in `Attack path`); receiving less tokens than you should. 

This problem is especially pronounced with currency tokens with different decimals. As per the documentation, `USDC` and `USDT` will be used. Both tokens have 6 decimals precision.

### PoC

Change the price of the launch token to 2 and run the following test in `Launch.UpdateParticipation.t.sol`

```solidity
    function test_UpdateParticipation_DecreaseAmount() public {
        // Prepare update participation request
        UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(750);

        bytes memory updateSignature = _signRequest(abi.encode(updateRequest));
        uint256 initialCurrencyBalance = currency.balanceOf(user1);

        // Expect ParticipationUpdated event
        vm.expectEmit();
        emit ParticipationUpdated(
            updateRequest.launchGroupId,
            updateRequest.newLaunchParticipationId,
            testUserId,
            user1,
            updateRequest.tokenAmount,
            address(currency)
        );

        vm.startPrank(user1);
        // Update participation
        launch.updateParticipation(updateRequest, updateSignature);

        // Verify update
        ParticipationInfo memory newInfo = launch.getParticipationInfo(updateRequest.newLaunchParticipationId);

        // token amounts differ
        assertEq(
            newInfo.tokenAmount, launch.getUserTokensByLaunchGroup(updateRequest.launchGroupId, updateRequest.userId)
        );
        vm.stopPrank();
    }
```

### Mitigation

```diff
     if (prevInfo.currencyAmount > newCurrencyAmount) {
+          uint256 newTokenAmount = userTokenAmount - (prevInfo.tokenAmount - request.tokenAmount);
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
-            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
+            if (newTokenAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
-            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
+            userTokens.set(request.userId, newTokenAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
+          uint256 newTokenAmount = userTokenAmount + (request.tokenAmount - prevInfo.tokenAmount);
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
-            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
+            if (newTokenAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
-            userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
+            userTokens.set(request.userId, newTokenAmount);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }
```