Shallow Coral Griffin

Medium

# Incorrect update of _userTokensByLaunchGroup in updateParticipation allows bypassing user max/min limits

### Summary

The `_userTokensByLaunchGroup` is incorrectly updated in updateParticipation. Since `_userTokensByLaunchGroup` can be set higher or lower than the actual value, it can be used to bypass `maxTokenAmountPerUser` and `minTokenAmountPerUser` limits.


### Root Cause

In [Launch.sol#L361](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L361) and [Launch.sol#L374](https://github.com/sherlock-audit/2025-02-rova/blob/53fb6d71d253676bfbd00926e8f217f40c62d8c5/rova-contracts/src/Launch.sol#L374), `_userTokensByLaunchGroup` is updated with incorrect values. It updates using `userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount)` or `userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount)`, where `userTokenAmount` is the amount of launch tokens requested by the user, and `refundCurrencyAmount` is the amount of payment tokens to be refunded, not the launch token request amount. `additionalCurrencyAmount` is the amount of additional tokens the user must pay. 


### Internal Pre-conditions

1. User calls `participate` to join a launch group
2. The difference becomes larger when the launch token and the payment token have different decimals or are significantly different in price, and when `tokenPriceBps` is changed. 


### External Pre-conditions

None

### Attack Path

1. User calls `updateParticipation` to adjust the requested launch token amount


### Impact

`_userTokensByLaunchGroup` is set to incorrect values. Since it can be set higher or lower than the actual value, users can bypass `maxTokenAmountPerUser` and `minTokenAmountPerUser` limits.


### PoC

You can run it by adding it in Launch.UpdateParticipation.t.sol 

```solidity
function test_PoC_UpdateParticipation() public {

    // Update tokenPriceBps
    vm.startPrank(manager);
    launch.setLaunchGroupCurrency(
        testLaunchGroupId, address(currency), CurrencyConfig({tokenPriceBps: (1 * 10 ** currency.decimals()) / 2, isEnabled: true}) // decrease tokenPriceBps
    );
    vm.stopPrank();
    
    uint256 tokensSold = launch.getUserTokensByLaunchGroup(testLaunchGroupId, testUserId);
    assertEq(tokensSold, 1000 * 10 ** launch.tokenDecimals());

    uint256 newTokenAmount = 1000; // new token amount is the same but tokenPriceBps is decreased
    uint256 newTokenAmountWithDecimals = newTokenAmount * 10 ** launch.tokenDecimals();
    // Prepare update participation request
    UpdateParticipationRequest memory updateRequest = _createUpdateParticipationRequest(newTokenAmount);
    bytes memory updateSignature = _signRequest(abi.encode(updateRequest));

    vm.startPrank(user1);
    uint256 updatedCurrencyAmount =
        _getCurrencyAmount(updateRequest.launchGroupId, updateRequest.currency, updateRequest.tokenAmount);
    currency.approve(address(launch), updatedCurrencyAmount);

    // Update participation
    launch.updateParticipation(updateRequest, updateSignature);

    // Verify update
    ParticipationInfo memory newInfo = launch.getParticipationInfo(updateRequest.newLaunchParticipationId);
    assertEq(newInfo.tokenAmount, newTokenAmountWithDecimals);

    tokensSold = launch.getUserTokensByLaunchGroup(testLaunchGroupId, testUserId);
    assertLt(tokensSold, newTokenAmountWithDecimals);

    // user can buy more than maxTokenAmountPerUser because _userTokensByLaunchGroup is less than real value

    vm.stopPrank();
}
```

### Mitigation

Change to `userTokens.set(request.userId, userTokenAmount - prevInfo.tokenAmount + request.tokenAmount);`